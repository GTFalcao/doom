
actor BishopClass : MonsterPawnStrife
{
	player.morphweapon "BishopWeapon"
	health 500
	player.maxhealth 500
	painchance 170
	mass 1000
	speed 0.5
	bloodtype "StrifeSpark1"
	player.soundclass "mldr"
	states
	{
		Spawn:
			MLDR A -1
			stop
		See:
			MLDR B 6
			MLDR C 6 //A_PlaySound("crusader/pain",CHAN_BODY)
			MLDR D 6
			MLDR A 6 //A_PlaySound("crusader/pain",CHAN_BODY)
			loop
		Pain:
			MLDR A 1 A_PlaySound("crusader/pain",chan_voice)
			goto Spawn
		Missile:
			MLDR F 5
			goto Spawn
		Melee:
			MLDR E 3 bright
			goto Missile
		Death:
			MLDR G 3 Bright A_ChangeFlag("NOBLOOD",1)
			MLDR H 5 Bright A_PlayerScream
			MLDR I 4 Bright A_TossGib
			MLDR J 4 Bright
			MLDR KL 3 Bright
			MLDR M 4 Bright A_NoBlocking
			MLDR N 4 Bright
			MLDR O 4 Bright A_TossGib
			MLDR P 4 Bright
			MLDR Q 4 Bright A_TossGib
			MLDR R 4 Bright
			MLDR S 4 Bright A_TossGib
			MLDR T 4 Bright
			MLDR U 4 Bright A_TossGib
			MLDR V -1 Bright
			stop
	}
}

actor BishopClassHitbox : BishopClass
{
	height 110
	radius 30
	player.viewheight 100
	player.attackzoffset 40
}

actor BishopWeapon : BaseMonsterWeapon
{
	obituary "%o was blown away by %k."
	weapon.sisterweapon "BishopWeaponPowered"
	tag "Homing missile launcher"
	weapon.ammotype1 "MonsterManaBlue"
	+WEAPON.DONTBOB
	states
	{
		Select:
			WPS2 A 1 A_Raise
			wait
		Deselect:
			WPS2 A 1 A_Lower
			wait
		Ready:
			WPS2 A 1 A_WeaponReady
			wait
		Fire:
			WPS2 B 3 SetPlayerProperty(0,1,0)
			WPS2 C 0 A_GunFlash
			WPS2 C 0 A_TakeInventory("MonsterManaBlue",4)
			WPS2 C 0 A_JumpIfTargetInLOS("Homing",0,JLOSF_ALLYNOJUMP)
			WPS2 C 3 bright A_FireCustomMissile("BishopMissile02",0,0,0,9)
			WPS2 B 3
			WPS2 A 37 SetPlayerProperty(0,0,0)
			WPS2 A 92 A_JumpIfInventory("MonsterManaBlue",4,"Ready")
			goto Ready
		Homing:
			WPS2 C 3 bright A_FireCustomMissile("BishopMissile0",0,0,0,9)
			WPS2 B 3
			WPS2 A 37 SetPlayerProperty(0,0,0)
			WPS2 A 92 A_JumpIfInventory("MonsterManaBlue",4,"Ready")
			goto Ready
	}
}

actor BishopWeaponPowered : BishopWeapon
{
	weapon.sisterweapon "BishopWeapon"
	+WEAPON.POWERED_UP
	states
	{
		Fire:
			WPS2 B 3 SetPlayerProperty(0,1,0)
			WPS2 C 0 A_GunFlash
			WPS2 C 0 A_TakeInventory("MonsterManaBlue",4)
			WPS2 C 0 A_JumpIfTargetInLOS("Homing",0,JLOSF_ALLYNOJUMP)
			WPS2 C 3 bright A_FireCustomMissile("BishopMissile02",0,0,0,9)
			WPS2 B 3
			WPS2 C 0 A_GunFlash
			WPS2 C 0 A_TakeInventory("MonsterManaBlue",4)
			WPS2 C 0 A_JumpIfTargetInLOS("Homing2",0,JLOSF_ALLYNOJUMP)
			WPS2 C 3 bright A_FireCustomMissile("BishopMissile02",0,0,0,9)
			WPS2 B 3
			WPS2 C 0 A_GunFlash
			WPS2 C 0 A_TakeInventory("MonsterManaBlue",4)
			WPS2 C 0 A_JumpIfTargetInLOS("Homing3",0,JLOSF_ALLYNOJUMP)
			WPS2 C 3 bright A_FireCustomMissile("BishopMissile02",0,0,0,9)
			WPS2 B 3
			WPS2 A 45 SetPlayerProperty(0,0,0)
			WPS2 A 132 A_JumpIfInventory("MonsterManaBlue",12,"Ready")
			goto Ready
		Homing:
			WPS2 C 3 bright A_FireCustomMissile("BishopMissile0",0,0,0,9)
			goto Fire+5
		Homing2:
			WPS2 C 3 bright A_FireCustomMissile("BishopMissile0",0,0,0,9)
			goto Fire+10
		Homing3:
			WPS2 C 3 bright A_FireCustomMissile("BishopMissile0",0,0,0,9)
			goto Fire+15
	}
}

actor BishopMissile0 : BishopMissile
{
	damage (10 * random(1,4) * ACS_ExecuteWithResult(455,5) / 100)
	-STRIFEDAMAGE
	+MTHRUSPECIES
	states
	{
		Death:
			SMIS A 0 A_SetTranslucent(1, 1)
			SMIS A 5 Bright A_Explode(64 * ACS_ExecuteWithResult(455,5) / 100, 64, 0)
			SMIS B 5 Bright
			SMIS C 4 Bright
			SMIS DEFG 2 Bright
			stop
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,85 * ACS_ExecuteWithResult(455,4) / 10)
			goto Strife1
		Strife1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Strife4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Strife2")
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3])
			goto Death
		Strife2:
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 2)
			goto Death
		Strife4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Strife8")
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 4)
			goto Death
		Strife8:
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 8)
			goto Death
	}
}

actor BishopMissile02 : BishopMissile0 { -SEEKERMISSILE }

actor BishopMorph : MonsterMorph
{
	tag "Bishop"
	inventory.icon "ARTIMLDR"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("BishopHitboxCheck",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(450,0,2,3)
			stop
	}
}

actor BishopMorphDirect : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,2,3)
			stop
	}
}

actor BishopHitboxCheck : BaseHitboxCheck
{
	height 110
	radius 30
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_GiveToTarget("BishopMorphDirect",1)
			stop
	}
}

actor BishopMorpher : MonsterMorpher { powerup.type MorphMldr }

actor PowerMorphMldr : PowerMorphStrife { powermorph.playerclass "BishopClass" }

actor BishopMorpherHitbox : MonsterMorpher { powerup.type MorphMldrHitbox }

actor PowerMorphMldrHitbox : PowerMorphStrife { powermorph.playerclass "BishopClassHitbox" }