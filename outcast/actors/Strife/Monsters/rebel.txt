
actor RebelClass : MonsterPawnStrife
{
	player.morphweapon "RebelWeapon"
	health 60
	player.maxhealth 60
	gibhealth 30
	player.soundclass "rebel"
	states
	{
		Spawn:
			HMN1 P 5 A_Jump(20,"Look1","Look2")
			loop
		Look1:
			HMN1 Q 8
			HMN1 Q 8 A_Jump(128,"Spawn")
			goto Spawn
		Look2:
			HMN1 R 8	
			HMN1 R 8 A_Jump(128,"Spawn")
			goto Spawn
		See:
			HMN1 ABCD 6
			loop
		Pain:
			HMN1 O 6 A_PlaySound("rebel/pain",2)
			goto Spawn
		Missile:
			HMN1 E 22
			goto Spawn
		Melee:
			HMN1 F 10
			goto Missile
		Death:
    		HMN1 G 5
    		HMN1 H 5 A_PlayerScream
    		HMN1 I 4 A_NoBlocking
    		HMN1 JKLM 4
    		HMN1 N -1
    		Stop
  		XDeath:
    		RGIB A 4 A_TossGib
    		RGIB B 4 A_PlaySound("strifegib",2)
    		RGIB C 3 A_NoBlocking
    		RGIB DEF 3 A_TossGib
			RGIB G 3
    		RGIB H 1400
			RGIB H 10 A_CheckPlayerDone
			wait
		Burn:
			BURN A 3 Bright A_PlaySound("human/imonfire",2)
			BURN BC 3 Bright A_SpawnItemEx("FireDroplet",0,0,24,frandom(-1.0,1.0),frandom(-1.0,1.0),frandom(0.1,1.0),0,32)
			BURN D 3 Bright A_NoBlocking
			BURN E 5 Bright A_SpawnItemEx("FireDroplet",0,0,24,frandom(-1.0,1.0),frandom(-1.0,1.0),frandom(0.1,1.0),0,32)
			BURN FGH 5 Bright
			BURN IJ 5 Bright A_SpawnItemEx("FireDroplet",0,0,24,frandom(-1.0,1.0),frandom(-1.0,1.0),frandom(0.1,1.0),0,32)
			BURN KL 5 Bright
			BURN MN 5 Bright A_SpawnItemEx("FireDroplet",0,0,24,frandom(-1.0,1.0),frandom(-1.0,1.0),frandom(0.1,1.0),0,32)
			BURN OPQPQ 5 Bright
			BURN RSTU 7 Bright
			BURN V -1
			stop
		Disintegrate:
			DISR A 5 A_PlaySound("misc/disruptordeath",2)
			DISR BC 5
			DISR D 5 A_NoBlocking
			DISR EF 5
			DISR GHIJ 4
			MEAT D 700
			MEAT D 10 A_CheckPlayerDone
			wait
	}
}

actor RebelWeapon : BaseMonsterWeapon
{
	obituary "%o was gunned down by %k."
	tag "Assault Rifle"
	weapon.sisterweapon "RebelWeaponPowered"
	weapon.ammotype1 "MonsterManaBlue"
	states
	{
		Ready:
			WPS0 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"LaserReady")
			WPS0 AAAA 1 A_WeaponReady
			loop
		LaserReady:
			WPS0 C 0 A_SpawnItemEx("AssaultGunPoweredLaser",0,0,41,512 * cos(angle),512 * sin(angle),512 * sin(-pitch),0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE|SXF_ABSOLUTEVELOCITY)
			WPS0 C 1 A_WeaponReady
			loop
		Deselect:
			WPS0 A 1 A_Lower
			wait
		Select:
			WPS0 A 1 A_Raise
			wait
		Fire:
			WPS0 BC 5
		Hold:
			WPS0 F 0 A_GunFlash
			WPS0 F 0 A_TakeInventory("MonsterManaBlue",1)
			WPS0 F 0 A_PlaySound("monsters/rifle",CHAN_WEAPON)
			WPS0 F 10 bright A_FireCustomMissile("StandardBulletStrife",random(-6,6),0,0,9)
			WPS0 F 0 A_PlaySound("monsters/rifle",CHAN_WEAPON)
			WPS0 E 5 bright A_FireCustomMissile("StandardBulletStrife",random(-6,6),0,0,9)
			WPS0 D 5 bright
			WPS0 C 60 A_JumpIfInventory("MonsterManaBlue",1,1)
			WPS0 C 10
			WPS0 B 5 A_ReFire
			goto Ready
	}
}

actor RebelWeaponPowered : RebelWeapon
{
	weapon.sisterweapon "RebelWeapon"
	+WEAPON.POWERED_UP
	states
	{
		Fire:
		Hold:
			WPS0 F 0 A_GunFlash
			WPS0 F 0 A_TakeInventory("MonsterManaBlue",1)
			WPS0 F 0 A_PlaySound("monsters/rifle",CHAN_WEAPON)
			WPS0 F 0 A_FireCustomMissile("StandardBulletStrifePowered",0,0,0,9)
			WPS0 FFFFFFFFFF 1 bright A_SpawnItemEx("AssaultGunPoweredLaser",0,0,41,512 * cos(angle),512 * sin(angle),512 * sin(-pitch),0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE|SXF_ABSOLUTEVELOCITY)
			WPS0 F 0 A_TakeInventory("MonsterManaBlue",1)
			WPS0 F 0 A_PlaySound("monsters/rifle",CHAN_WEAPON)
			WPS0 E 0 A_FireCustomMissile("StandardBulletStrifePowered",0,0,0,9)
			WPS0 EEEEE 1 bright A_SpawnItemEx("AssaultGunPoweredLaser",0,0,41,512 * cos(angle),512 * sin(angle),512 * sin(-pitch),0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE|SXF_ABSOLUTEVELOCITY)
			WPS0 DDDDD 1 bright A_SpawnItemEx("AssaultGunPoweredLaser",0,0,41,512 * cos(angle),512 * sin(angle),512 * sin(-pitch),0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE|SXF_ABSOLUTEVELOCITY)
			WPS0 C 0 A_JumpIfInventory("MonsterManaBlue",1,"Finish")
			goto DelayLaser
		Finish:
			WPS0 CCCCCCCCCC 1 A_SpawnItemEx("AssaultGunPoweredLaser",0,0,41,512 * cos(angle),512 * sin(angle),512 * sin(-pitch),0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE|SXF_ABSOLUTEVELOCITY)
			WPS0 C 0 A_ReFire
			WPS0 CCCCC 1 A_SpawnItemEx("AssaultGunPoweredLaser",0,0,41,512 * cos(angle),512 * sin(angle),512 * sin(-pitch),0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE|SXF_ABSOLUTEVELOCITY)
			goto Ready
		DelayLaser:
			WPS0 CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC 1 A_SpawnItemEx("AssaultGunPoweredLaser",0,0,41,512 * cos(angle),512 * sin(angle),512 * sin(-pitch),0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE|SXF_ABSOLUTEVELOCITY)
			goto Finish
	}
}

actor AssaultGunPoweredLaser : FastProjectile
{
	speed 512
	radius 1
	height 1
	renderstyle add
	+CLIENTSIDEONLY
	+NODAMAGETHRUST
	+PAINLESS
	+MTHRUSPECIES
	+DONTREFLECT
	states
	{
		Spawn:
			TNT1 A 4
			loop
		Death:
			TNT1 A 1 A_SpawnItemEx("AssaultGunPoweredLaser2",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY)
			stop
	}
}

actor AssaultGunPoweredLaser2
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	+NOBLOCKMAP
	renderstyle add
	states
	{
		Spawn:
			SHT1 C 2 bright
			stop
	}
}

actor StrifePuff0 : BulletPuff0
{
	states
	{
		Spawn:
			PUFY A 4 bright
			PUFY BCD 4
			stop
	}
}

actor StandardBulletStrife : StandardBullet
{
	states
	{
		Spawn:
			TNT1 A 4
			loop
		Death:
			TNT1 A 1 A_SpawnItem("StrifePuff0")
			stop
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,9 * ACS_ExecuteWithResult(455,4) / 10)
			goto Strife1
		Strife1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Strife4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Strife2")
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3])
			goto DefDeath
		Strife2:
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 2)
			goto DefDeath
		Strife4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Strife8")
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 4)
			goto DefDeath
		Strife8:
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 8)
			goto DefDeath
		DefDeath:
			TNT1 A 1
			stop
	}
}

actor StandardBulletStrifePowered : StandardBulletStrife
{
	damage (5 * random(1,5) * ACS_ExecuteWithResult(455,5) / 100)
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,15 * ACS_ExecuteWithResult(455,4) / 10)
			goto Strife1
	}
}

actor RebelMorph : MonsterMorph
{
	tag "Front Rebel"
	inventory.icon "ARTIHMN1"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("RebelHitboxCheck",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(450,0,1,0)
			stop
	}
}

actor RebelMorphDirect : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,1,0)
			stop
	}
}

actor RebelHitboxCheck : BaseHitboxCheck
{
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_GiveToTarget("RebelMorphDirect",1)
			stop
	}
}

actor RebelMorpher : MonsterMorpher { powerup.type MorphHmn1 }

actor PowerMorphHmn1 : PowerMorphStrife { powermorph.playerclass "RebelClass" }