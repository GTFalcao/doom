
actor Spectre2Class : MonsterPawnStrife
{
	player.morphweapon "Spectre2Weapon"
	health 1200
	mass 2000
	player.maxhealth 1200
	painchance 64
	speed 0.3
	bloodtype "HereticNoBlood"
	player.soundclass "alien"
	states
	{
		Spawn:
			ALN1 AB 10 bright
			loop
		See:
			ALN1 ABCDEFGHIJK 4 bright
			loop
		Pain:
			ALN1 J 2 A_PlaySound("alienspectre/pain",2)
			goto Spawn
		Missile:
			ALN1 JIH 4 bright
			goto Spawn
		Death:	
			AL1P A 6 Bright A_SpawnItemEx("AlienChunkSmall",0,0,10,random(-7,8),random(-7,8),random(0,15),0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY)
			AL1P B 6 Bright A_PlaySound("alienspectre/death",CHAN_VOICE,1.0,0,ATTN_NONE)
			AL1P C 6 Bright A_SpawnItemEx("AlienChunkSmall",0,0,10,random(-7,8),random(-7,8),random(0,15),0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY)
			AL1P DE 6 Bright
			AL1P F 6 Bright A_SpawnItemEx("AlienChunkSmall",0,0,10,random(-7,8),random(-7,8),random(0,15),0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY)
			AL1P G 6 Bright
			AL1P H 6 Bright A_SpawnItemEx("AlienChunkSmall",0,0,10,random(-7,8),random(-7,8),random(0,15),0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY)
			AL1P IJK 6 Bright
			AL1P LM 5 Bright
			AL1P N 5 Bright A_SpawnItemEx("AlienChunkLarge",0,0,10,random(-7,8),random(-7,8),random(0,15),0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY)
			AL1P OPQR 5 Bright
			FDMN T 10 A_CheckPlayerDone
			wait
	}
}

actor Spectre2ClassHitbox : Spectre2Class
{
	height 84
	radius 68
	player.viewheight 56
}

actor SpectreMeleeProj : TemplarMeleeProj
{
	obituary "%o was torn apart by spectre %k."
	damage (28 * random(1,4) * ACS_ExecuteWithResult(455,5) / 100)
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,7 * ACS_ExecuteWithResult(455,4))
			goto Strife1
	}
}

actor SpectreMeleeProj2 : TemplarMeleeProj2
{
	obituary "%o was torn apart by spectre %k."
	damage (56 * random(1,4) * ACS_ExecuteWithResult(455,5) / 100)
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,14 * ACS_ExecuteWithResult(455,4))
			goto Super::XDeath+1
	}
}

actor SpectreMeleeProj2NoSound : SpectreMeleeProj2
{
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,14 * ACS_ExecuteWithResult(455,4))
			goto TemplarMeleeProj2::XDeath+2
	}
}

actor Spectre2Weapon : BaseMonsterWeaponFlight
{
	obituary "%o was struck down by spectre %k."
	weapon.sisterweapon "Spectre2WeaponPowered"
	weapon.ammotype1 "MonsterManaBlue"
	weapon.ammotype2 "MonsterManaGreen"
	tag "Sigil 2"
	+WEAPON.NOALERT
	states
	{
		Ready:
			TNT1 A 10
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,1) == 0,2)
			TNT1 A 0 A_GiveInventory("MonFlight",1)
			TNT1 A 0 A_GiveInventory("Aln1Shadow",1)
			TNT1 A 1 A_WeaponReady
			wait
		Fire:
			TNT1 A 4
			TNT1 A 4 A_AlertMonsters
			TNT1 A 0 A_TakeInventory("MonsterManaBlue",4)
			TNT1 A 0 A_TakeInventory("MonsterManaGreen",4)
			TNT1 A 16 A_FireCustomMissile("SpectralLightningH30",0,0,0,9)
			TNT1 A 80 A_JumpIfInventory("MonsterManaBlue",4,2)
			TNT1 A 16
			goto Ready+4
			TNT1 A 80 A_JumpIfInventory("MonsterManaGreen",4,1)
			TNT1 A 16
			goto Ready+4
		AltFire:
			TNT1 A 4 A_PlaySound("reaver/blade",CHAN_WEAPON)
			TNT1 A 4
			TNT1 A 0 A_FireCustomMissile("SpectreMeleeProj",0,0,0,9)
			TNT1 A 8 A_JumpIfInventory("BerserkTracker",1,1)
			goto Ready+4
			TNT1 A 8 A_FireCustomMissile("SpectreMeleeProj",0,0,0,9)
			goto Ready+4
	}
}

actor Spectre2WeaponPowered : Spectre2Weapon
{
	tag "Sigil 2 powered"
	weapon.sisterweapon "Spectre2Weapon"
	+WEAPON.POWERED_UP
	states
	{
		Fire:
			TNT1 A 4
			TNT1 A 4 A_AlertMonsters
			TNT1 A 0 A_TakeInventory("MonsterManaBlue",8)
			TNT1 A 0 A_TakeInventory("MonsterManaGreen",8)
			TNT1 A 0 A_FireCustomMissile("SpectralLightningH33",0,0,-24,9)
			TNT1 A 16 A_FireCustomMissile("SpectralLightningH34",0,0,24,9)
			TNT1 A 80 A_JumpIfInventory("MonsterManaBlue",8,2)
			TNT1 A 16
			goto Ready+4
			TNT1 A 80 A_JumpIfInventory("MonsterManaGreen",8,1)
			TNT1 A 16
			goto Ready+4
		AltFire:
			TNT1 A 4 A_PlaySound("reaver/blade",CHAN_WEAPON)
			TNT1 A 4
			TNT1 A 0 A_FireCustomMissile("SpectreMeleeProj2",0,0,0,9)
			TNT1 A 8 A_JumpIfInventory("BerserkTracker",1,1)
			goto Ready+4
			TNT1 A 8 A_FireCustomMissile("SpectreMeleeProj2NoSound",0,0,0,9)
			goto Ready+4
	}
}

actor SpectralLightningH30 : SpectralLightningH3
{
	obituary "%o was struck down by spectre %k."
	damage (40 * random(1,4) * ACS_ExecuteWithResult(455,5) / 100)
	reactiontime 1024
	-STRIFEDAMAGE
	-SPECTRAL
	+MTHRUSPECIES
	states
	{
		Spawn:
			ZAP6 A 4 bright A_Countdown
			ZAP6 BC 4 Bright A_SpawnItemEx("SpectralLightningHTail0",0,0,0,0,0,0,0,SXF_ABSOLUTEVELOCITY|SXF_CLIENTSIDE)
			loop
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,16 * ACS_ExecuteWithResult(455,4))
			goto Strife1
		Strife1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Strife4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Strife2")
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3])
			goto Death
		Strife2:
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 2)
			goto Death
		Strife4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Strife8")
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 4)
			goto Death
		Strife8:
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 8)
			goto Death
		Death:
			ZAP1 B 3 bright A_Explode(64 * ACS_ExecuteWithResult(455,5) / 100, 32, 0)
			ZAP1 ABCDEFE 3 bright
			ZAP1 DCB 2 bright
			ZAP1 A 1 bright
			stop
	}
}

actor SpectralLightningHTail0
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	renderstyle add
	states
	{
		Spawn:
			ZAP6 ABC 5 bright
			stop
	}
}

actor SpectralLightningH33 : SpectralLightningH30
{
	+DONTREFLECT
	+FOILINVUL
	weaveindexxy 16
	states
	{
		Spawn:
			ZAP6 AAAA 1 bright A_Weave(2,0,3.0,0)
			ZAP6 B 0 A_SpectralLightningTail
			ZAP6 BBBB 1 bright A_Weave(2,0,3.0,0)
			ZAP6 C 0 A_SpectralLightningTail
			ZAP6 CCCC 1 bright A_Weave(2,0,3.0,0)
			loop
	}
}

actor SpectralLightningH34 : SpectralLightningH33
{
	states
	{
		Spawn:
			ZAP6 AAAA 1 bright A_Weave(2,0,-3.0,0)
			ZAP6 B 0 A_SpectralLightningTail
			ZAP6 BBBB 1 bright A_Weave(2,0,-3.0,0)
			ZAP6 C 0 A_SpectralLightningTail
			ZAP6 CCCC 1 bright A_Weave(2,0,-3.0,0)
			loop
	}
}

actor Spectre2Morph : MonsterMorph
{
	tag "Bishop's Spectre"
	inventory.icon "ARTIALN1"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("Spectre2HitboxCheck",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(450,0,3,3)
			stop
	}
}

actor Spectre2MorphDirect : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,3,3)
			stop
	}
}

actor Spectre2HitboxCheck : BaseHitboxCheck
{
	height 84
	radius 68
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_GiveToTarget("Spectre2MorphDirect",1)
			stop
	}
}

actor Spectre2Morpher : MonsterMorpher { powerup.type MorphAln2 }

actor PowerMorphAln2 : PowerMorphStrife { powermorph.playerclass "Spectre2Class" }

actor Spectre2MorpherHitbox : MonsterMorpher { powerup.type MorphAln2Hitbox }

actor PowerMorphAln2Hitbox : PowerMorphStrife { powermorph.playerclass "Spectre2ClassHitbox" }