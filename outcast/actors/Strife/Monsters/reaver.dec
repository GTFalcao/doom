
actor ReaverClass : MonsterPawnStrife
{
	player.morphweapon "ReaverWeapon"
	health 150
	player.maxhealth 150
	mass 400
	bloodtype "StrifeSpark1"
	player.soundclass "reav"
	states
	{
		Spawn:
			ROB1 A -1
			stop
		See:
			ROB1 BCED 6
			loop
		Pain:
			ROB1 A 4 A_PlaySound("reaver/pain",2)
			goto Spawn
		Missile:
			ROB1 H 8
			ROB1 I 10
			goto Spawn
		Melee:
			ROB1 F 8 bright
			ROB1 G 11 bright
			goto Spawn
		Death:
			ROB1 J 6
			ROB1 K 6 A_PlayerScream
			ROB1 L 5
			ROB1 M 5 A_NoBlocking
			ROB1 NOP 5
			ROB1 Q 6 A_Explode(32, 32)
			ROB1 R -1
			stop
		XDeath:
			ROB1 L 0 A_ChangeFlag("NOBLOOD",1)
			ROB1 L 5 A_TossGib
			ROB1 M 5 A_PlayerScream
			ROB1 N 5 A_TossGib
			ROB1 O 0 A_NoBlocking
			ROB1 OP 5 A_TossGib
			ROB1 Q 6 A_Explode(32, 32)
			ROB1 R -1
			stop
	}
}

actor ReaverBullet0 : StandardBulletStrife
{
	obituary "%o was shot down by %k."
	damage (6 * random(1,4) * ACS_ExecuteWithResult(455,5) / 100)
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,15 * ACS_ExecuteWithResult(455,4) / 10)
			goto Strife1
	}
}

actor ReaverMeleeProj : TemplarMeleeProj
{
	obituary "%o was sliced open by %k."
	damage (12 * random(1,4) * ACS_ExecuteWithResult(455,5) / 100)
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,3 * ACS_ExecuteWithResult(455,4))
			goto Strife1
	}
}

actor ReaverMeleeProj2 : TemplarMeleeProj2
{
	obituary "%o was sliced open by %k."
	damage (24 * random(1,4) * ACS_ExecuteWithResult(455,5) / 100)
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,6 * ACS_ExecuteWithResult(455,4))
			goto Super::XDeath+1
	}
}

actor ReaverMeleeProj2NoSound : ReaverMeleeProj2
{
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,6 * ACS_ExecuteWithResult(455,4))
			goto TemplarMeleeProj2::XDeath+2
	}
}

actor ReaverWeapon : BaseMonsterWeapon
{
	tag "Robo-Shotgun and slicer blade"
	weapon.sisterweapon "ReaverWeaponPowered"
	weapon.ammotype1 "MonsterManaGreen"
	+WEAPON.NOALERT
	states
	{
		Select:
			WPS2 D 1 A_Raise
			wait
		Deselect:
			WPS2 D 1 A_Lower
			wait
		Ready:
			WPS2 D 1 A_WeaponReady
			wait
		Fire:
			WPS2 E 2 A_GunFlash
			WPS2 F 2 A_AlertMonsters
			goto Hold+2
		Hold:
			WPS2 N 1 offset(0,72) A_GunFlash
			WPS2 N 1 offset(0,64)
			WPS2 N 1 offset(0,48)
			WPS2 N 1 offset(0,40) A_AlertMonsters
			WPS2 M 0 offset(0,32) A_TakeInventory("MonsterManaGreen",2)
			WPS2 MMM 0 A_FireCustomMissile("ReaverBullet0",random(-11,11),0,0,9)
			WPS2 M 3 bright A_PlaySound("reaver/attack",CHAN_WEAPON)
			WPS2 G 3 bright
			WPS2 NH 4
			WPS2 H 1 offset(0,38)
			WPS2 H 1 offset(0,45)
			WPS2 H 1 offset(0,52)
			WPS2 H 1 offset(0,59)
			WPS2 H 1 offset(0,66)
			WPS2 H 1 offset(0,73)
			WPS2 H 1 offset(0,80)
			TNT1 A 7 offset(0,32)
			TNT1 A 64 A_JumpIfInventory("MonsterManaGreen",2,1)
			TNT1 A 1 A_ReFire
		RaiseGun:
			WPS2 D 1 offset(0,80)
			WPS2 D 1 offset(0,72)
			WPS2 D 1 offset(0,64)
			WPS2 D 1 offset(0,56)
			WPS2 D 1 offset(0,48)
			WPS2 D 1 offset(0,40)
			goto Ready
		AltFire:
			WPS2 I 4 A_PlaySound("reaver/blade",CHAN_WEAPON)
			goto AltHold+1
		AltHold:
			WPS2 L 4 A_PlaySound("reaver/blade",CHAN_WEAPON)
			WPS2 J 4
			WPS2 K 0 A_FireCustomMissile("ReaverMeleeProj",0,0,0,9)
			WPS2 K 8 A_JumpIfInventory("BerserkTracker",1,1)
			goto DoubleHit+1
		DoubleHit:
			WPS2 K 8 A_FireCustomMissile("ReaverMeleeProj",0,0,0,9)
			WPS2 K 0 A_ReFire
			WPS2 K 1 offset(0,41)
			WPS2 K 1 offset(0,50)
			WPS2 K 1 offset(0,59)
			WPS2 K 1 offset(0,68)
			WPS2 K 1 offset(0,77)
			WPS2 K 1 offset(0,86)
			TNT1 A 8 offset(0,32)
			goto RaiseGun
	}
}

actor ReaverWeaponPowered : ReaverWeapon
{
	tag "Robo-Super Shotgun and slicer blade"
	weapon.sisterweapon "ReaverWeapon"
	+WEAPON.POWERED_UP
	states
	{
		Fire:
			WPS2 E 2 A_GunFlash
			WPS2 F 2 A_AlertMonsters
			goto Hold+2
		Hold:
			WPS2 N 1 offset(0,72) A_GunFlash
			WPS2 N 1 offset(0,64)
			WPS2 N 1 offset(0,48)
			WPS2 N 1 offset(0,40) A_AlertMonsters
			WPS2 M 0 offset(0,32) A_PlaySound("reaver/attack2",CHAN_WEAPON)
			WPS2 M 0 A_TakeInventory("MonsterManaGreen",10)
			WPS2 MMMMMMMMMMMMMM 0 A_FireCustomMissile("ReaverBullet0",random(-11,11),0,0,9,0,random(-7,7))
			WPS2 M 3 bright A_FireCustomMissile("ReaverBullet0",random(-11,11),0,0,9,0,random(-7,7))
			WPS2 G 3 bright
			WPS2 NH 4
			WPS2 H 1 offset(0,38)
			WPS2 H 1 offset(0,45)
			WPS2 H 1 offset(0,52)
			WPS2 H 1 offset(0,59)
			WPS2 H 1 offset(0,66)
			WPS2 H 1 offset(0,73)
			WPS2 H 1 offset(0,80)
			TNT1 A 45 offset(0,32)
			TNT1 A 140 A_JumpIfInventory("MonsterManaGreen",8,1)
			TNT1 A 1 A_ReFire
			goto RaiseGun
		AltFire:
			WPS2 I 4 A_PlaySound("reaver/blade",CHAN_WEAPON)
			goto AltHold+1
		AltHold:
			WPS2 L 4 A_PlaySound("reaver/blade",CHAN_WEAPON)
			WPS2 J 4
			WPS2 K 0 A_FireCustomMissile("ReaverMeleeProj2",0,0,0,9)
			WPS2 K 8 A_JumpIfInventory("BerserkTracker",1,1)
			goto DoubleHit+1
		DoubleHit:
			WPS2 K 8 A_FireCustomMissile("ReaverMeleeProj2NoSound",0,0,0,9)
			WPS2 K 0 A_ReFire
			WPS2 K 1 offset(0,41)
			WPS2 K 1 offset(0,50)
			WPS2 K 1 offset(0,59)
			WPS2 K 1 offset(0,68)
			WPS2 K 1 offset(0,77)
			WPS2 K 1 offset(0,86)
			TNT1 A 8 offset(0,32)
			goto RaiseGun
	}
}

actor ReaverMorph : MonsterMorph
{
	tag "Reaver"
	inventory.icon "ARTIROB1"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("ReaverHitboxCheck",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(450,0,1,1)
			stop
	}
}

actor ReaverMorphDirect : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,1,1)
			stop
	}
}

actor ReaverHitboxCheck : BaseHitboxCheck
{
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_GiveToTarget("ReaverMorphDirect",1)
			stop
	}
}

actor ReaverMorpher : MonsterMorpher { powerup.type MorphRob1 }

actor PowerMorphRob1 : PowerMorphStrife { powermorph.playerclass "ReaverClass" }