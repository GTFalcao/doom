
actor StalkerSpiderClass : MonsterPawnStrife
{
	player.morphweapon "StalkerSpiderWeapon"
	health 80
	player.maxhealth 80
	painchance 0
	speed 1.2
	bloodtype "StrifeSpark1"
	player.soundclass "stlk"
	states
	{
		Spawn:
			STLK K -1 A_StopSound(CHAN_BODY)
			stop
		See:
			STLK J 5 A_PlaySound("stalker/walk",CHAN_BODY)
			STLK K 5
			STLK L 5 A_PlaySound("stalker/walk",CHAN_BODY)
			STLK K 5
			loop
		Missile:
			STLK J 4 
			STLK K 6
			goto Spawn
		Melee:
			STLK M 8 bright
			STLK N 10
			goto Spawn
		Death:
			STLK O 4
			STLK P 4 A_PlayerScream
			STLK QRST 4
			STLK U 4 A_NoBlocking
			STLK VW 4
			STLK "XYZ[" 4 bright
			JUNK K 10 A_CheckPlayerDone
			wait
	}
}

actor StalkerSpiderClassHitbox : StalkerSpiderClass
{
	height 25
	radius 31
	player.viewheight 16
	player.attackzoffset -6
}

actor StalkerSpiderWeapon : BaseMonsterWeapon
{
	obituary "%o was bugged by %k."
	tag "Robo-bite"
	weapon.sisterweapon "StalkerSpiderWeaponPowered"
	+WEAPON.MELEEWEAPON
	+WEAPON.DONTBOB
	states
	{
		Select:
			WPS2 O 1 A_Raise
			wait
		Deselect:
			WPS2 O 1 A_Lower
			wait
		Ready:
			WPS2 O 1 A_WeaponReady
			wait
		Fire:
			WPS2 O 3 offset(0,35) A_PlaySound("stalker/attack",CHAN_BODY)
			WPS2 P 0 A_FireCustomMissile("StalkerSpiderMeleeProj",0,0,0,9)
			WPS2 P 3 A_JumpIfInventory("BerserkTracker",1,1)
			goto Ready
			WPS2 P 3 A_FireCustomMissile("StalkerSpiderMeleeProj",0,0,0,9)
			goto Ready
	}
}

actor StalkerSpiderWeaponPowered : StalkerSpiderWeapon
{
	weapon.sisterweapon "StalkerSpiderWeapon"
	tag "Mouth cannon"
	+WEAPON.POWERED_UP
	states
	{
		Fire:
			WPS2 Q 0 A_GunFlash
			WPS2 QQQQQ 0 A_FireCustomMissile("StalkerSpiderBullet",random(-9,9),0,0,0,0,random(-9,9))
			WPS2 Q 3 bright A_PlaySound("reaver/attack",CHAN_BODY)
			WPS2 R 3 bright offset(0,35)
			WPS2 P 4 offset(0,32)
			WPS2 O 3 offset(0,35)
			WPS2 O 3 offset(0,32)
			goto Ready
	}
}

actor StalkerSpiderMeleeProj : TemplarMeleeProj
{
	obituary "%o was bugged by %k."
	damage (2 * random(1,8) * ACS_ExecuteWithResult(455,5) / 100)
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,9 * ACS_ExecuteWithResult(455,4) / 10)
			goto Strife1
	}
}

actor StalkerSpiderBullet : StandardBulletStrife
{
	obituary "%o was shot down by %k's mouth cannon."
	damage (3 * random(1,4) * ACS_ExecuteWithResult(455,5) / 100)
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,75 * ACS_ExecuteWithResult(455,4) / 100)
			goto Strife1
	}
}

actor ChooseStalker : MonsterMorph
{
	tag "Stalker"
	inventory.icon "ARTISTLK"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("StalkerSpiderHitboxCheck2",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(453,0,3,1)
			TNT1 A 0 ACS_ExecuteAlways(450,0,0,1)
			stop
	}
}

actor StalkerSpiderMorph : MonsterMorph
{
	tag "Stalker"
	inventory.icon "ARTISTLK"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("StalkerSpiderHitboxCheck",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(450,0,0,1)
			stop
	}
}

actor StalkerSpiderMorphDirect : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,0,1)
			stop
	}
}

actor StalkerSpiderHitboxCheck : BaseHitboxCheck
{
	height 25
	radius 31
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_GiveToTarget("StalkerSpiderMorphDirect",1)
			stop
	}
}

actor StalkerSpiderMorphDirect2 : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(453,0,3,1)
			TNT1 A 0 ACS_ExecuteAlways(450,0,0,1)
			stop
	}
}

actor StalkerSpiderHitboxCheck2 : BaseHitboxCheck
{
	height 25
	radius 31
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_GiveToTarget("StalkerSpiderMorphDirect2",1)
			stop
	}
}

actor StalkerSpiderMorpher : MonsterMorpher { powerup.type MorphStlk }

actor PowerMorphStlk : PowerMorphStrife { powermorph.playerclass "StalkerSpiderClass" }

actor StalkerSpiderMorpherHitbox : MonsterMorpher { powerup.type MorphStlkHitbox }

actor PowerMorphStlkHitbox : PowerMorphStrife { powermorph.playerclass "StalkerSpiderClassHitbox" }