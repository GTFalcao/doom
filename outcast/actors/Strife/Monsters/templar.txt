
actor TemplarClass : MonsterPawnStrife
{
	player.morphweapon "TemplarWeapon"
	health 300
	player.maxhealth 300
	painchance 170
	mass 500
	speed 0.8
	bloodtype "StrifeSpark1"
	player.soundclass "pgrd"
	states
	{
		Spawn:
			PGRD A 5 A_Jump(16,"Look1","Look2")
			loop
		Look1:
			PGRD B 10
			goto Spawn
		Look2:
			PGRD C 10
			goto Spawn
		See:
			PGRD ABCD 6
			loop
		Pain:
			PGRD A 4 A_PlaySound("templar/pain",2)
			goto Spawn
		Missile:
			PGRD A 11 A_JumpIfInventory("shooting",1,"Slice")
			goto Spawn
		Slice:
			PGRD E 10
			PGRD F 10
			PGRD F 2 A_JumpIfInventory("shooting",1,"Slice")
			goto Spawn
		Melee:
			PGRD G 8 bright
			PGRD H 8 bright
			goto Spawn
		Death:
			PGRD I 0 A_ChangeFlag("NOBLOOD",1)
    		PGRD I 4 A_TossGib
			PGRD J 4 A_PlayerScream
			PGRD K 4 A_TossGib
			PGRD L 4 A_NoBlocking
			PGRD MN 4
			PGRD O 4 A_TossGib
			PGRD "PQRSTUVWXYZ[" 4
			PGRD "\" -1
			stop
	}
}

actor StrifeSpark1
{
	+NOBLOCKMAP
	+NOINTERACTION
	renderstyle add
	alpha 0.5
	states
	{
		Spawn:
			POW3 ABCDEFGH 3 bright
			stop
	}
}

actor StrifeSparkShort : StrifeSpark1
{
	+CLIENTSIDEONLY
	states
	{
		Spawn:
			POW3 ABCDEFGH 2 bright
			stop
	}
}

actor TemplarMeleeProj : ImpMeleeProj
{
	obituary "%o was clawed by %k."
	damage (20 * random(1,4) * ACS_ExecuteWithResult(455,5) / 100)
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,5 * ACS_ExecuteWithResult(455,4))
			goto Strife1
	}
}

actor TemplarMeleeProj2 : TemplarMeleeProj
{
	-BLOODSPLATTER
	+EXTREMEDEATH
	damage (40 * random(1,4) * ACS_ExecuteWithResult(455,5) / 100)
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,10 * ACS_ExecuteWithResult(455,4))
			TNT1 A 0 A_PlaySound("0demon/ouchbite",chan_body,0.8)
			TNT1 A 0 A_SpawnItemEx("StrifeBloodClient",0,0,0,random(-5,5),random(-5,5),frandom(2.0,6.0),0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE,0)
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,6) > 0, 8) // sv_fastweapons, I'll spare your fps
			TNT1 AAAA 0 A_SpawnItemEx("StrifeBloodClient",0,0,0,random(-5,5),random(-5,5),frandom(2.0,6.0),0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE,0)
			TNT1 AAAA 0 A_SpawnItemEx("StrifeBloodClient",0,0,0,random(-5,5),random(-5,5),frandom(2.0,6.0),0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE,64)
			goto Strife1
	}
}

actor TemplarMeleeProj2NoSound : TemplarMeleeProj2
{
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,10 * ACS_ExecuteWIthResult(455,4))
			goto Super::XDeath+2
	}
}

actor StrifeBloodClient : StrifeBlood { vspeed 0 +CLIENTSIDEONLY gravity 0.35 }

actor TemplarMaulerBullet : StandardBullet
{
	obituary "%o was vaporized by %k."
	damagetype "disintegrate"
	-BLOODSPLATTER
	damage (4 * random(1,4) * ACS_ExecuteWithResult(455,5) / 100)
	renderstyle add
	states
	{
		Death:
		DefDeath:
			MPUF AB 5 bright
			POW1 ABCDE 4 bright
			stop
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,ACS_ExecuteWithResult(455,4))
			goto Strife1
		Strife1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Strife4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Strife2")
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3])
			goto DefDeath
		Strife2:
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 2)
			goto DefDeath
		Strife4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Strife8")
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 4)
			goto DefDeath
		Strife8:
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 8)
			goto DefDeath
	}
}

actor TemplarWeapon : BaseMonsterWeapon
{
	tag "Mauler and slicers"
	weapon.ammotype1 "MonsterManaGreen"
	weapon.sisterweapon "TemplarWeaponPowered"
	+WEAPON.NOALERT
	+WEAPON.NOAUTOAIM
	states
	{
		Select:
			WPS1 F 1 A_Raise
			wait
		Deselect:
			WPS1 F 1 A_Lower
			wait
		Ready:
			WPS1 F 0 A_JumpIfInventory("PowerWeaponLevel2",1,"Ready2")
			WPS1 FFFFFGGGGGHHHHHIIIII 1 A_WeaponReady
			loop
		Ready2:
			WPS4 FFFFFFFGGGGGGGHHHHHHHGGGGGGG 1 A_WeaponReady
			goto Ready
		Fire:
			WPS1 F 1 offset(0,20)
			WPS1 F 1 offset(0,8)
			WPS1 F 1 offset(0,-4)
			WPS1 F 1 offset(0,-16)
		Hold:
			WPS1 A 0 offset(0,32) A_GunFlash
			WPS1 A 0 A_AlertMonsters
			WPS1 A 0 A_TakeInventory("MonsterManaGreen",3)
			WPS1 A 0 A_PlaySound("templar/shoot",CHAN_WEAPON)
			WPS1 A 0 A_FireCustomMissile("TemplarMaulerBullet",random(-11,11),0,0,9,0,random(-7,7))
			WPS1 A 0 A_FireCustomMissile("TemplarMaulerBullet",random(-11,11),0,0,9,0,random(-7,7))
			WPS1 A 0 A_FireCustomMissile("TemplarMaulerBullet",random(-11,11),0,0,9,0,random(-7,7))
			WPS1 A 0 A_FireCustomMissile("TemplarMaulerBullet",random(-11,11),0,0,9,0,random(-7,7))
			WPS1 A 0 A_FireCustomMissile("TemplarMaulerBullet",random(-11,11),0,0,9,0,random(-7,7))
			WPS1 A 0 A_FireCustomMissile("TemplarMaulerBullet",random(-11,11),0,0,9,0,random(-7,7))
			WPS1 A 0 A_FireCustomMissile("TemplarMaulerBullet",random(-11,11),0,0,9,0,random(-7,7))
			WPS1 A 0 A_FireCustomMissile("TemplarMaulerBullet",random(-11,11),0,0,9,0,random(-7,7))
			WPS1 A 0 A_FireCustomMissile("TemplarMaulerBullet",random(-11,11),0,0,9,0,random(-7,7))
			WPS1 A 3 bright A_FireCustomMissile("TemplarMaulerBullet",random(-11,11),0,0,9,0,random(-7,7))
			WPS1 B 1 bright offset(0,34) A_Light1
			WPS1 B 1 bright offset(0,36)
			WPS1 B 1 bright offset(0,38)
			WPS1 C 1 bright offset(0,40) A_Light2
			WPS1 C 1 bright offset(0,42)
			WPS1 D 1 bright offset(0,44)
			WPS1 D 1 bright offset(0,46)
			WPS1 E 1 bright offset(0,42)
			WPS1 E 1 bright offset(0,38)
			WPS1 H 5 offset(0,-8) A_Light0
			WPS1 I 5 offset(0,-16)
			WPS1 F 5 offset(0,-16)
			WPS1 G 5 offset(0,-16)
			WPS1 H 0 A_JumpIfInventory("MonsterManaGreen",3,1)
			goto EndFire
			WPS1 H 0 A_ReFire
			WPS1 H 1 offset(0,-10)
			WPS1 H 1 offset(0,-4)
			WPS1 H 1 offset(0,2)
			WPS1 H 1 offset(0,8)
			WPS1 I 1 offset(0,14)
			WPS1 I 1 offset(0,20)
			WPS1 I 1 offset(0,26)
			WPS1 I 1 offset(0,32)
			goto Ready
		  EndFire:
			WPS1 H 1 offset(0,-10)
			WPS1 H 1 offset(0,-4)
			WPS1 H 1 offset(0,2)
			WPS1 H 1 offset(0,8)
			WPS1 I 1 offset(0,14)
			WPS1 I 1 offset(0,20)
			WPS1 I 1 offset(0,26)
			WPS1 I 1 offset(0,32)
			WPS1 IFGHIFGHIFGHI 4
			goto Ready
		AltFire:
			WPS1 F 1 offset(0,38)
			WPS1 F 1 offset(0,44)
			WPS1 F 1 offset(0,50)
			WPS1 F 1 offset(0,56)
			WPS1 G 1 offset(0,62)
			WPS1 G 1 offset(0,68)
			WPS1 G 1 offset(0,68) A_GiveInventory("shooting",1)
			WPS1 G 0 offset(0,68) A_ReFire
			WPS1 H 1 offset(0,68) A_TakeInventory("shooting",1)
			WPS1 H 1 offset(0,62)
			WPS1 H 1 offset(0,56)
			WPS1 I 1 offset(0,50)
			WPS1 I 1 offset(0,44)
			WPS1 I 1 offset(0,38)
			goto Ready
		AltHold:
			WPS1 J 10 A_PlaySound("reaver/blade",CHAN_WEAPON)
			WPS1 K 0 A_FireCustomMissile("TemplarMeleeProj",0,0,0,9)
			WPS1 K 10 A_JumpIfInventory("BerserkTracker",1,"DoubleHit")
			WPS1 J 8 A_ReFire
			WPS1 H 1 offset(0,68) A_TakeInventory("shooting",1)
			WPS1 H 1 offset(0,62)
			WPS1 H 1 offset(0,56)
			WPS1 I 1 offset(0,50)
			WPS1 I 1 offset(0,44)
			WPS1 I 1 offset(0,38)
			goto Ready
		DoubleHit:
			WPS1 K 10 A_FireCustomMissile("TemplarMeleeProj",0,0,0,9)
			goto AltHold+3
	}
}

actor TemplarWeaponPowered : TemplarWeapon
{
	weapon.sisterweapon "TemplarWeapon"
	+WEAPON.POWERED_UP
	states
	{
		Fire:
			WPS4 F 0 A_GunFlash
			WPS4 F 1 A_PlaySound("weapons/mauler2charge",CHAN_WEAPON)
			WPS4 F 1 offset(0,23)
			WPS4 F 1 offset(0,14)
			WPS4 F 1 offset(0,5)
			WPS4 FFFF 4 offset(0,-4) A_GunFlash
			WPS4 G 2 A_Light1
			WPS4 GG 4 A_GunFlash
			WPS1 A 0 offset(0,32) A_AlertMonsters
			WPS1 A 0 A_TakeInventory("MonsterManaGreen",12)
			WPS1 A 3 bright A_FireCustomMissile("MaulerTorpedo0",0,0,0,9)
			WPS1 B 1 bright offset(0,34)
			WPS1 B 1 bright offset(0,36)
			WPS1 B 1 bright offset(0,38)
			WPS1 C 1 bright offset(0,40) A_Light2
			WPS1 C 1 bright offset(0,42)
			WPS1 D 1 bright offset(0,44)
			WPS1 D 1 bright offset(0,46)
			WPS1 E 2 bright
			WPS4 F 1 offset(0,12) A_Light0
			WPS4 F 1 offset(0,16)
			WPS4 F 1 offset(0,20)
			WPS4 F 1 offset(0,24)
			WPS4 F 1 offset(0,28)
			WPS4 F 2 offset(0,32)
			WPS4 GHGF 7
			WPS4 G 7 A_JumpIfInventory("MonsterManaGreen",12,"Ready")
			WPS4 HGFGHGFGHGFGHGFGHG 7 A_WeaponReady(WRF_NOFIRE)
			goto Ready
		AltFire:
			WPS4 F 1 offset(0,38)
			WPS4 F 1 offset(0,44)
			WPS4 F 1 offset(0,50)
			WPS4 F 1 offset(0,56)
			WPS4 F 1 offset(0,62)
			WPS4 F 1 offset(0,68)
			WPS4 F 1 offset(0,68) A_GiveInventory("shooting",1)
			WPS4 G 0 offset(0,68) A_ReFire
			WPS4 G 1 offset(0,68) A_TakeInventory("shooting",1)
			WPS4 G 1 offset(0,62)
			WPS4 G 1 offset(0,56)
			WPS4 G 1 offset(0,50)
			WPS4 G 1 offset(0,44)
			WPS4 G 1 offset(0,38)
			goto Ready
		AltHold:
			WPS1 J 10 A_PlaySound("reaver/blade",CHAN_WEAPON)
			WPS1 K 0 A_FireCustomMissile("TemplarMeleeProj2",0,0,0,9)
			WPS1 K 10 A_JumpIfInventory("BerserkTracker",1,"DoubleHit")
			WPS1 J 8 A_ReFire
			WPS4 G 1 offset(0,68) A_TakeInventory("shooting",1)
			WPS4 G 1 offset(0,62)
			WPS4 G 1 offset(0,56)
			WPS4 G 1 offset(0,50)
			WPS4 G 1 offset(0,44)
			WPS4 G 1 offset(0,38)
			goto Ready
		DoubleHit:
			WPS1 K 10 A_FireCustomMissile("TemplarMeleeProj2NoSound",0,0,0,9)
			goto AltHold+3
	}
}

actor MaulerTorpedo0 : MaulerTorpedo
{
	height 13
	fastspeed 25
	seesound "weapons/mauler2fire"
	deathsound "weapons/mauler2hit"
	damage (4 * random(1,4) * ACS_ExecuteWithResult(455,5) / 100)
	-STRIFEDAMAGE
	+MTHRUSPECIES
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,ACS_ExecuteWithResult(455,4))
			goto Strife1
		Strife1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Strife4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Strife2")
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3])
			goto Death
		Strife2:
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 2)
			goto Death
		Strife4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Strife8")
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 4)
			goto Death
		Strife8:
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 8)
			goto Death
		Death:
			THIT A 8 bright
			THIT B 8 bright A_SetArg(0,80)
		Torpedo:
			THIT C 0 A_SetArg(0,args[0] - 1)
			THIT C 0 A_CustomMissile("MaulerTorpedoWave0",0,0,args[0] * 4.5,CMF_AIMDIRECTION|CMF_TRACKOWNER)
			THIT C 8 bright A_JumpIf(args[0] > 0,"Torpedo")
			THIT DE 8 bright
			stop
	}
}

actor MaulerTorpedoWave0 : MaulerTorpedoWave
{
	-STRIFEDAMAGE
	+MTHRUSPECIES
	reactiontime 3
	damage (4 * random(1,4) * ACS_ExecuteWithResult(455,5) / 100)
	states
	{
		Spawn:
			TWAV ABC 9 bright A_Countdown
			loop
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,ACS_ExecuteWithResult(455,4))
			goto Strife1
		Strife1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Strife4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Strife2")
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3])
			goto Death
		Strife2:
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 2)
			goto Death
		Strife4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Strife8")
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 4)
			goto Death
		Strife8:
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 8)
			goto Death
		Death:
			TWAV C 9 bright
			Stop
	}
}

actor TemplarMorph : MonsterMorph
{
	tag "Templar"
	inventory.icon "ARTIPGRD"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("TemplarHitboxCheck",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(450,0,2,2)
			stop
	}
}

actor TemplarMorphDirect : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,2,2)
			stop
	}
}

actor TemplarHitboxCheck : BaseHitboxCheck
{
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_GiveToTarget("TemplarMorphDirect",1)
			stop
	}
}

actor TemplarMorpher : MonsterMorpher { powerup.type MorphPgrd }

actor PowerMorphPgrd : PowerMorphStrife { powermorph.playerclass "TemplarClass" }