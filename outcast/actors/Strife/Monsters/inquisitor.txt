
actor InquisitorClass : MonsterPawnStrife
{
	player.morphweapon "InquisitorWeapon"
	health 1000
	player.maxhealth 1000
	mass 2000
	speed 0.4
	painchance 0
	bloodtype "StrifeSpark1"
	player.soundclass "inqui"
	+NORADIUSDMG
	states
	{
		Spawn:
			ROB3 A -1
			stop
		See:
			ROB3 C 8
			ROB3 E 6 A_PlaySound("inquisitor/walk",CHAN_BODY)
			ROB3 D 8
			ROB3 B 6
			loop
		Missile:
			ROB3 AAA 1 A_JumpIfInventory("shooting",1,"Grenade")
			ROB3 FFF 1 bright A_JumpIfInventory("shooting",1,"Grenade")
			ROB3 F 9 bright
			ROB3 G 10 bright
			goto Spawn
		Grenade:
			ROB3 K 7
			ROB3 J 10 bright
			goto Spawn
		Melee:
			ROB3 H 4 bright
			ROB3 I 6 bright
			goto Spawn
		Death:
			ROB3 L 0 A_ChangeFlag("NOBLOOD",1)
			ROB3 L 4 A_TossGib
			ROB3 M 4 A_PlaySound("inquisitor/death",6,1.0,0,ATTN_NONE)
			ROB3 N 4 A_TossGib
			ROB3 O 4 Bright A_Explode(128, 128)
			ROB3 P 4 Bright A_TossGib
			ROB3 Q 4 Bright A_NoBlocking
			ROB3 RSTUV 4 A_TossGib
			ROB3 W 4 Bright A_Explode(128, 128)
			ROB3 XY 4 Bright A_TossGib
			ROB3 Z 4 A_TossGib
			ROB3 "[" 4 A_TossGib
			ROB3 "\" 3 A_TossGib
			ROB3 "]" 3 bright A_Explode(128, 128)
			RBB3 A 3 bright A_SpawnItemEx("InquisitorArm",0,0,24,frandom(-3.0,3.0),frandom(-3.0,3.0),frandom(1.0,4.0),random(0,180),SXF_NOCHECKPOSITION)
			RBB3 B 3 bright A_TossGib
			RBB3 CD 3 A_TossGib
			RBB3 E -1
			stop
	}
}

actor InquisitorClassHitbox : InquisitorClass
{
	height 116
	radius 60
	player.viewheight 100
}

actor InquisitorWeapon : BaseMonsterWeapon
{
	weapon.ammotype1 "MonsterManaBlue"
	weapon.ammotype2 "MonsterManaGreen"
	obituary "%o was sentenced by %k."
	weapon.sisterweapon "InquisitorWeaponPowered"
	tag "Heavy Robo-Shotgun, dual grenades and jetpack"
	+WEAPON.NOALERT
	states
	{
		Ready:
			WPS3 H 1 A_WeaponReady
			wait
		Fire:
			WPS3 H 0 SetPlayerProperty(0,1,0)
			WPS3 H 0 A_JumpIfTargetInLOS("Grenade",0,JLOSF_ALLYNOJUMP,400)
			WPS3 H 3 A_AlertMonsters
			WPS3 J 0 A_PlaySound("reaver/attack",CHAN_WEAPON)
			WPS3 J 0 A_FireCustomMissile("ReaverBullet02",random(-11,11),0,0,9)
			WPS3 J 0 A_FireCustomMissile("ReaverBullet02",random(-11,11),0,0,9)
			WPS3 J 2 bright A_FireCustomMissile("ReaverBullet02",random(-11,11),0,0,9)
			WPS3 I 3 bright
			WPS3 K 3 A_TakeInventory("MonsterManaGreen",2)
			WPS3 J 0 A_PlaySound("reaver/attack",CHAN_WEAPON)
			WPS3 J 0 A_FireCustomMissile("ReaverBullet02",random(-11,11),0,0,9)
			WPS3 J 0 A_FireCustomMissile("ReaverBullet02",random(-11,11),0,0,9)
			WPS3 J 2 bright A_FireCustomMissile("ReaverBullet02",random(-11,11),0,0,9)
			WPS3 I 3 bright
			WPS3 K 3 A_TakeInventory("MonsterManaGreen",2)
			WPS3 K 5 SetPlayerProperty(0,0,0)
			WPS3 L 8 
			WPS3 H 11 A_JumpIfInventory("MonsterManaGreen",4,6)
			WPS3 HHHHH 11 A_WeaponReady(WRF_NOFIRE)
			WPS3 HHHHHHHHH 1 A_WeaponReady(WRF_NOPRIMARY)
			goto Ready
		Grenade:
			WPS3 H 1 offset(0,42) A_GiveInventory("shooting",1)
			WPS3 H 1 offset(0,52)
			WPS3 H 1 offset(0,62)
			WPS3 H 1 offset(0,72)
			WPS3 H 1 offset(0,82)
			WPS3 H 1 offset(0,92)
			WPS3 H 1 offset(0,102) A_AlertMonsters
			WPS3 H 0 A_TakeInventory("MonsterManaBlue",8)
			WPS3 H 0 A_FireCustomMissile("InquisitorShot0",-2,0,16,64,0,2)
			WPS3 H 10 A_FireCustomMissile("InquisitorShot0",2,0,-16,64,0,-5)
			WPS3 H 5 SetPlayerProperty(0,0,0)
			WPS3 H 1 offset(0,92) A_TakeInventory("shooting",1)
			WPS3 H 1 offset(0,82)
			WPS3 H 1 offset(0,72)
			WPS3 H 1 offset(0,62)
			WPS3 H 1 offset(0,52)
			WPS3 H 1 offset(0,42)
			WPS3 H 10 offset(0,32) A_JumpIfInventory("MonsterManaBlue",8,8)
			WPS3 HHHHHHH 10 A_WeaponReady(WRF_NOFIRE)
			WPS3 HHHHHHHHHHHH 1 A_WeaponReady(WRF_NOPRIMARY)
			goto Ready
		AltFire:
			WPS3 H 0 ACS_ExecuteAlways(451,0,6,60)
			WPS3 H 0 A_JumpIf(ACS_ExecuteWithResult(455,1) == 0,2)
			WPS3 H 0 A_GiveInventory("MonFlight",1)
			WPS3 H 4 bright A_GunFlash
			WPS3 H 0 A_PlaySound("inquisitor/jump",6,1.0,1)
		AltHold:
			WPS3 H 8 bright A_GunFlash
			WPS3 H 0 A_ReFire
			WPS3 HHHHH 1 ThrustThing(angle*256/360, 1, 0, 0)
			WPS3 H 0 A_PlaySound("inquisitor/sight",6)
			WPS3 H 6 A_TakeInventory("PowerFlight",1)
			WPS3 H 2 ACS_ExecuteAlways(451,0,6,40)
			goto Ready
	}
}

actor InquisitorWeaponPowered : InquisitorWeapon
{
	weapon.sisterweapon "InquisitorWeapon"
	+WEAPON.POWERED_UP
	tag "Phosphorous grenades and turbo jetpack"
	states
	{
		Fire:
			WPS3 H 0 SetPlayerProperty(0,1,0)
			WPS3 H 1 offset(0,42) A_GiveInventory("shooting",1)
			WPS3 H 1 offset(0,52)
			WPS3 H 1 offset(0,62)
			WPS3 H 1 offset(0,72)
			WPS3 H 1 offset(0,82)
			WPS3 H 1 offset(0,92)
			WPS3 H 1 offset(0,102) A_AlertMonsters
			WPS3 H 0 A_TakeInventory("MonsterManaBlue",20)
			WPS3 H 0 A_FireCustomMissile("InquisitorShot02",-2,0,16,64,0,2)
			WPS3 H 10 A_FireCustomMissile("InquisitorShot02",2,0,-16,64,0,-5)
			WPS3 H 5 SetPlayerProperty(0,0,0)
			WPS3 H 1 offset(0,92) A_TakeInventory("shooting",1)
			WPS3 H 1 offset(0,82)
			WPS3 H 1 offset(0,72)
			WPS3 H 1 offset(0,62)
			WPS3 H 1 offset(0,52)
			WPS3 H 1 offset(0,42)
			WPS3 H 10 offset(0,32) A_JumpIfInventory("MonsterManaBlue",20,16)
			WPS3 HHHHHHHHHHHHHHHHHH 10 A_WeaponReady(WRF_NOFIRE)
			WPS3 HHHHHHHHHHHH 1 A_WeaponReady(WRF_NOPRIMARY)
			goto Ready
		AltFire:
			WPS3 H 0 ACS_ExecuteAlways(451,0,6,85)
			WPS3 H 0 A_JumpIf(ACS_ExecuteWithResult(455,1) == 0,2)
			WPS3 H 0 A_GiveInventory("MonFlight",1)
			WPS3 H 4 bright A_GunFlash
			WPS3 H 0 A_PlaySound("inquisitor/jump",6,1.0,1)
			goto AltHold
	}
}

actor ReaverBullet02 : ReaverBullet0 { obituary "%o was sentenced by %k." }

actor InquisitorShot0 : InquisitorShot
{
	+MTHRUSPECIES
	+SEEKERMISSILE
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	-STRIFEDAMAGE
	obituary "%o was sentenced by %k."
	damage (20 * random(1,4) * ACS_ExecuteWithResult(455,5) / 100)
	states
	{
		Spawn:
			UBAM A 0
			UBAM A 0 A_SeekerMissile(1,90,SMF_PRECISE)
			UBAM A 0 A_ChangeVelocity(0,0,2)
			UBAM AB 3 A_Countdown
			goto Spawn+2
		Death:
			BNG2 A 0 A_SetTranslucent(1,1)
			BNG2 A 4 bright A_Explode(75 * ACS_ExecuteWithResult(455,5) / 100, 192, 0)
			BNG2 BCDEFGHI 4 bright
			stop
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,12 * ACS_ExecuteWithResult(455,4))
			goto Strife1
		Strife1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Strife4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Strife2")
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3])
			goto Death
		Strife2:
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 2)
			goto Death
		Strife4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Strife8")
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 4)
			goto Death
		Strife8:
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 8)
			goto Death
	}
}

actor InquisitorShot02 : InquisitorShot0
{
	damage (10 * random(1,4) * ACS_ExecuteWithResult(455,5) / 100)
	states
	{
		Spawn:
			GRIN A 0
			GRIN A 0 A_SeekerMissile(1,90,SMF_PRECISE)
			GRIN A 0 A_ChangeVelocity(0,0,2)
			GRIN AB 3 A_Countdown
			goto Spawn+2
		Death:
			BNG3 A 0 A_SetTranslucent(1,1)
			BNG2 A 0 A_Explode(40 * ACS_ExecuteWithResult(455,5) / 100, 80, 0)
			BNG3 A 4 bright A_CustomMissile("PhosphorousFire0",0,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER)
			BNG3 BCDEFGH 4 bright
			stop
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,65 * ACS_ExecuteWithResult(455,4) / 10)
			goto Strife1
		Strife1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Strife4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Strife2")
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3])
			goto Death
		Strife2:
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 2)
			goto Death
		Strife4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Strife8")
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 4)
			goto Death
		Strife8:
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 8)
			goto Death
	}
}

actor PhosphorousFire0
{
	reactiontime 40
	speed 0
	radius 16
	height 32
	renderstyle add
	obituary "%o was sentenced to a burning agony by %k."
	PROJECTILE
	+FLOORHUGGER
	+THRUACTORS
	+DONTSPLASH
	states
	{
		Spawn:
			FLBE ABC 2 bright
			FLBE D 0 A_SetArg(0,random(-30,30))
			FLBE D 0 A_SetArg(1,random(-30,30))
			FLBE D 3 bright A_ChangeVelocity(args[0] * 0.1,args[1] * 0.1,0,CVF_REPLACE)	// directly using random in A_ChangeVelocity desyncs terribly online!
			FLBE E 0 A_CustomMissile("PhosphorousFire03",0,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER)
			FLBE E 3 bright A_SpawnItemEx("PhosphorousFire02",random(-31,31),random(-31,31),0,frandom(-5.0,5.0),frandom(-5.0,5.0),0,0,SXF_TRANSFERPOINTERS)
			FLBE F 0 A_PlaySound("world/largefire",CHAN_VOICE)
			FLBE F 3 bright A_Countdown
			FLBE G 3 bright A_SpawnItemEx("PhosphorousFire02",random(-31,31),random(-31,31),0,frandom(-5.0,5.0),frandom(-5.0,5.0),0,0,SXF_TRANSFERPOINTERS)
			goto Spawn+3
		Death:
			FLBE HIJK 2 bright
			stop
	}
}

actor PhosphorousFire02 : PhosphorousFire0
{
	reactiontime 1
	+STEPMISSILE
	-FLOORHUGGER
	states
	{
		Spawn:
			FLBE ABC 2 bright
			FLBE D 0 A_ScaleVelocity(0.875)
			FLBE D 3 bright A_PlaySound("world/largefire",CHAN_VOICE)
			FLBE E 3 bright A_CustomMissile("PhosphorousFire03",0,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER)
			FLBE F 0 A_ScaleVelocity(0.75)
			FLBE F 3 bright A_PlaySound("world/largefire",CHAN_VOICE)
			FLBE G 3 bright A_CustomMissile("PhosphorousFire03",0,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER)
			FLBE G 1 bright A_Countdown
			goto Spawn+3
	}
}

actor PhosphorousFire03 : PhosphorousFire0
{
	speed 1
	radius 40
	damage (3 * random(1,4) * ACS_ExecuteWithResult(455,5) / 100)
	-THRUACTORS
	-FLOORHUGGER
	+STEPMISSILE
	+NODAMAGETHRUST
	states
	{
		Spawn:
			TNT1 A 1
			stop
		Death:
			TNT1 A 1
			stop
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,75 * ACS_ExecuteWithResult(455,4) / 100)
			goto Strife1
		Strife1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Strife4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Strife2")
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3])
			goto Death
		Strife2:
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 2)
			goto Death
		Strife4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Strife8")
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 4)
			goto Death
		Strife8:
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 8)
			goto Death
	}
}

actor InquisitorMorph : MonsterMorph
{
	tag "Inquisitor"
	inventory.icon "ARTIROB3"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("InquisitorHitboxCheck",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(450,0,3,1)
			stop
	}
}

actor InquisitorMorphDirect : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,3,1)
			stop
	}
}

actor InquisitorHitboxCheck : BaseHitboxCheck
{
	height 116
	radius 60
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_GiveToTarget("InquisitorMorphDirect",1)
			stop
	}
}

actor InquisitorMorpher : MonsterMorpher { powerup.type MorphRob3 }

actor PowerMorphRob3 : PowerMorphStrife { powermorph.playerclass "InquisitorClass" }

actor InquisitorMorpherHitbox : MonsterMorpher { powerup.type MorphRob3Hitbox }

actor PowerMorphRob3Hitbox : PowerMorphStrife { powermorph.playerclass "InquisitorClassHitbox" }