
actor LoremasterClass : MonsterPawnStrife
{
	player.morphweapon "LoremasterWeapon"
	health 800
	player.maxhealth 800
	painchance 64
	mass 300
	bloodtype "StrifeSpark1"
	player.soundclass "lore"
	speed 0.5
	states
	{
		Spawn:
			PRST AB 10
			loop
		See:
			PRST ABCD 8
			PRST A 0 A_Jump(251,"See")
			PRST A 0 A_Jump(1,"See2")
			loop
		See2:
			PRST A 0 A_PlaySound("loremaster/taunt",0)
			goto See
		Pain:
			PRST A 4 A_PlaySound("loremaster/pain",2)
			goto Spawn
		Missile:
			PRST E 4
			PRST F 6
			PRST E 6
			goto Spawn
		Death:
			PDED A 6
			PDED B 6 A_PlayerScream
			PDED C 6
			PDED D 6 A_Fall
			PDED E 6
			PDED FGHIJIJIJKL 5
			PDED MNOP 4
			PDED Q 4
			PDED RS 4
			PDED T -1
			stop
	}
}

actor LoremasterWeapon : BaseMonsterWeaponFlight
{
	obituary "%o couldn't escape from %k's grasp."
	weapon.sisterweapon "LoremasterWeaponPowered"
	tag "Grappling hook"
	weapon.ammotype1 "MonsterManaBlue"
	+WEAPON.NOALERT
	states
	{
		Fire:
			WPS3 G 1 A_AlertMonsters
			WPS3 EC 1
			WPS3 B 1 A_TakeInventory("MonsterManaBlue",3)
			WPS3 A 0 A_PlaySound("Loremaster/chain",CHAN_WEAPON)
			WPS3 A 8 A_FireCustomMissile("LoreShot0",0,0,0,9)
			WPS3 BCDEFG 2
			TNT1 A 12
			TNT1 A 100 A_JumpIfInventory("MonsterManaBlue",3,1)
			TNT1 AAAAAAAAAA 1 A_WeaponReady(WRF_NOPRIMARY)
			goto Ready+3
		AltFire:
			WPS3 G 1 A_PlaySound("reaver/blade",CHAN_WEAPON)
			WPS3 FEDCB 1
			WPS3 A 0 A_FireCustomMissile("LoremasterMeleeProj",0,0,0,9)
			WPS3 A 4 A_JumpIfInventory("BerserkTracker",1,"DoubleHit")
			goto DoubleHit+1
		AltHold:
			WPS3 D 2 A_PlaySound("reaver/blade",CHAN_WEAPON)
			WPS3 CB 2
		DoubleHit:
			WPS3 A 4 A_FireCustomMissile("LoremasterMeleeProj",0,0,0,9)
			WPS3 BC 1
			WPS3 D 1 A_ReFire
			WPS3 EFG 1
			goto Ready+3
	}
}

actor LoremasterWeaponPowered : LoremasterWeapon
{
	weapon.sisterweapon "LoremasterWeapon"
	+WEAPON.POWERED_UP
	states
	{
		Fire:
			WPS3 G 1 A_AlertMonsters
			WPS3 EC 1
			WPS3 B 1 A_TakeInventory("MonsterManaBlue",10)
			WPS3 A 0 A_PlaySound("Loremaster/chain",CHAN_WEAPON)
			WPS3 A 8 A_FireCustomMissile("LoreShot02",0,0,0,9)
			WPS3 BCDEFG 2
			TNT1 A 42
			TNT1 A 160 A_JumpIfInventory("MonsterManaBlue",3,1)
			TNT1 AAAAAAAAAA 1 A_WeaponReady(WRF_NOPRIMARY)
			goto Ready+3
		AltFire:
			WPS3 G 1 A_PlaySound("reaver/blade",CHAN_WEAPON)
			WPS3 FEDCB 1
			WPS3 A 0 A_FireCustomMissile("LoremasterMeleeProj2",0,0,0,9)
			WPS3 A 4 A_JumpIfInventory("BerserkTracker",1,"DoubleHit")
			goto DoubleHit+1
		AltHold:
			WPS3 D 2 A_PlaySound("reaver/blade",CHAN_WEAPON)
			WPS3 CB 2
		DoubleHit:
			WPS3 A 4 A_FireCustomMissile("LoremasterMeleeProj2NoSound",0,0,0,9)
			WPS3 BC 1
			WPS3 D 1 A_ReFire
			WPS3 EFG 1
			goto Ready+3
	}
}

actor LoremasterMeleeProj : ReaverMeleeProj { obituary "%o couldn't escape from %k's grasp." }

actor LoremasterMeleeProj2 : ReaverMeleeProj2 { obituary "%o couldn't escape from %k's grasp." }

actor LoremasterMeleeProj2NoSound : ReaverMeleeProj2NoSound { obituary "%o couldn't escape from %k's grasp." }

actor LoreShot0 : LoreShot
{
	-STRIFEDAMAGE
	+MTHRUSPECIES
	damage (20 * random(1,4) * ACS_ExecuteWithResult(455,5) / 100)
	fastspeed 25
	height 10
	seesound ""
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,5 * ACS_ExecuteWithResult(455,4))
			goto Strife1
		Strife1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Strife4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Strife2")
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3])
			goto Super::Death
		Strife2:
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 2)
			goto Super::Death
		Strife4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Strife8")
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 4)
			goto Super::Death
		Strife8:
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 8)
			goto Super::Death
	}
}

actor LoreShot02 : LoreShot0
{
	+RIPPER
	+NODAMAGETHRUST
	+DONTREFLECT
	+FOILINVUL
	damage 0
	states
	{
		Spawn:
			OCLW A 0 A_LoremasterChain
			OCLW AA 1 A_CustomMissile("LoreShot03",0,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER)
			loop
	}
}

actor LoreShot03
{
	PROJECTILE
	+MTHRUSPECIES
	+DONTREFLECT
	+NODAMAGETHRUST
	radius 10
	height 10
	damage (10 * random(1,4) * ACS_ExecuteWithResult(455,5) / 100)
	speed 1
	states
	{
		Spawn:
			TNT1 A 1
			stop
		Death:
			TNT1 A 1
			stop
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,25 * ACS_ExecuteWithResult(455,4) / 10)
			goto Strife1
		Strife1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Strife4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Strife2")
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3])
			goto Death
		Strife2:
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 2)
			goto Death
		Strife4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Strife8")
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 4)
			goto Death
		Strife8:
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 8)
			goto Death
	}
}

actor LoremasterMorph : MonsterMorph
{
	tag "Loremaster"
	inventory.icon "ARTIPRST"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("LoremasterHitboxCheck",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(450,0,2,1)
			stop
	}
}

actor LoremasterMorphDirect : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,2,1)
			stop
	}
}

actor LoremasterHitboxCheck : BaseHitboxCheck
{
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_GiveToTarget("LoremasterMorphDirect",1)
			stop
	}
}

actor LoremasterMorpher : MonsterMorpher { powerup.type MorphPrst }

actor PowerMorphPrst : PowerMorphStrife { powermorph.playerclass "LoremasterClass" }