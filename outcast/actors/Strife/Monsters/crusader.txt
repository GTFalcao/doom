
actor CrusaderClass : MonsterPawnStrife
{
	player.morphweapon "CrusaderWeapon"
	health 400
	player.maxhealth 400
	painchance 170
	mass 1000
	speed 0.5
	bloodtype "StrifeSpark1"
	player.soundclass "crus"
	states
	{
		Spawn:
			ROB2 Q -1
			stop
		See:
			ROB2 B 6
			ROB2 C 6 //A_PlaySound("crusader/pain",CHAN_BODY)
			ROB2 D 6
			ROB2 A 6 //A_PlaySound("crusader/pain",CHAN_BODY)
			loop
		Pain:
			ROB2 A 1 A_PlaySound("crusader/pain",chan_voice)
			goto Spawn
		Missile:
			ROB2 E 5
			ROB2 A 6
			goto Spawn
		Melee:
			ROB2 F 3 bright
			goto Missile
		Death:
			ROB2 G 0 A_ChangeFlag("NOBLOOD",1)
			ROB2 G 3 A_PlayerScream
			ROB2 H 5 A_TossGib
			ROB2 I 4 Bright A_TossGib
			ROB2 J 4 Bright A_Explode(64, 64)
			ROB2 K 4 Bright A_Fall
			ROB2 L 4 A_Explode(64, 64)
			ROB2 MN 4 A_TossGib
			ROB2 O 4 A_Explode(64, 64)
			ROB2 P -1
			stop
	}
}

actor CrusaderClassHitbox : CrusaderClass
{
	height 64
	radius 40
	player.attackzoffset 4
}

actor CrusaderWeapon : BaseMonsterWeapon
{
	obituary "%o was swept away by %k."
	tag "Tri-missile launcher and flamethrower"
	weapon.ammotype1 "MonsterManaBlue"
	weapon.ammotype2 "MonsterManaGreen"
	states
	{/*
		Select:
			TNT1 A 1 A_Raise
			wait
		Deselect:
			TNT1 A 1 A_Lower
			wait*/
		Ready1:
			WPS0 L 1 A_WeaponReady
			wait
		Ready2:
			WPS0 G 0 A_PlaySound("weapons/flameidle",chan_weapon)
			WPS0 GGGHHH 1 bright A_WeaponReady
			loop
		Fire:
			WPS0 L 0 A_JumpIfInventory("shooting",1,"ShootFlamer")
			WPS0 L 0 A_JumpIfInventory("submerged",1,"PreShootFlamer")
			WPS0 L 3 SetPlayerProperty(0,1,0)
			WPS0 M 0 A_JumpIfInventory("PowerWeaponLevel2",1,"FirePowered")
			WPS0 M 0 A_TakeInventory("MonsterManaBlue",3)
			WPS0 M 0 A_FireCustomMissile("CrusaderMissile0",1,0,-4,0)
			WPS0 M 0 A_FireCustomMissile("CrusaderMissile0",-1,0,4,0)
			WPS0 M 2 bright A_FireCustomMissile("CrusaderMissile0",0,0,0,16)
			WPS0 MN 2 bright
			WPS0 N 2 bright SetPlayerProperty(0,0,0)
			WPS0 OPQ 2 bright
			WPS0 L 8
			WPS0 L 0 A_JumpIfInventory("MonsterManaBlue",3,9)
			WPS0 LLLLLLLL 10 A_WeaponReady(WRF_NOFIRE)
			WPS0 LLLLLLLLLLLLLLL 1 A_WeaponReady(WRF_NOPRIMARY)
			goto Ready1
		FirePowered:
			WPS0 M 0 A_TakeInventory("MonsterManaBlue",6)
			WPS0 M 0 A_FireCustomMissile("CrusaderMissile04",0,0,-12,0)
			WPS0 M 0 A_FireCustomMissile("CrusaderMissile03",0,0,12,0)
			WPS0 M 2 bright A_FireCustomMissile("CrusaderMissile02",0,0,0,21)
			WPS0 MN 2 bright
			WPS0 N 2 bright SetPlayerProperty(0,0,0)
			WPS0 OPQ 2 bright
			WPS0 L 8
			WPS0 L 0 A_JumpIfInventory("MonsterManaBlue",6,9)
			WPS0 LLLLLLLL 10 A_WeaponReady(WRF_NOFIRE)
			WPS0 LLLLLLLLLLLLLLL 1 A_WeaponReady(WRF_NOPRIMARY)
			goto Ready1
		RaiseRocket:
			WPS0 R 1 bright offset(-18,33)
			WPS0 R 1 bright offset(-33,35)
			WPS0 R 1 bright offset(-45,40)
			WPS0 R 1 bright offset(-55,48)
			WPS0 R 1 bright offset(-63,58)
			WPS0 R 1 bright offset(-68,70)
			WPS0 R 1 bright offset(-70,85)
		Ready:
			TNT1 A 10 
			WPS0 L 1 offset(76,82) A_TakeInventory("submerged",1)
			WPS0 L 1 offset(56,67) A_TakeInventory("submerged",1)
			WPS0 L 1 offset(40,55) A_TakeInventory("submerged",1)
			WPS0 L 1 offset(26,45) A_TakeInventory("submerged",1)
			WPS0 L 1 offset(14,38) A_TakeInventory("submerged",1)
			WPS0 L 1 offset(6,35) A_TakeInventory("submerged",1)
			WPS0 L 1 offset(2,33) A_TakeInventory("submerged",1)
			WPS0 LLLLLLLLLLLL 1 A_WeaponReady(WRF_NOSECONDARY)
			goto Ready1
		AltFire:
			WPS0 L 1 A_JumpIfInventory("submerged",1,"RaiseRocket")
			WPS0 L 1 offset(2,33)
			WPS0 L 1 offset(6,35)
			WPS0 L 1 offset(14,38)
			WPS0 L 1 offset(26,45)
			WPS0 L 1 offset(40,55)
			WPS0 L 1 offset(56,67)
			WPS0 L 1 offset(76,82)
			TNT1 A 10
			WPS0 R 1 bright offset(-70,85) A_GiveInventory("submerged",1)
			WPS0 R 1 bright offset(-68,70) A_GiveInventory("submerged",1)
			WPS0 R 1 bright offset(-63,58) A_GiveInventory("submerged",1)
			WPS0 R 1 bright offset(-55,48) A_GiveInventory("submerged",1)
			WPS0 R 1 bright offset(-45,40) A_GiveInventory("submerged",1)
			WPS0 R 1 bright offset(-33,35) A_GiveInventory("submerged",1)
			WPS0 R 1 bright offset(-18,33) A_GiveInventory("submerged",1)
			WPS0 G 0 A_PlaySound("weapons/flameidle",chan_weapon)
			WPS0 GGGHHH 1 bright A_WeaponReady(WRF_NOSECONDARY)
			WPS0 G 0 A_PlaySound("weapons/flameidle",chan_weapon)
			WPS0 GGGHHH 1 bright A_WeaponReady(WRF_NOSECONDARY)
			goto Ready2
		PreShootFlamer:
			WPS0 I 1 offset(-32,60) SetPlayerProperty(0,1,0)
			WPS0 I 1 offset(-24,53) A_GiveInventory("shooting",1)
			WPS0 I 1 offset(-16,46)
			WPS0 I 1 offset(-8,39) A_StopSound(chan_weapon)
		ShootFlamer:
			WPS0 J 0 offset(-1,32) A_GunFlash
			WPS0 J 0 A_JumpIfInventory("PowerWeaponLevel2",1,"ShootFlamerPowered")
			WPS0 J 2 bright A_FireCustomMissile("FastFlameMissile0",10,0,0,9)
			WPS0 J 2 bright A_FireCustomMissile("FastFlameMissile0",5,0,0,9)
			WPS0 K 2 bright A_FireCustomMissile("FastFlameMissile0",0,0,0,9)
			WPS0 K 1 bright A_GunFlash
			WPS0 K 1 bright A_FireCustomMissile("FastFlameMissile0",-5,0,0,9)
			WPS0 J 1 bright A_TakeInventory("MonsterManaGreen",1)
			WPS0 J 2 bright A_FireCustomMissile("FastFlameMissile0",-10,0,0,9)
			WPS0 J 0 A_GunFlash
			WPS0 J 1 bright A_FireCustomMissile("FastFlameMissile0",-5,0,0,9)
			WPS0 K 1 bright 
			WPS0 K 2 bright A_FireCustomMissile("FastFlameMissile0",0,0,0,9)
			WPS0 J 0 A_TakeInventory("MonsterManaGreen",1)
			WPS0 J 2 bright A_FireCustomMissile("FastFlameMissile0",5,0,0,9)
			WPS0 J 0 A_GunFlash
			WPS0 J 1 bright A_FireCustomMissile("FastFlameMissile0",10,0,0,9)
			WPS0 K 3 bright A_JumpIfInventory("MonsterManaGreen",2,1)
			goto Delay
			WPS0 I 2 A_ReFire
			WPS0 I 2 SetPlayerProperty(0,0,0)
			WPS0 I 1 offset(-8,39) A_TakeInventory("shooting",1)
			WPS0 I 1 offset(-16,46)
			WPS0 I 1 offset(-24,53)
			WPS0 I 1 offset(-32,60)
			WPS0 G 0 A_PlaySound("weapons/flameidle",chan_weapon)
			WPS0 GH 3 bright A_WeaponReady(WRF_NOPRIMARY)
			WPS0 G 0 A_PlaySound("weapons/flameidle",chan_weapon)
			WPS0 GH 3 bright A_WeaponReady(WRF_NOPRIMARY)
			goto Ready2
		ShootFlamerPowered:
			WPS0 J 2 bright A_FireCustomMissile("FastFlameMissile02",10,0,0,9)
			WPS0 J 2 bright A_FireCustomMissile("FastFlameMissile02",5,0,0,9)
			WPS0 K 0 A_TakeInventory("MonsterManaGreen",1)
			WPS0 K 2 bright A_FireCustomMissile("FastFlameMissile02",0,0,0,9)
			WPS0 K 1 bright A_GunFlash
			WPS0 K 1 bright A_FireCustomMissile("FastFlameMissile02",-5,0,0,9)
			WPS0 J 1 bright A_TakeInventory("MonsterManaGreen",1)
			WPS0 J 2 bright A_FireCustomMissile("FastFlameMissile02",-10,0,0,9)
			WPS0 J 0 A_GunFlash
			WPS0 J 1 bright A_FireCustomMissile("FastFlameMissile02",-5,0,0,9)
			WPS0 K 1 bright A_TakeInventory("MonsterManaGreen",1)
			WPS0 K 2 bright A_FireCustomMissile("FastFlameMissile02",0,0,0,9)
			WPS0 J 2 bright A_FireCustomMissile("FastFlameMissile02",5,0,0,9)
			WPS0 J 0 A_GunFlash
			WPS0 J 1 bright A_FireCustomMissile("FastFlameMissile02",10,0,0,9)
			WPS0 K 0 A_TakeInventory("MonsterManaGreen",1)
			WPS0 K 3 bright A_JumpIfInventory("MonsterManaGreen",4,1)
			goto Delay
			WPS0 I 2 A_ReFire
			WPS0 I 2 SetPlayerProperty(0,0,0)
			WPS0 I 1 offset(-8,39) A_TakeInventory("shooting",1)
			WPS0 I 1 offset(-16,46)
			WPS0 I 1 offset(-24,53)
			WPS0 I 1 offset(-32,60)
			WPS0 G 0 A_PlaySound("weapons/flameidle",chan_weapon)
			WPS0 GH 3 bright A_WeaponReady(WRF_NOPRIMARY)
			WPS0 G 0 A_PlaySound("weapons/flameidle",chan_weapon)
			WPS0 GH 3 bright A_WeaponReady(WRF_NOPRIMARY)
			goto Ready2
		Delay:
			WPS0 K 3 bright
			WPS0 I 2
			WPS0 I 2 SetPlayerProperty(0,0,0)
			WPS0 I 1 offset(-8,39) A_TakeInventory("shooting",1)
			WPS0 I 1 offset(-16,46)
			WPS0 I 1 offset(-24,53)
			WPS0 I 1 offset(-32,60)
			WPS0 G 0 A_PlaySound("weapons/flameidle",chan_weapon)
			WPS0 GH 3 bright A_WeaponReady(WRF_NOFIRE)
			WPS0 G 0 A_PlaySound("weapons/flameidle",chan_weapon)
			WPS0 GH 3 bright A_WeaponReady(WRF_NOFIRE)
			WPS0 G 0 A_PlaySound("weapons/flameidle",chan_weapon)
			WPS0 GH 3 bright A_WeaponReady(WRF_NOFIRE)
			WPS0 G 0 A_PlaySound("weapons/flameidle",chan_weapon)
			WPS0 GH 3 bright A_WeaponReady(WRF_NOFIRE)
			goto Ready2
	}
}

actor CrusaderMissile0 : CrusaderMissile
{
	obituary "%o was swept away by %k."
	damage (9 * random(1,4) * ACS_ExecuteWithResult(455,5) / 100)
	fastspeed 28
	-STRIFEDAMAGE
	+MTHRUSPECIES
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,225 * ACS_ExecuteWithResult(455,4) / 100)
			goto Strife1
		Death:
			SMIS A 5 bright A_SetTranslucent(1, 1)
			SMIS B 5 bright
			SMIS C 4 bright
			SMIS DEFG 2 bright
			Stop
		Strife1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Strife4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Strife2")
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3])
			goto Death
		Strife2:
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 2)
			goto Death
		Strife4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Strife8")
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 4)
			goto Death
		Strife8:
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 8)
			goto Death
	}
}

actor CrusaderMissile02 : CrusaderMissile0
{
	damage (16 * random(1,4) * ACS_ExecuteWithResult(455,5) / 100)
	weaveindexxy 0
	weaveindexz 16
	+SEEKERMISSILE
	+DONTREFLECT
	states
	{
		Spawn:
			MICR A 0 A_RocketInFlight
			MICR A 0 A_SetArg(0,6)
			MICR A 0 A_SeekerMissile(0,2)
			MICR A 1 bright A_Weave(2,2,1.5,2.625)
			MICR A 0 A_SetArg(0,args[0] - 1)
			MICR A 0 A_JumpIf(args[0] <= 0,"Spawn")
			goto Spawn+2
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,4 * ACS_ExecuteWithResult(455,4))
			goto Strife1
	}
}

actor CrusaderMissile03 : CrusaderMissile02
{
	weaveindexxy 16
	weaveindexz 32
	states
	{
		Spawn:
			MICR A 0 A_RocketInFlight
			MICR A 0 A_SetArg(0,6)
			MICR A 0 A_SeekerMissile(0,2)
			MICR A 1 bright A_Weave(2,2,-1.5,-1.5)
			MICR A 0 A_SetArg(0,args[0] - 1)
			MICR A 0 A_JumpIf(args[0] <= 0,"Spawn")
			goto Spawn+2
	}
}

actor CrusaderMissile04 : CrusaderMissile02
{
	weaveindexxy 48
	weaveindexz 32
	states
	{
		Spawn:
			MICR A 0 A_RocketInFlight
			MICR A 0 A_SetArg(0,6)
			MICR A 0 A_SeekerMissile(0,2)
			MICR A 1 bright A_Weave(2,2,-1.5,1.5)
			MICR A 0 A_SetArg(0,args[0] - 1)
			MICR A 0 A_JumpIf(args[0] <= 0,"Spawn")
			goto Spawn+2
	}
}

actor FastFlameMissile0 : FastFlameMissile
{
	obituary "%o was swept away by %k."
	damage (2 * random(1,4) * ACS_ExecuteWithResult(455,5) / 100)
	-STRIFEDAMAGE
	+MTHRUSPECIES
	states
	{
		Spawn:
			FRBL A 0
			FRBL A 0 A_ChangeVelocity(0,0,1)
			FRBL AB 3 bright
			FRBL C 3 bright A_Countdown
			goto Spawn+2
		Crash:
		XDeath:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Strife4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Strife2")
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",5 * ACS_ExecuteWithResult(455,4) / 10)
			goto Super::Death
		Strife2:
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",ACS_ExecuteWithResult(455,4))
			goto Super::Death
		Strife4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Strife8")
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",2 * ACS_ExecuteWithResult(455,4))
			goto Super::Death
		Strife8:
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",4 * ACS_ExecuteWithResult(455,4))
			goto Super::Death
	}
}

actor FastFlameMissile02 : FastFlameMissile0
{
	scale 1.25
	radius 10
	height 13
	+DONTREFLECT
	states
	{
		Death:
			FRBL D 0 A_Explode(random(4,6) * ACS_ExecuteWithResult(455,4) / 100,32,0)
			FRBL D 5 bright A_FlameDie
			FRBL EFGHI 5 bright
			stop
		Crash:
		XDeath:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Strife4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Strife2")
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",ACS_ExecuteWithResult(455,4))
			goto Death
		Strife2:
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",2 * ACS_ExecuteWithResult(455,4))
			goto Death
		Strife4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Strife8")
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",4 * ACS_ExecuteWithResult(455,4))
			goto Death
		Strife8:
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",8 * ACS_ExecuteWithResult(455,4))
			goto Death
	}
}

actor CrusaderMorph : MonsterMorph
{
	tag "Crusader"
	inventory.icon "ARTIROB2"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("CrusaderHitboxCheck",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(450,0,1,3)
			stop
	}
}

actor CrusaderMorphDirect : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,1,3)
			stop
	}
}

actor CrusaderHitboxCheck : BaseHitboxCheck
{
	height 64
	radius 40
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_GiveToTarget("CrusaderMorphDirect",1)
			stop
	}
}

actor CrusaderMorpher : MonsterMorpher { powerup.type MorphRob2 }

actor PowerMorphRob2 : PowerMorphStrife { powermorph.playerclass "CrusaderClass" }

actor CrusaderMorpherHitbox : MonsterMorpher { powerup.type MorphRob2Hitbox }

actor PowerMorphRob2Hitbox : PowerMorphStrife { powermorph.playerclass "CrusaderClassHitbox" }