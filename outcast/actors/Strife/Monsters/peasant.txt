
actor PeasantClass : MonsterPawnStrife
{
	player.morphweapon "PeasantWeapon"
	health 31
	player.maxhealth 31
	gibhealth 16
	speed 1.2
	player.soundclass "peas"
	+NOTARGET
	states
	{
		Spawn:
			PEAS A 10
			loop
		See:
			PEAS ABCD 10
			loop
		Pain:
			PEAS G 6 A_PlaySound("peasant/pain",2)
			goto Spawn
		Melee:
			PEAS E 6
			goto Missile+1
		Missile:
			PEAS E 8
			PEAS F 10
			goto Spawn
		Death:
    		PEAS G 5
    		PEAS H 5 A_PlayerScream
    		PEAS I 6
    		PEAS J 5 A_NoBlocking
    		PEAS KLMN 5
    		PEAS O 1400
    		PEAS X 5
    		PEAS Y 1400
			FDMN T -1
    		stop
  		XDeath:
    		PEAS R 5 A_TossGib
    		PEAS S 5 A_PlaySound("strifegib",2)
    		PEAS T 5 A_NoBlocking
    		PEAS UVW 4 A_TossGib
    		Goto Death+9
		Burn:
			BURN A 3 Bright A_PlaySound("human/imonfire",2)
			BURN BC 3 Bright A_SpawnItemEx("FireDroplet",0,0,24,frandom(-1.0,1.0),frandom(-1.0,1.0),frandom(0.1,1.0),0,32)
			BURN D 3 Bright A_NoBlocking
			BURN E 5 Bright A_SpawnItemEx("FireDroplet",0,0,24,frandom(-1.0,1.0),frandom(-1.0,1.0),frandom(0.1,1.0),0,32)
			BURN FGH 5 Bright
			BURN IJ 5 Bright A_SpawnItemEx("FireDroplet",0,0,24,frandom(-1.0,1.0),frandom(-1.0,1.0),frandom(0.1,1.0),0,32)
			BURN KL 5 Bright
			BURN MN 5 Bright A_SpawnItemEx("FireDroplet",0,0,24,frandom(-1.0,1.0),frandom(-1.0,1.0),frandom(0.1,1.0),0,32)
			BURN OPQPQ 5 Bright
			BURN RSTU 7 Bright
			BURN V -1
			stop
		Disintegrate:
			DISR A 5 A_PlaySound("misc/disruptordeath",2)
			DISR BC 5
			DISR D 5 A_NoBlocking
			DISR EF 5
			DISR GHIJ 4
			MEAT D 700
			MEAT D 10 A_CheckPlayerDone
			wait
	}
}

actor BeggarClass : PeasantClass
{
	player.morphweapon "BeggarWeapon"
	player.soundclass "beggar"
	states
	{
		Spawn:
			BEGR A 10
			loop
		See:
			BEGR ABD 8
			loop
		Pain:
			BEGR A 6 A_PlaySound("peasant/pain",2)
			goto Spawn
		Missile:
			BEGR E 10
			BEGR F 12
			goto Spawn
		Death:
    		BEGR A 4
    		BEGR H 4 A_PlayerScream
    		BEGR I 4
    		BEGR J 4 A_NoBlocking
    		BEGR KLMN 4
    		BEGR O -1
    		Stop
  		XDeath:
    		PEAS R 5 A_TossGib
    		PEAS S 5 A_PlaySound("strifegib",2)
    		PEAS T 5 A_NoBlocking
    		PEAS UVW 4 A_TossGib
    		Goto Super::Death+9
	}
}

actor PeasantWeapon : BaseMonsterWeapon
{
	obituary "%o should have never picked a fight with %k."
	weapon.sisterweapon "PeasantWeaponPowered"
	+WEAPON.NOALERT
	tag "Fists"
	states
	{
		Select:
			WPS1 L 1 A_Raise
			wait
		Deselect:
			WPS1 L 1 A_Lower
			wait
		Ready:
			WPS1 L 10
			WPS1 L 1 SetPlayerProperty(0,1,1)
			WPS1 L 1 A_WeaponReady
			wait
		Fire:
			WPS1 MN 4
			WPS1 O 0 A_FireCustomMissile("PeasantMeleeProj",0,0,0,9)
			WPS1 O 4 A_JumpIfInventory("BerserkTracker",1,3)
			WPS1 N 4
			WPS1 M 4 A_ReFire
			goto Ready+2
			WPS1 O 4 A_FireCustomMissile("PeasantMeleeProj",0,0,0,9)
			WPS1 N 4
			WPS1 M 4 A_ReFire
			goto Ready+2
		Hold:
			WPS1 P 4
			goto Fire+1
	}
}

actor PeasantWeaponPowered : PeasantWeapon
{
	weapon.sisterweapon "PeasantWeapon"
	+WEAPON.POWERED_UP
	states
	{
		Fire:
			WPS1 PPP 4 A_GunFlash
			WPS1 PP 2 A_GunFlash
			WPS1 N 4
			WPS1 O 0 A_PlaySound("peasant/active",CHAN_WEAPON)
			WPS1 O 0 A_FireCustomMissile("PeasantMeleeProj2",0,0,0,9)
			WPS1 O 4 A_JumpIfInventory("BerserkTracker",1,1)
			goto FireDone
			WPS1 O 4 A_FireCustomMissile("PeasantMeleeProj2",0,0,0,9)
		FireDone:
			WPS1 O 1 offset(-20,42)
			WPS1 O 1 offset(-40,52)
			WPS1 O 1 offset(-60,62)
			WPS1 O 1 offset(-80,80)
			WPS1 O 1 offset(-100,92)
			WPS1 O 1 offset(-120,102)
			TNT1 A 10 offset(0,32)
			TNT1 A 1 A_WeaponReady
			WPS1 L 1 offset(0,57)
			WPS1 L 1 offset(0,52)
			WPS1 L 1 offset(0,47)
			WPS1 L 1 offset(0,42)
			WPS1 L 1 offset(0,37)
			goto Ready
	}
}

actor BeggarWeapon : BaseMonsterWeapon
{
	obituary "%o was beaten to death by the poor %k."
	weapon.sisterweapon "BeggarWeaponPowered"
	+WEAPON.NOALERT
	tag "Broken Bottle"
	states
	{
		Select:
			WPS1 T 1 A_Raise
			wait
		Deselect:
			WPS1 T 1 A_Lower
			wait
		Ready:
			WPS1 T 10
			WPS1 T 1 SetPlayerProperty(0,1,1)
		IsReady:
			WPS1 T 1 A_WeaponReady
			wait
		Fire:
			WPS1 UV 6
			WPS1 W 0 A_FireCustomMissile("BeggarMeleeProj",0,0,0,9)
			WPS1 W 8 A_JumpIfInventory("BerserkTracker",1,1)
			goto IsReady
			WPS1 W 8 A_FireCustomMissile("BeggarMeleeProj",0,0,0,9)
			goto IsReady
			
	}
}

actor BeggarWeaponPowered : BeggarWeapon
{
	weapon.sisterweapon "BeggarWeapon"
	+WEAPON.POWERED_UP
	states
	{
		Fire:
			WPS1 U 3
			WPS1 U 3 A_PlaySound("FighterHammerContinuous",CHAN_WEAPON)
			WPS1 V 6 
			WPS1 W 4 offset(-8,40) A_FireCustomMissile("BeggarBottle",0,0,0,9)
			WPS1 W 1 offset(-16,48)
			WPS1 W 1 offset(-24,56)
			WPS1 W 1 offset(-32,64)
			WPS1 W 1 offset(-40,72)
			WPS1 W 1 offset(-48,80)
			WPS1 W 1 offset(-56,88)
			WPS1 W 1 offset(-64,96)
			TNT1 A 7 offset(0,32)
			TNT1 A 6 A_WeaponReady
			WPS1 T 1 offset(0,68)
			WPS1 T 1 offset(0,62)
			WPS1 T 1 offset(0,56)
			WPS1 T 1 offset(0,50)
			WPS1 T 1 offset(0,44)
			WPS1 T 1 offset(0,38)
			WPS1 T 1 offset(0,32)
			goto IsReady
	}
}

actor PeasantMeleeProj : StandardMeleeProj
{
	obituary "%o should have never picked a fight with %k."
	states
	{
		Death:
			TNT1 A 0 A_PlaySound("misc/metalhit")
			TNT1 A 1 A_SpawnItem("StrifeSpark")
			stop
		Crash:
		XDeath:
			TNT1 A 0 A_PlaySound("misc/meathit")
			TNT1 A 0 A_SetArg(3,135 * ACS_ExecuteWithResult(455,4) / 100)
			goto Strife1
	}
}

actor PeasantMeleeProj2 : PeasantMeleeProj
{
	speed 100
	damage (12 * random(1,8) * ACS_ExecuteWithResult(455,5) / 100)
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_PlaySound("FighterPunchMiss")
			stop
		Death:
			TNT1 A 0 A_PlaySound("FighterPunchHitWall")
			TNT1 A 1 A_SpawnItem("StrifeSpark")
			stop
		Crash:
		XDeath:
			TNT1 A 0 A_PlaySound("FighterPunchHitThing")
			TNT1 A 0 A_SetArg(3,54 * ACS_ExecuteWithResult(455,4) / 10)
			goto Strife1
	}
}

actor BeggarMeleeProj : StandardMeleeProj
{
	obituary "%o was beaten to death by the poor %k."
	damage (15 * random(1,8) * ACS_ExecuteWithResult(455,5) / 400)
	deathsound "beggar/bottlehit"
	states
	{
		Death:
			TNT1 A 1
			stop
		Crash:
		XDeath:
			TNT1 A 0 A_PlaySound("misc/meathit")
			TNT1 A 0 A_SetArg(3,169 * ACS_ExecuteWithResult(455,4) / 100)
			goto Strife1
	}
}

actor BeggarBottle
{
	PROJECTILE
	+MTHRUSPECIES
	-NOGRAVITY
	damage (6 * random(1,8) * ACS_ExecuteWithResult(455,5) / 100)
	gravity 0.6
	radius 12
	height 10
	scale 0.7
	speed 28
	fastspeed 35
	deathsound "beggar/bottlehit"
	states
	{
		Spawn:
			BBTL A 0
			BBTL A 0 A_ChangeVelocity(0,0,2.25)
			BBTL ABCD 3
			goto Spawn+2
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,27 * ACS_ExecuteWithResult(455,4) / 10)
			goto Strife1
		Strife1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Strife4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Strife2")
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3])
			goto Death
		Strife2:
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 2)
			goto Death
		Strife4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Strife8")
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 4)
			goto Death
		Strife8:
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 8)
			goto Death
		Death:
			TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("BeggarBottleBit",random(-8,8),random(-8,8),random(0,8),frandom(-5.0,5.0),frandom(-5.0,5.0),frandom(0.0,2.0),0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION,16)
			TNT1 A 1
			stop
	}
}

actor BeggarBottleBit : SerpentGib1
{
	+CLIENTSIDEONLY
	-NOGRAVITY
	+NOTELEPORT
	+CANNOTPUSH
	+DROPOFF
	radius 1
	height 2
	states
	{
		Spawn:
			BBTL E 0
			BBTL E 140 A_Jump(170,"Spawn2","Spawn3")
			goto Rand
		Spawn2:
			BBTL F 140
			goto Rand
		Spawn3:
			BBTL G 140
			goto Rand
		Rand:
			BBTL "#" 1 A_SetArg(0,12)
			BBTL "#" 4 A_Jump(32,1)
			BBTL "#" 0 A_SetArg(0,args[0] - 1)
			BBTL "#" 1 A_JumpIf(args[0] < 0,"Fade")
			goto Rand+1
		Fade:
			"----" AA 10 A_SinkGib
			stop
	}
}

actor Peasant1Morph : CustomInventory
{
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.AUTOACTIVATE
	states
	{
		Pickup:
			TNT1 A 0 A_Jump(128,"Beggar")
			TNT1 A 0 A_GiveInventory("PeasantMorph",1)
			stop
		Beggar:
			TNT1 A 0 A_GiveInventory("BeggarMorph",1)
			stop
	}
}

actor PeasantMorph : MonsterMorph
{
	tag "Peasant"
	inventory.icon "ARTIPEAS"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("PeasantHitboxCheck",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(450,0,0,0)
			stop
	}
}

actor PeasantMorphDirect : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,0,0)
			stop
	}
}

actor PeasantHitboxCheck : BaseHitboxCheck
{
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_GiveToTarget("PeasantMorphDirect",1)
			stop
	}
}

actor BeggarMorph : MonsterMorph
{
	tag "Beggar"
	inventory.icon "ARTIBEGR"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("BeggarHitboxCheck",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(450,0,2,0)
			stop
	}
}

actor BeggarMorphDirect : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,2,0)
			stop
	}
}

actor BeggarHitboxCheck : BaseHitboxCheck
{
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_GiveToTarget("BeggarMorphDirect",1)
			stop
	}
}

actor PeasantMorpher : MonsterMorpher { powerup.type MorphPeas }

actor PowerMorphPeas : PowerMorphStrife { powermorph.playerclass "PeasantClass" }

actor BeggarMorpher : MonsterMorpher { powerup.type MorphBegr }

actor PowerMorphBegr : PowerMorphStrife { powermorph.playerclass "BeggarClass" }