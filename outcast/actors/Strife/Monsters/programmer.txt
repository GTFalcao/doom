
actor ProgrammerClass : MonsterPawnStrife
{
	player.morphweapon "ProgrammerWeapon"
	health 1100
	mass 1000
	player.maxhealth 1100
	painchance 100
	speed 0.4
	bloodtype "StrifeSpark1"
	player.soundclass "prog"
	states
	{
		Spawn:
			PRGR E -1
			stop
		Init:
			PRGR A 32
			PRGR BCD 5
			goto Spawn
		See:
			PRGR EF 3
			loop
		Pain:
			PRGR K 6 A_PlaySound("programmer/pain",2)
			goto Spawn
		Missile:
			PRGR FE 3
			goto Spawn
		Melee:
			PRGR GH 5
			PRGR IJ 5 bright
			goto Spawn
		Death:
			PRGR L 0 A_ChangeFlag("NOBLOOD",1)
			PRGR L 7 Bright A_TossGib
			PRGR M 7 Bright A_PlaySound("programmer/death",CHAN_VOICE,1.0,0,ATTN_NONE)
			PRGR N 7 Bright A_TossGib
			PRGR O 7 Bright A_NoBlocking
			PRGR P 7 Bright A_TossGib
			PRGR Q 7 Bright A_SpawnItemEx("ProgrammerBase",0,0,24,frandom(-2.0,2.0),frandom(-2.0,2.0),frandom(1.0,2.0),0,SXF_NOCHECKPOSITION)
			PRGR R 7 Bright
			PRGR S 6 Bright
			PRGR TUVW 5
			PRGR X -1
			Stop
	}
}

actor ProgrammerClassHitbox : ProgrammerClass
{
	height 84
	radius 56
	player.viewheight 72
	player.attackzoffset 24
}

actor SpectralLightningSpot0
{
	PROJECTILE
	+SEEKERMISSILE
	+DONTREFLECT
	+MTHRUSPECIES
	+STEPMISSILE
	-NOGRAVITY
	radius 1
	height 8
	renderstyle translucent
	alpha 0.6
	speed 0
	reactiontime 1
	states
	{
		Spawn:
			ZAP5 A 0
			ZAP5 A 1 A_Fire(8)
			ZAP5 A 1 A_SetGravity(3.0)
			ZAP5 A 4 A_JumpIf(waterlevel > 2,"Destroy")
			wait
		Destroy:
			ZAP5 A 0 A_Countdown
			goto Death
		Death:
			ZAP5 A 0 A_SetGravity(1.0)
			ZAP5 A 0 A_SetArg(2,70)
			ZAP5 A 0 A_SetArg(1,random(-40,40))
			ZAP5 A 0 A_SetArg(0,random(-40,40))
			ZAP5 A 0 A_SetAngle(angle + (15 * random(-6,6)))
			ZAP5 A 4 bright A_SetArg(2, args[2] - 1)
			ZAP5 B 0 A_JumpIf(args[2] <= 0, "Death2")
			ZAP5 B 0 A_ChangeVelocity(args[0] * 0.05,args[1] * 0.05,0,CVF_REPLACE)
			ZAP5 B 0 A_CustomMissile("SpectralLightningV10",(ceilingz - z) - 32,random(-3,3) * 50,0,CMF_AIMDIRECTION|CMF_TRACKOWNER,-90)
			ZAP5 B 4 bright A_CustomMissile("SpectralLightningV20",(ceilingz - z) - 32,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER,-90)
			ZAP5 CD 4 bright A_SetArg(2, args[2] - 1)
			goto Death+2
		Death2:
			ZAP5 B 4 bright
			stop
	}
}

actor SpectralLightningSpot02 : SpectralLightningSpot0
{
	states
	{
		Death:
			ZAP5 A 0 A_SetGravity(1.0)
			ZAP5 A 0 A_SetArg(2,64)
			ZAP5 A 0 A_SetArg(1,random(-40,40))
			ZAP5 A 0 A_SetArg(0,random(-40,40))
			ZAP5 A 0 A_SetAngle(angle + (15 * random(-6,6)))
			ZAP5 D 0 A_CustomMissile("SpectralLightningV11",(ceilingz - z) - 13,random(-40,40),0,CMF_AIMDIRECTION|CMF_TRACKOWNER,random(-90,-65))
			ZAP5 A 4 bright A_SetArg(2, args[2] - 1)
			ZAP5 B 0 A_JumpIf(args[2] <= 0, "Death2")
			ZAP5 B 0 A_ChangeVelocity(args[0] * 0.05,args[1] * 0.05,0,CVF_REPLACE)
			ZAP5 A 0 A_SetAngle(angle + (15 * random(-6,6)))
			ZAP5 B 0 A_CustomMissile("SpectralLightningV11",(ceilingz - z) - 13,random(-40,40),0,CMF_AIMDIRECTION|CMF_TRACKOWNER,random(-90,-65))
			ZAP5 B 4 bright A_CustomMissile("SpectralLightningV20",(ceilingz - z) - 32,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER,-90)
			ZAP5 A 0 A_SetAngle(angle + (15 * random(-6,6)))
			ZAP5 D 0 A_CustomMissile("SpectralLightningV11",(ceilingz - z) - 13,random(-40,40),0,CMF_AIMDIRECTION|CMF_TRACKOWNER,random(-90,-65))
			ZAP5 C 4 bright A_SetArg(2, args[2] - 1)
			ZAP5 A 0 A_SetAngle(angle + (15 * random(-6,6)))
			ZAP5 D 0 A_CustomMissile("SpectralLightningV11",(ceilingz - z) - 13,random(-40,40),0,CMF_AIMDIRECTION|CMF_TRACKOWNER,random(-90,-65))
			ZAP5 D 4 bright A_SetArg(2, args[2] - 1)
			goto Death+2
	}
}

actor SpectralLightningV10
{
	PROJECTILE
	+MTHRUSPECIES
	renderstyle add
	seesound "weapons/sigil"
	deathsound "weapons/sigilhit"
	speed 18
	radius 8
	height 24
	damage (20 * random(1,4) * ACS_ExecuteWithResult(455,5) / 100)
	states
	{
		Spawn:
			ZOT1 AB 4 bright
			ZOT1 CDE 6 bright
			loop
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,5 * ACS_ExecuteWithResult(455,4))
			goto Strife1
		Strife1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Strife4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Strife2")
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3])
			goto Death
		Strife2:
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 2)
			goto Death
		Strife4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Strife8")
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 4)
			goto Death
		Strife8:
			TNT1 A 0 A_GiveToTarget("StrifeDamagePoint",args[3] * 8)
			goto Death
		Death:
			ZAP1 E 3 bright
			ZAP1 DCB 2 bright
			ZAP1 A 1 bright
			Stop
	}
}

actor SpectralLightningV11 : SpectralLightningV10
{
	speed 20
	height 12
	+DONTREFLECT
	+FOILINVUL
	states
	{
		Spawn:
			ZOT3 ABCDE 4 bright
			loop
		Death:
			ZAP1 BABCDEFE 3 bright
			ZAP1 DCB 2 bright
			ZAP1 A 1 bright
			stop
	}
}

actor SpectralLightningV20 : SpectralLightningV10
{
	damage (10 * random(1,4) * ACS_ExecuteWithResult(455,5) / 100)
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,25 * ACS_ExecuteWithResult(455,4) / 10)
			goto Strife1
	}
}

actor ProgrammerWeapon : BaseMonsterWeaponFlight
{
	obituary "%o was deleted by %k."
	weapon.ammotype1 "MonsterManaBlue"
	weapon.ammotype2 "MonsterManaGreen"
	weapon.sisterweapon "ProgrammerWeaponPowered"
	tag "Sigil 1"
	+WEAPON.NOALERT
	+WEAPON.DONTBOB
	states
	{
		Ready:
			TNT1 A 50
			TNT1 A 0 A_TakeInventory("PowerInvulnerable",1)
			TNT1 A 0 SetPlayerProperty(0,0,0)
			TNT1 A 0 SetPlayerProperty(0,0,1)
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,1) == 0,2)
			TNT1 A 0 A_GiveInventory("MonFlight",1)
			TNT1 A 1 A_WeaponReady
			wait
		Fire:
			WPS3 N 0 A_GunFlash
			WPS3 N 0 A_AlertMonsters
			WPS3 N 7 A_PlaySound("weapons/sigilcharge",CHAN_WEAPON)
			WPS3 O 5 A_TakeInventory("MonsterManaBlue",10)
			WPS3 P 5 A_TakeInventory("MonsterManaGreen",10)
			WPS3 Q 4
			WPS3 Q 0 A_PlaySound("weapons/sigilcharge",CHAN_WEAPON)
			WPS3 Q 0 A_LightInverse
			WPS3 Q 8 bright A_FireCustomMissile("SpectralLightningSpot0",0,0,0,9)
			WPS3 Q 4 A_Light0
			WPS3 Q 1 offset(0,38)
			WPS3 Q 1 offset(0,44)
			WPS3 Q 1 offset(0,50)
			WPS3 Q 1 offset(0,56)
			WPS3 Q 1 offset(0,62)
			WPS3 Q 1 offset(0,68)
			WPS3 Q 1 offset(0,74)
			WPS3 Q 1 offset(0,80)
			TNT1 A 180 A_JumpIfInventory("MonsterManaBlue",10,2)
			TNT1 A 49
			goto Ready+6
			TNT1 A 180 A_JumpIfInventory("MonsterManaGreen",10,1)
			TNT1 A 49
			goto Ready+6
		AltFire:
			TNT1 A 0 A_PlaySound("programmer/clank",CHAN_WEAPON)
			TNT1 A 0 A_FireCustomMissile("ProgrammerMeleeProj",0,0,0,9)
			TNT1 A 9 A_JumpIfInventory("BerserkTracker",1,1)
			goto Ready+6
			TNT1 A 9 A_FireCustomMissile("ProgrammerMeleeProj",0,0,0,9)
			goto Ready+6
	}
}

actor ProgrammerWeaponPowered : ProgrammerWeapon
{
	weapon.sisterweapon "ProgrammerWeapon"
	tag "Sigil 1 Powered"
	+WEAPON.POWERED_UP
	states
	{
		AltFire:
		Fire:
			WPS3 N 0 A_GunFlash
			WPS3 N 0 A_AlertMonsters
			WPS3 N 7 A_PlaySound("weapons/sigilcharge",CHAN_WEAPON)
			WPS3 O 5 A_TakeInventory("MonsterManaBlue",25)
			WPS3 P 5 A_TakeInventory("MonsterManaGreen",25)
			WPS3 Q 4
			WPS3 Q 0 A_PlaySound("weapons/sigilcharge",CHAN_WEAPON)
			WPS3 Q 0 A_LightInverse
			WPS3 Q 8 bright A_FireCustomMissile("SpectralLightningSpot02",0,0,0,9)
			WPS3 Q 4 A_Light0
			WPS3 Q 1 offset(0,38)
			WPS3 Q 1 offset(0,44)
			WPS3 Q 1 offset(0,50)
			WPS3 Q 1 offset(0,56)
			WPS3 Q 1 offset(0,62)
			WPS3 Q 1 offset(0,68)
			WPS3 Q 1 offset(0,74)
			WPS3 Q 1 offset(0,80)
			TNT1 A 220 A_JumpIfInventory("MonsterManaBlue",25,2)
			TNT1 A 69
			goto Ready+6
			TNT1 A 220 A_JumpIfInventory("MonsterManaGreen",25,1)
			TNT1 A 69
			goto Ready+6
	}
}

actor ProgrammerMeleeProj : ReaverMeleeProj { obituary "%o was deleted by %k." }

actor ProgrammerMorph : MonsterMorph
{
	tag "Programmer"
	inventory.icon "ARTIPRGR"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("ProgrammerHitboxCheck",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(450,0,3,2)
			stop
	}
}

actor ProgrammerMorphDirect : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,3,2)
			stop
	}
}

actor ProgrammerHitboxCheck : BaseHitboxCheck
{
	height 84
	radius 56
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_GiveToTarget("ProgrammerMorphDirect",1)
			stop
	}
}

actor ProgrammerMorpher : MonsterMorpher { powerup.type MorphPrgr }

actor PowerMorphPrgr : PowerMorphStrife { powermorph.playerclass "ProgrammerClass" }

actor ProgrammerMorpherHitbox : MonsterMorpher { powerup.type MorphPrgrHitbox }

actor PowerMorphPrgrHitbox : PowerMorphStrife { powermorph.playerclass "ProgrammerClassHitbox" }