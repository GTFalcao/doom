
// Mana

actor MonsterManaBlue : Ammo replaces Mana1
{
	//radius 8
	//height 8
	inventory.amount 45
	inventory.maxamount 600
	ammo.backpackamount 60
	ammo.backpackmaxamount 1000
	inventory.icon "MAN1A0"
	inventory.pickupmessage "Blue Mana"
	inventory.pickupsound "*fist"
	+FLOATBOB
	+DONTGIB
	states
	{
		Spawn:
			MAN1 ABCDEFGHI 4 bright
			loop
	}
}

actor MonsterManaGreen : Ammo replaces Mana2
{
	//radius 8
	//height 8
	inventory.amount 45
	inventory.maxamount 600
	ammo.backpackamount 60
	ammo.backpackmaxamount 1000
	inventory.icon "MAN2A0"
	inventory.pickupmessage "Green Mana"
	inventory.pickupsound "*fist"
	+FLOATBOB
	+DONTGIB
	states
	{
		Spawn:
			MAN2 ABCDEFGHIJKLMNOP 4 bright
			loop
	}
}

actor MonsterManaCombined : CustomInventory replaces Mana3
{
	//radius 8
	//height 8
	inventory.pickupmessage "Combined Mana"
	inventory.pickupsound "*fist"
	+FLOATBOB
	states
	{
		Spawn:
			MAN3 ABCDEFGHIJKLMNOP 4 bright
			loop
		Pickup:
			TNT1 A 0 A_GiveInventory("MonsterManaBlue",60)
			TNT1 A 0 A_GiveInventory("MonsterManaGreen",60)
			stop
	}
}

actor MonsterManaDropped : MonsterManaCombined
{
	states
	{
		Pickup:
			TNT1 A 0 A_GiveInventory("MonsterManaBlue",40)
			TNT1 A 0 A_GiveInventory("MonsterManaGreen",40)
			stop
	}
}

// The base for all morphed monsters.

actor MonsterPawn : PlayerPawn
{
	species "MonsterMorph"
	damagefactor "slime", 0
	player.jumpz 9		// Hexen compatibility
	dropitem "MonsterManaDropped"
	+THRUSPECIES
	+NOSKIN
	+DONTHARMSPECIES
	+NOICEDEATH
	//+CANSUPERMORPH
	//+CROUCHABLEMORPH
}

// The base for inventory items that morph you into a monster.

actor MonsterMorph : CustomInventory
{
	inventory.maxamount 1
	+INVENTORY.UNDROPPABLE
	+INVENTORY.INVBAR
	inventory.usesound ""
	inventory.pickupmessage "Picked up a morph item, so you're probably cheating."
	inventory.icon "ARTIPOSS"
	states
	{
		Spawn:
			TNT1 A -1
			stop
		Use:
			TNT1 A 0 ACS_ExecuteAlways(450,0,0)
			stop
	}
}

// The base for the items responsible for directly morphing you into a monster:
// used when playing with real hitboxes by the projectile that checks for room.

actor MonsterMorphDirect : CustomInventory
{
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.AUTOACTIVATE
	inventory.pickupsound ""
	inventory.pickupmessage ""
	states
	{
		Spawn:
			TNT1 A -1
			stop
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,0)
			stop
	}
}

actor BaseHitboxCheck
{
	height 56
	radius 16
	+SOLID
	+MISSILE
	+MTHRUSPECIES
	states
	{
		Spawn:
			TNT1 A 1
			stop
		Death:
			TNT1 A 1 A_GiveToTarget("TooFatToMorphHere",1)
			stop
	}
}

actor TooFatToMorphHere : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(463)
			stop
	}
}

// The actual item given to you by ACS when you use the custom inventory items.

actor MonsterMorpher : PowerupGiver
{
	powerup.duration 0x7FFFFFFD
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	powerup.type MorphPoss
}

// Terminator sphere

actor IsTerminator : inventory { }

actor TerminatorSphere0 : CustomInventory replaces Terminator
{
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	inventory.pickupsound "none"
	inventory.pickupmessage "terminator!"
	states
	{
		Spawn:
			ARNO AAAAAABBBBBBCCCCCCDDDDDD 1 bright A_SpawnItemEx("TerminatorSpark",0,0,10,frandom(-3.0,3.0),frandom(-3.0,3.0),frandom(2.0,4.0),0,SXF_CLIENTSIDE)
			loop
		Pickup:
			TNT1 A 0 A_GiveInventory("IsTerminator",1)
			TNT1 A 0 ACS_ExecuteAlways(465)
			stop
	}
}

actor TerminatorSpark
{
	+NOBLOCKMAP
	+NOINTERACTION
	+CLIENTSIDEONLY
	renderstyle add
	alpha 0.8
	states
	{
		Spawn:
			ARNO EFG 9 bright
			stop
	}
}

// Monster powers

actor MonRadSuit : PowerupGiver
{
	powerup.type "MonIronFeet"
	powerup.duration 0x7FFFFFFD
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
}

actor PowerMonIronFeet : PowerIronFeet { powerup.color 0, 0, 0, 0 }

actor MonFlight : PowerupGiver
{
	powerup.type "Flight"
	powerup.duration 0x7FFFFFFD
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
}

actor MonGhost : ArtiInvisibility
{
  	+INVENTORY.AUTOACTIVATE
  	+INVENTORY.ALWAYSPICKUP
	powerup.duration 0x7FFFFFFD
}

actor WizGhost : MonGhost { powerup.type "TempGhost" }

actor PowerTempGhost : PowerGhost { }

actor MonShadow : ShadowArmor
{
	-INVENTORY.INVBAR
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	powerup.duration 0x7FFFFFFD
	powerup.strength 60
}

actor Aln1Shadow : MonShadow { powerup.strength 34 }

actor MnalShadow : MonShadow { powerup.strength 50 }

actor MonBlur : BlurSphere
{ powerup.duration 0x7FFFFFFD }

actor MonTempInvul : InvulnerabilitySphere
{
	powerup.color "00 00 00" 0.0
	powerup.duration 0x7FFFFFFD
}

// Other stuff

actor MakeMeShootable : CustomInventory
{
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	states
	{
		Spawn:
			TNT1 A -1
			stop
		Use:
			TNT1 A 0 A_SetShootable
			stop
	}
}

actor SpawnProtection : InvulnerabilitySphere
{
	powerup.color "00 00 60" 0.25
	powerup.duration 0x7FFFFFFD
}

actor SpawnProtectionShort : SpawnProtection { powerup.duration -4 }

actor NoTelefog
{
	+NOBLOCKMAP
  	+NOGRAVITY
	states
	{
		Spawn:
			TNT1 A 1
			stop
	}
}

actor AmmoSatchel0 : AmmoSatchel replaces AmmoSatchel { inventory.pickupsound "*fist" }