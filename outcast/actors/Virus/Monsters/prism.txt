
actor PrismClass : MonsterPawnVirus
{
	player.morphweapon "PrismWeapon"
	bloodtype "PrismShard0"
	health 60
	player.maxhealth 60
	speed 0.85
	mass 80
	player.soundclass "prism"
	states
	{
		Spawn:
			PRSM AB 4
			loop
		See:
			PRSM AB 4
			loop
		Pain:
			PRSM A 4 A_PlaySound("prism/pain",CHAN_VOICE)
			goto Spawn
		Missile:
			PRSM C 6 bright
			PRSM D 8 bright
			goto Spawn
		Melee:
			PRSM E 6 bright
			goto Spawn
		Death:
			PRSM HHHHHH 0 A_SpawnItemEx("PrismShard0",random(-2,2),random(-2,2),random(22,26),random(-5,5),random(-5,5),frandom(-0,1.0),0,SXF_ABSOLUTEVELOCITY)
			PRSM H 6 bright A_PlayerScream
			PRSM I 6 bright A_NoBlocking
			PRSM JK 6 bright
			PRSM L -1
			stop
	}
}

actor PrismClassHitbox : PrismClass
{
	height 32
	radius 10
	player.viewheight 24
	player.attackzoffset -4
}

actor PrismShard0
{
	height 4
	radius 3
	speed 5
	bouncetype Doom
	PROJECTILE
	-NOGRAVITY
	+CANNOTPUSH
	+BOUNCEONACTORS
	states
	{
		Spawn:
			PFRG ABABABAB 6
			PFRG AB 3
			PFRG C 35
			stop
	}
}

actor PrismWeapon : BaseMonsterWeaponFlight
{
	weapon.sisterweapon "PrismWeaponPowered"
	obituary "%o was buzzed by %k."
	weapon.ammotype1 "MonsterManaGreen"
	tag "Prism tripleshot"
	states
	{
		Fire:
			TNT1 A 0 A_TakeInventory("MonsterManaGreen",1)
			TNT1 A 6 A_FireCustomMissile("PrismShot0",0,0,0,9)
			TNT1 A 0 A_TakeInventory("MonsterManaGreen",1)
			TNT1 A 6 A_FireCustomMissile("PrismShot0",0,0,0,9)
			TNT1 A 0 A_GunFlash
			TNT1 A 0 A_TakeInventory("MonsterManaGreen",1)
			TNT1 A 6 A_FireCustomMissile("PrismShot0",0,0,0,9)
			TNT1 A 80 A_JumpIfInventory("MonsterManaGreen",3,1)
			TNT1 A 22
			goto Ready+3
	}
}

actor PrismWeaponPowered : PrismWeapon
{
	weapon.sisterweapon "PrismWeapon"
	tag "Prism streamfire"
	+WEAPON.POWERED_UP
	states
	{
		Fire:
			TNT1 A 0 A_JumpIfInventory("MonsterManaGreen",1,1)
			goto Burst
			TNT1 A 0 A_TakeInventory("MonsterManaGreen",1)
			TNT1 A 6 A_FireCustomMissile("PrismShot0",0,0,0,9)
			TNT1 A 1 A_ReFire
			goto Ready+3
		Burst:
			TNT1 A 0 A_TakeInventory("MonsterManaGreen",1)
			TNT1 A 6 A_FireCustomMissile("PrismShot0",0,0,0,9)
			TNT1 A 0 A_TakeInventory("MonsterManaGreen",1)
			TNT1 A 6 A_FireCustomMissile("PrismShot0",0,0,0,9)
			TNT1 A 0 A_TakeInventory("MonsterManaGreen",1)
			TNT1 A 6 A_FireCustomMissile("PrismShot0",0,0,0,9)
			TNT1 A 36
			goto Ready+3
	}
}

actor PrismShot0
{
	seesound "prism/fire"
	deathsound "prism/firex"
	renderstyle add
	alpha 0.7
	speed 30
	height 8
	radius 4
	damage (2 * random(1,8) * ACS_ExecuteWithResult(455,5) / 100)
	PROJECTILE
	+MTHRUSPECIES
	states
	{
		Spawn:
			PRSH ABCD 4 bright
			loop
		Death:
			PRSH AEF 4 bright
			stop
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,9 * ACS_ExecuteWithResult(455,4) / 10)
			goto Virus1
		Virus1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Virus4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Virus2")
			TNT1 A 0 A_GiveToTarget("VirusDamagePoint",args[3])
			goto Death
		Virus2:
			TNT1 A 0 A_GiveToTarget("VirusDamagePoint",args[3] * 2)
			goto Death
		Virus4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Virus8")
			TNT1 A 0 A_GiveToTarget("VirusDamagePoint",args[3] * 4)
			goto Death
		Virus8:
			TNT1 A 0 A_GiveToTarget("VirusDamagePoint",args[3] * 8)
			goto Death
	}
}

actor PrismMorph : MonsterMorph
{
	tag "Prism"
	inventory.icon "ARTIPRSM"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("PrismHitboxCheck",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(450,0,1)
			stop
	}
}

actor PrismMorphDirect : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,1)
			stop
	}
}

actor PrismHitboxCheck : BaseHitboxCheck
{
	height 16
	radius 8
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_GiveToTarget("PrismMorphDirect",1)
			stop
	}
}

actor PrismMorpher : MonsterMorpher { powerup.type MorphPrsm }

actor PrismMorpherHitbox : MonsterMorpher { powerup.type MorphPrsmHitbox }

actor PowerMorphPrsm : PowerMorphVirus { powermorph.playerclass "PrismClass" }

actor PowerMorphPrsmHitbox : PowerMorphVirus { powermorph.playerclass "PrismClassHitbox" }