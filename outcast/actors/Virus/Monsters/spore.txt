
actor SporeClass : MonsterPawnVirus
{
	player.morphweapon "SporeWeapon"
	health 100
	player.maxhealth 100
	speed 0.75
	player.soundclass "spore"
	states
	{
		Spawn:
			SPOR A -1
			stop
		See:
			SPOR A 8
			loop
		Pain:
			SPOR A 4 A_PlaySound("spore/pain",CHAN_VOICE)
			goto Spawn
		Missile:
			SPOR B 6
			goto Spawn
		Melee:
			SPOR C 8 bright
			goto Spawn
		Death:
			SPOR G 6
			SPOR D 6 A_PlaySound ("spore/buzz")
			SPOR G 5
			SPOR D 6 A_PlaySound ("spore/buzz")
			SPOR G 4
			SPOR D 6 A_PlaySound ("spore/buzz")
			SPOR G 3
			SPOR D 6 A_PlaySound ("spore/buzz")
			SPOR G 2
			SPOR D 6 A_PlaySound ("spore/buzz")
			SPOR G 3
			SPOR D 4 A_PlayerScream
			SPOR E 4 A_NoBlocking
			SPOR E 6
			SPOR F -1
			stop
	}
}

actor SporeClassHitbox : SporeClass
{
	height 32
	radius 16
	player.viewheight 24
	player.attackzoffset -4
}

actor SporeWeapon : BaseMonsterWeaponFlight
{
	weapon.sisterweapon "SporeWeaponPowered"
	obituary "%o fell afoul of infected spore %k."
	weapon.ammotype1 "MonsterManaGreen"
	tag "Blaster"
	states
	{
		Fire:
			TNT1 A 6
			TNT1 A 0 A_GunFlash
			TNT1 A 0 A_TakeInventory("MonsterManaBlue",1)
			TNT1 A 8 A_FireCustomMissile("SporeShot0",0,0,0,9)
			TNT1 A 40 A_JumpIfInventory("MonsterManaBlue",1,1)
			TNT1 A 6
			TNT1 A 1 A_ReFire
			goto Ready+3
	}
}

actor SporeWeaponPowered : SporeWeapon
{
	weapon.sisterweapon "SporeWeapon"
	+WEAPON.POWERED_UP
	states
	{
		Fire:
			TNT1 A 6
			TNT1 A 0 A_GunFlash
			TNT1 A 0 A_TakeInventory("MonsterManaBlue",1)
			TNT1 A 4 A_FireCustomMissile("SporeShot0",0,0,0,9)
			TNT1 A 0 A_GunFlash
			TNT1 A 0 A_TakeInventory("MonsterManaBlue",1)
			TNT1 A 4 A_FireCustomMissile("SporeShot0",0,0,0,9)
			TNT1 A 0 A_GunFlash
			TNT1 A 0 A_TakeInventory("MonsterManaBlue",1)
			TNT1 A 4 A_FireCustomMissile("SporeShot0",0,0,0,9)
			TNT1 A 60 A_JumpIfInventory("MonsterManaBlue",3,1)
			TNT1 A 12
			TNT1 A 1 A_ReFire
			goto Ready+3
	}
}

actor SporeShot0
{
	speed 25
	fastspeed 30
	renderstyle add
	alpha 0.8
	radius 6
	height 10
	damage (4 * random(1,8) * ACS_ExecuteWithResult(455,5) / 100)
	seesound "weapons/phblaster"
	deathsound "weapons/phblasterx"
	obituary "%o fell afoul of infected spore %k."
	PROJECTILE
	+MTHRUSPECIES
	states
	{
		Spawn:
			BLAS AB 6 bright
			loop
		Death:
			BLAS BCDEF 4 bright
			stop
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,18 * ACS_ExecuteWithResult(455,4) / 10)
			goto Virus1
		Virus1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Virus4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Virus2")
			TNT1 A 0 A_GiveToTarget("VirusDamagePoint",args[3])
			goto Death
		Virus2:
			TNT1 A 0 A_GiveToTarget("VirusDamagePoint",args[3] * 2)
			goto Death
		Virus4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Virus8")
			TNT1 A 0 A_GiveToTarget("VirusDamagePoint",args[3] * 4)
			goto Death
		Virus8:
			TNT1 A 0 A_GiveToTarget("VirusDamagePoint",args[3] * 8)
			goto Death
	}
}

actor SporeMorph : MonsterMorph
{
	tag "Spore"
	inventory.icon "ARTISPOR"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("SporeHitboxCheck",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(450,0,2)
			stop
	}
}

actor SporeMorphDirect : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,2)
			stop
	}
}

actor SporeHitboxCheck : BaseHitboxCheck
{
	height 32
	radius 16
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_GiveToTarget("SporeMorphDirect",1)
			stop
	}
}

actor SporeMorpher : MonsterMorpher { powerup.type MorphSpor }

actor SporeMorpherHitbox : MonsterMorpher { powerup.type MorphSporHitbox }

actor PowerMorphSpor : PowerMorphVirus { powermorph.playerclass "SporeClass" }

actor PowerMorphSporHitbox : PowerMorphVirus { powermorph.playerclass "SporeClassHitbox" }