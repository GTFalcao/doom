
actor VirusClass : MonsterPawnVirus
{
	player.morphweapon "VirusWeapon"
	health 500
	player.maxhealth 500
	painchance 0
	speed 0.6
	player.soundclass "virusm"
	states
	{
		Spawn:
			VIRU AB 4
			loop
		See:
			VIRU AB 4
			loop
		Missile:
			VIRU CDE 4 bright
			VIRU F 6 bright
			goto Spawn
		Melee:
			VIRU GHGHIJ 4 bright
			goto Spawn
		Death:
			VIRU A 10 A_PlayerScream
			VIRU K 0 A_SpawnItemEx("VirusBit0",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY)
			VIRU K 4 A_PlaySound("virus/snap")
			VIRU L 3 A_NoBlocking
			VIRU MNOP 3
			VIRU Q -1
			stop
	}
}

actor VirusClassHitbox : VirusClass
{
	height 36
	radius 12
	player.viewheight 32
	player.attackzoffset -24
}

actor VirusWeapon : BaseMonsterWeaponFlight
{
	weapon.sisterweapon "VirusWeaponPowered"
	obituary "%o was infected with %k's virus."
	weapon.ammotype1 "MonsterManaGreen"
	tag "Virus shot"
	states
	{
		Fire:
			TNT1 A 0 ACS_ExecuteAlways(451,0,6,25)
			TNT1 A 0 A_PlaySound("virus/charge",CHAN_WEAPON)
			TNT1 A 0 A_TakeInventory("MonsterManaGreen",1)
			TNT1 AAAA 2 A_FireCustomMissile("VirusFleck",random(0,360),0,random(-1,1),9)
			TNT1 A 0 A_PlaySound("virus/charge",CHAN_WEAPON)
			TNT1 A 0 A_TakeInventory("MonsterManaGreen",1)
			TNT1 AAAA 2 A_FireCustomMissile("VirusFleck",random(0,360),0,random(-1,1),9)
			TNT1 A 0 A_GunFlash
			TNT1 A 0 A_PlaySound("virus/charge",CHAN_WEAPON)
			TNT1 A 0 A_TakeInventory("MonsterManaGreen",1)
			TNT1 AAAA 2 A_FireCustomMissile("VirusFleck",random(0,360),0,random(-1,1),9)
			TNT1 A 0 A_PlaySound("virus/charge",CHAN_WEAPON)
			TNT1 A 0 A_TakeInventory("MonsterManaGreen",1)
			TNT1 AAAAAA 2 A_FireCustomMissile("VirusFleck",random(0,360),0,random(-1,1),9)
			TNT1 A 4 A_FireCustomMissile("VirusShot0",0,0,0,9)
			TNT1 A 2 ACS_ExecuteAlways(451,0,6,60)
			TNT1 A 136 A_JumpIfInventory("MonsterManaGreen",4,1)
			TNT1 A 26
			goto Ready+3
	}
}

actor VirusWeaponPowered : VirusWeapon
{
	weapon.sisterweapon "VirusWeapon"
	+WEAPON.POWERED_UP
	states
	{
		Fire:
			TNT1 A 0 ACS_ExecuteAlways(451,0,6,25)
			TNT1 A 0 A_PlaySound("virus/charge",CHAN_WEAPON)
			TNT1 A 0 A_TakeInventory("MonsterManaGreen",1)
			TNT1 AAAA 1 A_FireCustomMissile("VirusFleck",random(0,360),0,random(-1,1),9)
			TNT1 A 0 A_TakeInventory("MonsterManaGreen",1)
			TNT1 AAAA 1 A_FireCustomMissile("VirusFleck",random(0,360),0,random(-1,1),9)
			TNT1 A 0 A_PlaySound("virus/charge",CHAN_WEAPON)
			TNT1 A 0 A_TakeInventory("MonsterManaGreen",1)
			TNT1 AAAA 1 A_FireCustomMissile("VirusFleck",random(0,360),0,random(-1,1),9)
			TNT1 A 0 A_TakeInventory("MonsterManaGreen",1)
			TNT1 AAAA 1 A_FireCustomMissile("VirusFleck",random(0,360),0,random(-1,1),9)
			TNT1 A 0 A_GunFlash
			TNT1 A 0 A_PlaySound("virus/charge",CHAN_WEAPON)
			TNT1 A 0 A_TakeInventory("MonsterManaGreen",1)
			TNT1 AAAA 1 A_FireCustomMissile("VirusFleck",random(0,360),0,random(-1,1),9)
			TNT1 A 0 A_TakeInventory("MonsterManaGreen",1)
			TNT1 AAAA 1 A_FireCustomMissile("VirusFleck",random(0,360),0,random(-1,1),9)
			TNT1 A 0 A_GunFlash
			TNT1 A 0 A_PlaySound("virus/charge",CHAN_WEAPON)
			TNT1 A 0 A_TakeInventory("MonsterManaGreen",1)
			TNT1 AAAAAA 1 A_FireCustomMissile("VirusFleck",random(0,360),0,random(-1,1),9)
			TNT1 A 0 A_TakeInventory("MonsterManaGreen",1)
			TNT1 AAAAAA 1 A_FireCustomMissile("VirusFleck",random(0,360),0,random(-1,1),9)
			TNT1 A 0 A_FireCustomMissile("VirusShot0",-1,0,0,9)
			TNT1 A 4 A_FireCustomMissile("VirusShot0",1,0,0,9)
			TNT1 A 2 ACS_ExecuteAlways(451,0,6,60)
			TNT1 A 136 A_JumpIfInventory("MonsterManaGreen",4,1)
			TNT1 A 26
			goto Ready+3
	}
}

actor VirusFleck
{
	height 4
	radius 2
	speed 8
	damage (random(1,7) * ACS_ExecuteWithResult(455,5) / 100)
	obituary "%o was infected with %k's virus."
	deathsound "virus/firex"
	renderstyle add
	alpha 0.4
	PROJECTILE
	-NOGRAVITY
	+MTHRUSPECIES
	states
	{
		Spawn:
			FLEK A -1 bright
			stop
		Death:
			FLEK A 6 bright
			stop
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,4 * ACS_ExecuteWithResult(455,4) / 10)
			goto Virus1
		Virus1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Virus4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Virus2")
			TNT1 A 0 A_GiveToTarget("VirusDamagePoint",args[3])
			goto Death
		Virus2:
			TNT1 A 0 A_GiveToTarget("VirusDamagePoint",args[3] * 2)
			goto Death
		Virus4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Virus8")
			TNT1 A 0 A_GiveToTarget("VirusDamagePoint",args[3] * 4)
			goto Death
		Virus8:
			TNT1 A 0 A_GiveToTarget("VirusDamagePoint",args[3] * 8)
			goto Death
   }
}

actor VirusShot0 : VirusFleck
{
	speed 55
	alpha 0.6
	radius 10
	height 8
	damage (14 * random(1,8) * ACS_ExecuteWithResult(455,5) / 100)
	seesound "virus/fire"
	deathsound ""
	+NOGRAVITY
	+DONTREFLECT
	states
	{
		Spawn:
			VSHT AB 4 bright
			loop
		Death:
			VSHT AB 5 bright
			stop
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,63 * ACS_ExecuteWithResult(455,4) / 10)
			goto Virus1
		Virus1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Virus4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Virus2")
			TNT1 A 0 A_GiveToTarget("VirusDamagePoint",args[3])
			goto Death
		Virus2:
			TNT1 A 0 A_GiveToTarget("VirusDamagePoint",args[3] * 2)
			goto Death
		Virus4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Virus8")
			TNT1 A 0 A_GiveToTarget("VirusDamagePoint",args[3] * 4)
			goto Death
		Virus8:
			TNT1 A 0 A_GiveToTarget("VirusDamagePoint",args[3] * 8)
			goto Death
	}
}

actor VirusBit0 : DroneBit0
{
	speed 0
	states
	{
		Spawn:
			VBIT A -1
			stop
		Death:
			VBIT A -1
			stop
	}
}

actor VirusMorph : MonsterMorph
{
	tag "Virus"
	inventory.icon "ARTIVIRU"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("VirusHitboxCheck",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(450,0,5)
			stop
	}
}

actor VirusMorphDirect : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,5)
			stop
	}
}

actor VirusHitboxCheck : BaseHitboxCheck
{
	height 36
	radius 12
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_GiveToTarget("VirusMorphDirect",1)
			stop
	}
}

actor VirusMorpher : MonsterMorpher { powerup.type MorphViru }

actor VirusMorpherHitbox : MonsterMorpher { powerup.type MorphViruHitbox }

actor PowerMorphViru : PowerMorphVirus { powermorph.playerclass "VirusClass" }

actor PowerMorphViruHitbox : PowerMorphVirus { powermorph.playerclass "VirusClassHitbox" }