
actor AirCarrierClass : MonsterPawnVirus
{
	player.morphweapon "AirCarrierWeapon"
	health 600
	player.maxhealth 600
	mass 600
	speed 0.5
	player.soundclass "chair"
	states
	{
		Spawn:
			ACAR FFFF 1 A_JumpIfInventory("artisummonturret",1,"Spawn2")
			ACAR F -1
			stop
		Spawn2:
			ACAR A 6
			goto Spawn
		See:
			ACAR FFFF 1 A_JumpIfInventory("artisummonturret",1,"See2")
			ACAR F -1
			stop
		See2:
			ACAR A 6
			goto See
		Pain:
			ACAR F 0 A_JumpIfInventory("artisummonturret",1,2)
			ACAR F 4 A_PlaySound("aircarrier/pain",CHAN_VOICE)
			goto Spawn
			ACAR A 4 A_PlaySound("aircarrier/pain",CHAN_VOICE)
			goto Spawn
		Missile:
			ACAR IIII 1 bright A_JumpIfInventory("shooting",1,"Drone2")
			ACAR J 6 bright
			goto Spawn
		Melee:
			ACAR B 4 bright
			ACAR C 8 bright
			goto Spawn
		Drone:
			ACAR DED 10
			goto Spawn
		Drone2:
			ACAR GGGG 1 A_JumpIfInventory("artisummonturret",1,"Drone")
			ACAR G 6
			ACAR HG 10
			goto Spawn
		Death:
			PFRG C 0 A_SpawnItemEx("MillDeathExplosion",0,0,32,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY)
			PFRG C 0 A_SpawnItemEx("AirCarrierEngine0",0,24,20,32 * cos(angle),32 * sin(angle),24 * sin(-pitch),0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY)
			PFRG C 0 A_SpawnItemEx("AirCarrierEngine0",0,-24,20,32 * cos(angle),32 * sin(angle),24 * sin(-pitch),0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY)
			PFRG C 2 A_PlayerScream
			PFRG C -1 A_NoBlocking
			stop
	}
}

actor AirCarrierClassHitbox : AirCarrierClass
{
	height 64
	radius 48
	player.attackzoffset 4
}

actor artisummonturret : CustomInventory
{
	inventory.icon "ARTIATUR"
	inventory.usesound "*taunt"
	scale 0.6
	tag "Drop turret"
	+VISIBILITYPULSE
	+INVENTORY.INVBAR
	+INVENTORY.UNDROPPABLE
	states
	{
		Spawn:
			TURR A 8 bright
			loop
		Use:
			TNT1 A 0 ACS_ExecuteAlways(451,0,6,65)
			TNT1 A 0 A_SpawnItemEx("Turret0",0,0,0,0,0,0,angle,SXF_NOCHECKPOSITION|SXF_ABSOLUTEANGLE)
			stop
	}
}

actor dronelimit : inventory { inventory.maxamount 50 }

actor AirCarrierWeapon : BaseMonsterWeaponFlight
{
	obituary "%o was overwhelmed by %k."
	weapon.ammotype1 "MonsterManaBlue"
	tag "Turret, energy shooter and drone summon"
	states
	{
		Ready:
			TNT1 A 10
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,1) == 0,2)
			TNT1 A 0 A_GiveInventory("MonFlight",1)
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,20) > 0,"SpeedMe")
			TNT1 A 0 A_GiveInventory("dronelimit",20)
			TNT1 A 1 A_GiveInventory("artisummonturret",1)
		IsReady:
			TNT1 A 1 A_WeaponReady
			wait
		SpeedMe:
			TNT1 A 0 ACS_ExecuteAlways(451,0,6,65)
			goto IsReady
		Fire:
			TNT1 A 4 A_JumpIfInventory("artisummonturret",1,"Fire2")
			TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"FireTome")
			TNT1 A 0 A_TakeInventory("MonsterManaBlue",1)
			TNT1 A 6 A_FireCustomMissile("AirCarrierShot0",0,0,0,9)
			TNT1 A 30 A_JumpIfInventory("MonsterManaBlue",1,1)
			TNT1 A 5
			goto IsReady
		FireTome:
			TNT1 A 0 A_TakeInventory("MonsterManaBlue",2)
			TNT1 A 6 A_FireCustomMissile("AirCarrierShot02",0,0,0,9)
			TNT1 A 32 A_JumpIfInventory("MonsterManaBlue",2,1)
			TNT1 A 6
			goto IsReady
		Fire2:
			TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"Fire2Tome")
			TNT1 A 4 A_GunFlash
			TNT1 A 0 A_TakeInventory("MonsterManaBlue",1)
			TNT1 A 5 A_FireCustomMissile("AirCarrierBasicShot0",0,0,0,9)
			TNT1 A 4 A_GunFlash
			TNT1 A 0 A_TakeInventory("MonsterManaBlue",1)
			TNT1 A 5 A_FireCustomMissile("AirCarrierBasicShot0",0,0,0,9)
			TNT1 A 4 A_GunFlash
			TNT1 A 0 A_TakeInventory("MonsterManaBlue",1)
			TNT1 A 5 A_FireCustomMissile("AirCarrierBasicShot0",0,0,0,9)
			TNT1 A 84 A_JumpIfInventory("MonsterManaBlue",3,1)
			TNT1 A 15
			goto IsReady
		Fire2Tome:
			TNT1 A 0 A_JumpIfInventory("MonsterManaBlue",1,1)
			goto Burst
			TNT1 A 4 A_GunFlash
			TNT1 A 0 A_TakeInventory("MonsterManaBlue",1)
			TNT1 A 3 A_FireCustomMissile("AirCarrierBasicShot0",0,0,0,9)
			TNT1 A 1 A_ReFire
			goto IsReady
		Burst:
			TNT1 A 4 A_GunFlash
			TNT1 A 5 A_FireCustomMissile("AirCarrierBasicShot0",0,0,0,9)
			TNT1 A 4 A_GunFlash
			TNT1 A 5 A_FireCustomMissile("AirCarrierBasicShot0",0,0,0,9)
			TNT1 A 4 A_GunFlash
			TNT1 A 5 A_FireCustomMissile("AirCarrierBasicShot0",0,0,0,9)
			TNT1 A 36
			goto IsReady
		AltFire:
			TNT1 A 0 A_JumpIfInventory("dronelimit",1,1)
			goto Fire
			TNT1 A 0 A_GiveInventory("shooting",1)
			TNT1 A 20 SetPlayerProperty(0,1,0)
			TNT1 A 0 A_TakeInventory("dronelimit",1)
			TNT1 A 0 A_JumpIfInventory("PowerWeaponLevel2",1,"AltTome")
			TNT1 A 10 A_SpawnItemEx("Drone0",0,0,46,0,0,0,angle,SXF_NOCHECKPOSITION)
		AltFinish:
			TNT1 A 10 SetPlayerProperty(0,0,0)
			TNT1 AAAAA 2 A_TakeInventory("shooting",1)
			TNT1 AAAAAAAAAAAAAAA 1 A_WeaponReady(WRF_NOSECONDARY)
			goto IsReady
		AltTome:
			TNT1 A 10 A_SpawnItemEx("Drone02",0,0,46,0,0,0,angle,SXF_NOCHECKPOSITION)
			goto AltFinish
	}
}

actor AirCarrierEngine0
{
	radius 10
	height 16
	speed 32
	PROJECTILE
	+MTHRUSPECIES
	+DONTREFLECT
	+ROCKETTRAIL
	+FLOATBOB
	damage (2 * random(20,30) * ACS_ExecuteWithResult(455,5) / 100)
	obituary "%o was kamikaze-bombed by %k's engines."
	deathsound "virusexplosion"
	states
	{
		Spawn:
			ACEN A 0
			ACEN A 0 A_PlaySound("meshwalker/fire",CHAN_VOICE)
			ACEN A 4 bright
			wait
		Death:
			EXPL B 8 bright A_Explode(2 * random(20,30) * ACS_ExecuteWithResult(455,5) / 100, 128, 1)
			EXPL C 6 bright
			EXPL D 4 bright
			stop
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,10 * ACS_ExecuteWithResult(455,4))
			goto Virus1
		Virus1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Virus4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Virus2")
			TNT1 A 0 A_GiveToTarget("VirusDamagePoint",args[3])
			goto Death
		Virus2:
			TNT1 A 0 A_GiveToTarget("VirusDamagePoint",args[3] * 2)
			goto Death
		Virus4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Virus8")
			TNT1 A 0 A_GiveToTarget("VirusDamagePoint",args[3] * 4)
			goto Death
		Virus8:
			TNT1 A 0 A_GiveToTarget("VirusDamagePoint",args[3] * 8)
			goto Death
	}
}

actor AirCarrierBasicShot0 : BasicShot0
{
	damage (5 * random(1,8) * ACS_ExecuteWithResult(455,5) / 100)
	obituary "%o was overwhelmed by %k."
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,225 * ACS_ExecuteWithResult(455,4) / 100)
			goto Virus1
	}
}

actor AirCarrierShot0 : AirCarrierBasicShot0
{
	speed 23
	fastspeed 30
	seesound "aircarrier/shot"
	deathsound "aircarrier/shot"
	damage (4 * random(1,8) * ACS_ExecuteWithResult(455,5) / 100)
	states
	{
		Spawn:
			ACSH AB 6 bright
			loop
		Death:
			ACSH BCD 4 bright
			stop
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,18 * ACS_ExecuteWithResult(455,4) / 10)
			goto Virus1
		Virus1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Virus4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Virus2")
			TNT1 A 0 A_GiveToTarget("VirusDamagePoint",args[3])
			goto Death
		Virus2:
			TNT1 A 0 A_GiveToTarget("VirusDamagePoint",args[3] * 2)
			goto Death
		Virus4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Virus8")
			TNT1 A 0 A_GiveToTarget("VirusDamagePoint",args[3] * 4)
			goto Death
		Virus8:
			TNT1 A 0 A_GiveToTarget("VirusDamagePoint",args[3] * 8)
			goto Death
	}
}

actor AirCarrierShot02 : SporeShot0
{
	damage (9 * random(1,8) * ACS_ExecuteWithResult(455,5) / 100)
	translation "176:191=200:207"
	obituary "%o was overwhelmed by %k."
	states
	{
		Spawn:
			BLAS AAAABBBB 1 bright A_SpawnItemEx("BlasterPoweredSpark",0,0,0,frandom(-2.5,2.5),frandom(-2.5,2.5),frandom(-2.5,2.5),0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEVELOCITY|SXF_CLIENTSIDE)
			loop
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,405 * ACS_ExecuteWithResult(455,4) / 100)
			goto Virus1
	}
}

actor BlasterPoweredSpark
{
	renderstyle add
	alpha 0.8
	scale 0.2
	+CLIENTSIDEONLY
	+NOINTERACTION
	states
	{
		Spawn:
			BLAS A 5 bright
			BLAS AAAAAAA 1 bright A_FadeOut(0.1)
			stop
	}
}

actor Turret0
{
	obituary "%o couldn't breach the static defenses."
	health 200
	height 32
	radius 32
	mass 0x7FFFFFFF
	speed 0
	painchance 40
	seesound "turret/sight"
	activesound "turret/idle"
	painsound "turret/pain"
	deathsound "turret/death"
	species "MonsterMorph"
	MONSTER
	-COUNTKILL
	+THRUSPECIES
	+NEVERFAST
	+NOBLOOD
	+NORADIUSDMG
	+MISSILEMORE
	+LOOKALLAROUND
	states
	{
		Spawn:
			TURR ABAC 4 A_Look
			loop
		See:
			TURR AABBAACC 4 A_Chase
			loop
		Missile:
			TURR D 6 A_FaceTarget
			TURR E 12 bright A_CustomMissile("TurretBasicShot0",28,0,0)
			TURR D 6 A_CPosRefire
			loop
		Pain:
			TURR A 14 A_Pain
			goto See
		Death:
			TURR F 0 A_SpawnItemEx("MillDeathExplosion",0,0,20,0,0,0,0,SXF_NOCHECKPOSITION)
			TURR F 1 A_Fall
			TURR F -1 A_Scream
			stop
	}
}

actor Drone0
{
	health 40
	mass 40
	obituary "%o somehow managed to be downed by a drone."
	height 16
	radius 8
	speed 12
	painchance 255
	seesound "drone/sight"
	activesound "drone/idle"
	painsound "drone/pain"
	deathsound "drone/death"
	species "MonsterMorph"
	MONSTER
	-COUNTKILL
	+THRUSPECIES
	+NEVERFAST
	+NOBLOOD
	+FLOAT
	+NOGRAVITY
	states
	{
		Spawn:
			DRON A 8 A_Look
			loop
		See:
			DRON A 4 A_Chase
			loop
		Missile:
			DRON B 6 A_FaceTarget
			DRON C 0 A_FaceTarget
			DRON C 6 bright A_CustomMissile("DroneBasicShot0",8,0,0)
			goto See
		Pain:
			DRON A 4 A_Pain
			goto See
		Death:
			DRON D 6 bright A_SpawnItemEx("DroneBit0", 0, 0, 10, random(2,4), random(2,4), random(3,5), random(0,360), SXF_ABSOLUTEVELOCITY)
    		DRON E 6 bright A_Scream
    		DRON F 6 bright A_NoBlocking
    		DRON GH 6 bright
    		DRON I -1
    		Stop
	}
}

actor Drone02 : Drone0
{
	translation "177:177=201:201", "181:181=202:202", "186:186=204:204"
	health 60
	+MISSILEMORE
}

actor TurretBasicShot0 : BasicShot0
{
	damage 3
	fastspeed 20
	obituary "%o couldn't breach the static defenses."
	states
	{
		Crash:
		XDeath:
			goto Super::Death
	}
}

actor DroneBasicShot0 : TurretBasicShot0
{
	obituary "%o somehow managed to be downed by a drone."
	damage 1
}

actor AirCarrierMorph : MonsterMorph
{
	tag "AirCarrier"
	inventory.icon "ARTIACAR"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("AirCarrierHitboxCheck",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(450,0,6)
			stop
	}
}

actor AirCarrierMorphDirect : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,6)
			stop
	}
}

actor AirCarrierHitboxCheck : BaseHitboxCheck
{
	height 64
	radius 48
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_GiveToTarget("AirCarrierMorphDirect",1)
			stop
	}
}

actor AirCarrierMorpher : MonsterMorpher { powerup.type MorphAcar }

actor AirCarrierMorpherHitbox : MonsterMorpher { powerup.type MorphAcarHitbox }

actor PowerMorphAcar : PowerMorphVirus { powermorph.playerclass "AirCarrierClass" }

actor PowerMorphAcarHitbox : PowerMorphVirus { powermorph.playerclass "AirCarrierClassHitbox" }