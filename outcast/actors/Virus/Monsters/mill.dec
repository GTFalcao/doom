
actor MillClass : MonsterPawnVirus
{
	player.morphweapon "MillWeapon"
	health 300
	player.maxhealth 300
	speed 0.65
	mass 250
	player.soundclass "mill"
	states
	{
		Spawn:
			MILL A -1
			stop
		See:
			MILL A 0 A_PlaySound("mill/fly",6)
			MILL AAAAAAAAAAAAAA 5
			loop
		Pain:
			MILL A 4 A_PlaySound("mill/pain",CHAN_VOICE)
			goto Spawn
		Missile:
			MILL B 8 bright
			goto Spawn
		Melee:
			MILL C 6 bright
			goto Spawn
		Death:
			MILL D 6 A_PlayerScream
			MILL E 8
			MILL F 8
			MILL G 8 A_PlaySound("meshwalker/beep",6)
			MILL H 0 A_Stop
			MILL H 0 A_SpawnItemEx("MillDeathExplosion",0,0,24,0,0,0,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY)
			MILL H -1 A_NoBlocking
			stop
	}
}

actor MillClassHitbox : MillClass
{
	height 44
	radius 24
	player.viewheight 36
	player.attackzoffset -2
}

actor MillWeapon : BaseMonsterWeaponFlight
{
	//weapon.sisterweapon "MillWeaponPowered"
	obituary "%o was lasered by %k."
	weapon.ammotype1 "MonsterManaBlue"
	tag "Laser"
	states
	{
		Fire:
			TNT1 A 6 A_JumpIfInventory("PowerWeaponLevel2",1,"FireTome")
			TNT1 A 0 A_GunFlash
			TNT1 A 0 A_TakeInventory("MonsterManaBlue",2)
			TNT1 A 8 A_FireCustomMissile("MillLaser0",0,0,11,9)
			TNT1 A 72 A_JumpIfInventory("MonsterManaBlue",2,1)
			TNT1 A 22
			goto Ready+3
		FireTome:
			TNT1 A 6
			TNT1 A 0 A_JumpIfInventory("MonsterManaBlue",1,1)
			goto Burst
			TNT1 A 0 A_GunFlash
			TNT1 A 0 A_PlaySound("weapons/laserbeam",CHAN_WEAPON)
			TNT1 A 0 A_PlaySound("weapons/laserstream",6,1.0,1)
		Hold:
			TNT1 A 0 A_TakeInventory("MonsterManaBlue",1)
			TNT1 AA 2 A_FireCustomMissile("MillLaser02",0,0,11,9)
			TNT1 A 0 A_GunFlash
			TNT1 AA 2 A_FireCustomMissile("MillLaser02",0,0,11,9)
			TNT1 A 0 A_JumpIfInventory("MonsterManaBlue",1,1)
			goto Burst
			TNT1 A 1 A_ReFire
			TNT1 A 6 A_PlaySound("mill/pain",6)
			goto Ready+3
		Burst:
			TNT1 A 6
			TNT1 A 0 A_GunFlash
			TNT1 A 4 A_FireCustomMissile("MillLaser0",2,0,11,9)
			TNT1 A 0 A_GunFlash
			TNT1 A 10 A_FireCustomMissile("MillLaser0",-2,0,11,9)
			TNT1 A 84 A_PlaySound("mill/pain",6)
			goto Ready+3
	}
}

actor MillDeathExplosion
{
	radius 8
	height 16
	speed 0
	PROJECTILE
	+THRUACTORS
	+DONTREFLECT
	states
	{
		Spawn:
			EXPL B 0
			EXPL B 0 A_PlaySound("virusexplosion")
			EXPL B 8 bright A_Explode(3 * random(8,20) * ACS_ExecuteWithResult(455,5) / 100, 128, 1)
			EXPL C 6 bright
			EXPL D 4 bright
			stop
	}
}

actor MillDeathExplosion2 : MillDeathExplosion
{
	states
	{
		Spawn:
			EXPL B 0
			EXPL B 0 A_PlaySound("virusexplosion2")
			goto Super::Spawn+2
	}
}

actor MillLaser0
{
	speed 42
	renderstyle add
	alpha 1
	radius 10
	height 8
	damage (8 * random(1,8) * ACS_ExecuteWithResult(455,5) / 100)
	obituary "%o was lasered by %k."
	seesound "weapons/laserbeam"
	PROJECTILE
	+MTHRUSPECIES
	+SEEKERMISSILE
	+DONTREFLECT
	states
	{
		Spawn:
			LASR A 0
			LASR AAAAAA 0 A_SeekerMissile(1,90,SMF_PRECISE)
			LASR A 1 bright A_SpawnItemEx("MillLaserTrail",0,0,0,0,0,0,angle,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION|SXF_ABSOLUTEANGLE|SXF_ABSOLUTEVELOCITY|SXF_CLIENTSIDE)
			wait
		Death:
			TNT1 A 1
			stop
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,36 * ACS_ExecuteWithResult(455,4) / 10)
			goto Virus1
		Virus1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Virus4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Virus2")
			TNT1 A 0 A_GiveToTarget("VirusDamagePoint",args[3])
			goto Death
		Virus2:
			TNT1 A 0 A_GiveToTarget("VirusDamagePoint",args[3] * 2)
			goto Death
		Virus4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Virus8")
			TNT1 A 0 A_GiveToTarget("VirusDamagePoint",args[3] * 4)
			goto Death
		Virus8:
			TNT1 A 0 A_GiveToTarget("VirusDamagePoint",args[3] * 8)
			goto Death
	}
}

actor MillLaser02 : MillLaser0
{
	seesound ""
	damage (random(1,7) * ACS_ExecuteWithResult(455,5) / 100)
	reactiontime 12
	states
	{
		Spawn:
			LASR A 0
			LASR AAAAAA 0 A_SeekerMissile(1,90,SMF_PRECISE)
		Spawn2:
			LASR A 0 A_Countdown
			LASR A 1 bright A_SpawnItemEx("MillLaserTrail",0,0,0,0,0,0,angle,SXF_NOCHECKPOSITION|SXF_ABSOLUTEANGLE|SXF_ABSOLUTEVELOCITY|SXF_CLIENTSIDE)
			loop
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,4 * ACS_ExecuteWithResult(455,4) / 10)
			goto Virus1
	}
}

actor MillLaserTrail
{
	+NOINTERACTION
	+NOBLOCKMAP
	+CLIENTSIDEONLY
	renderstyle add
	alpha 0.8
	states
	{
		Spawn:
			LASR AAAAAAAAA 1 bright A_FadeOut(0.1)
			stop
	}
}

actor MillMorph : MonsterMorph
{
	tag "Mill"
	inventory.icon "ARTIMILL"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("MillHitboxCheck",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(450,0,4)
			stop
	}
}

actor MillMorphDirect : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,4)
			stop
	}
}

actor MillHitboxCheck : BaseHitboxCheck
{
	height 44
	radius 24
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_GiveToTarget("MillMorphDirect",1)
			stop
	}
}

actor MillMorpher : MonsterMorpher { powerup.type MorphMill }

actor MillMorpherHitbox : MonsterMorpher { powerup.type MorphMillHitbox }

actor PowerMorphMill : PowerMorphVirus { powermorph.playerclass "MillClass" }

actor PowerMorphMillHitbox : PowerMorphVirus { powermorph.playerclass "MillClassHitbox" }