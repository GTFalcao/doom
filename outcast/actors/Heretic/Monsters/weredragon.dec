
actor WeredragonClass : MonsterPawnHeretic
{
	player.morphweapon "WeredragonWeapon"
	health 220
	player.maxhealth 220
	speed 0.8
	gibhealth 110
	mass 1000
	player.soundclass "beast"
	states
	{
		Spawn:
			BEAS AB 10
			loop
		See:
			BEAS ABCDEF 3
			loop
		Pain:
			BEAS E 6 A_PlaySound("beast/pain",2)
			goto Spawn
		Missile:
			BEAS H 10
		Melee:
			BEAS I 12 bright
			goto Spawn
		Death:
			BEAS R 1
    		BEAS R 5 A_PlaySound("none",CHAN_WEAPON)
    		BEAS S 6 A_PlayerScream
    		BEAS TUV 6
    		BEAS W 6 A_NoBlocking
    		BEAS XY 6
    		BEAS Z -1
    		Stop
  		XDeath:
			BEAS J 1
    		BEAS J 4 A_PlaySound("none",CHAN_WEAPON)
    		BEAS K 6 A_PlayerScream
    		BEAS L 5
    		BEAS M 6
   	 		BEAS N 5
    		BEAS O 6 A_NoBlocking
    		BEAS P 5
    		BEAS Q -1
    		stop
	}
}

actor WeredragonClassHitbox : WeredragonClass
{
	height 74
	radius 30
	player.viewheight 64
}

actor WeredragonWeapon : BaseMonsterWeapon
{
	+WEAPON.NOALERT
	obituary "%o was charred by %k."
	weapon.ammotype1 "MonsterManaBlue"
	weapon.sisterweapon "WeredragonWeaponPowered"
	tag "Fire breath"
	states
	{
		Select:
			WPH1 M 1 A_Raise
			wait
		Deselect:
			WPH1 M 1 A_Lower
			wait
		Ready:
			WPH1 M 1 A_WeaponReady
			wait
		Fire:
			WPH1 M 10 A_AlertMonsters
			WPH1 N 0 A_GunFlash
			WPH1 N 0 A_TakeInventory("MonsterManaBlue",2)
			WPH1 N 10 bright A_FireCustomMissile("BeastBall0",0,0,0,9)
			WPH1 M 40 A_JumpIfInventory("MonsterManaBlue",2,"Ready")
			goto Ready
		AltFire:
			WPH1 M 10
			WPH1 N 0 A_GunFlash
			WPH1 N 0 A_PlaySound("beast/attack",CHAN_WEAPON)
			WPH1 N 0 A_FireCustomMissile("WeredragonMeleeProj",0,0,0,9)
			WPH1 N 10 bright A_JumpIfInventory("BerserkTracker",1,1)
			goto Ready
			WPH1 N 10 bright A_FireCustomMissile("WeredragonMeleeProj",0,0,0,9)
			goto Ready
	}
}

actor WeredragonWeaponPowered : WeredragonWeapon
{
	+WEAPON.POWERED_UP
	weapon.sisterweapon "WeredragonWeapon"
	states
	{
		AltFire:
		Fire:
			WPH1 M 0 A_JumpIfInventory("MonsterManaBlue",3,1)
			goto Super::AltFire
			WPH1 M 10 A_AlertMonsters
			WPH1 N 0 A_PlaySound("beast/flame",CHAN_WEAPON,1.0,1)
		AltHold:
		Hold:
			WPH1 M 0 A_JumpIfInventory("MonsterManaBlue",1,1)
			goto Super::AltFire
			WPH1 N 0 A_GunFlash
			WPH1 N 0 A_TakeInventory("MonsterManaBlue",1)
			WPH1 NNNNNNN 1 bright A_FireCustomMissile("WeredragonPoweredFlame",0,0,0,9)
			WPH1 N 0 A_JumpIfInventory("MonsterManaBlue",1,1)
			goto Finish
			WPH1 N 2 bright A_ReFire
			WPH1 M 15 A_PlaySound("beast/active",CHAN_WEAPON)
			goto Ready
		Finish:
			WPH1 N 2 bright
			WPH1 M 15 A_PlaySound("beast/active",CHAN_WEAPON)
			goto Ready
	}
}

actor WeredragonMeleeProj : ImpMeleeProj
{
	obituary "%o was charred by %k."
	damage (13 * random(1,8) * ACS_ExecuteWithResult(455,5) / 100)
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,585 * ACS_ExecuteWithResult(455,4) / 100)
			goto Heretic1
	}
}

actor BeastBall0 : BeastBall
{
	+MTHRUSPECIES
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	fastspeed 25
	obituary "%o was charred by %k."
	damage (11 * random(1,8) * ACS_ExecuteWithResult(455,5) / 100)
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,495 * ACS_ExecuteWithResult(455,4) / 100)
			goto Heretic1
		Heretic1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Heretic4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Heretic2")
			TNT1 A 0 A_GiveToTarget("HereticDamagePoint",args[3])
			goto Super::Death
		Heretic2:
			TNT1 A 0 A_GiveToTarget("HereticDamagePoint",args[3] * 2)
			goto Super::Death
		Heretic4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Heretic8")
			TNT1 A 0 A_GiveToTarget("HereticDamagePoint",args[3] * 4)
			goto Super::Death
		Heretic8:
			TNT1 A 0 A_GiveToTarget("HereticDamagePoint",args[3] * 8)
			goto Super::Death
	}
}

actor WeredragonPoweredFlame
{
	radius 6
	height 8
	speed 15
	fastspeed 22
	obituary "%o was charred by %k."
	damage (random(1,10) * ACS_ExecuteWithResult(455,5) / 100)
	damagetype fire
	PROJECTILE
	+MTHRUSPECIES
	+DONTREFLECT
	+FOILINVUL
	renderstyle add
	alpha 0.75
	states
	{
		Spawn:
			FX09 A 0 bright
			FX09 A 2 bright A_ChangeVelocity(0,0,1.0)
			FX09 BABA 2 bright
			FX09 B 2 bright A_ChangeVelocity(0,0,1.5)
			FX09 CDEF 2 bright
			stop
		Death:
			FX09 G 3 bright
			FX09 H 3 bright A_ChangeVelocity(0,0,1.8)
			FX09 I 4 bright
			FX09 JK 5 bright
			stop
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,55 * ACS_ExecuteWithResult(455,4) / 100)
		Heretic1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Heretic4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Heretic2")
			TNT1 A 0 A_GiveToTarget("HereticDamagePoint",args[3])
			goto Death
		Heretic2:
			TNT1 A 0 A_GiveToTarget("HereticDamagePoint",args[3] * 2)
			goto Death
		Heretic4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Heretic8")
			TNT1 A 0 A_GiveToTarget("HereticDamagePoint",args[3] * 4)
			goto Death
		Heretic8:
			TNT1 A 0 A_GiveToTarget("HereticDamagePoint",args[3] * 8)
			goto Death
	}
}

actor WeredragonMorph : MonsterMorph
{
	tag "Weredragon"
	inventory.icon "ARTIBEAS"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("WeredragonHitboxCheck",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(450,0,8)
			stop
	}
}

actor WeredragonMorphDirect : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,8)
			stop
	}
}

actor WeredragonHitboxCheck : BaseHitboxCheck
{
	height 74
	radius 30
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_GiveToTarget("WeredragonMorphDirect",1)
			stop
	}
}

actor WeredragonMorpher : MonsterMorpher { powerup.type MorphBeas }

actor PowerMorphBeas : PowerMorphHeretic { powermorph.playerclass "WeredragonClass" }

actor WeredragonMorpherHitbox : MonsterMorpher { powerup.type MorphBeasHitbox }

actor PowerMorphBeasHitbox : PowerMorphHeretic { powermorph.playerclass "WeredragonClassHitbox" }