
actor GolemClass : MonsterPawnHeretic
{
	player.morphweapon "GolemWeapon"
	health 80
	player.maxhealth 80
	speed 1.3
	player.soundclass "golem"
	states
	{
		Spawn:
			MUMM AB 10
			loop
		See:
			MUMM ABCD 4
			loop
		Pain:
			MUMM H 8 A_PlaySound("mummy/pain",2)
			goto Spawn
		Missile:
			MUMM EEEEEEEE 1 A_JumpIfInventory("DoingInvis",1,"AltGhost")
			MUMM F 8
			MUMM G 10
			goto Spawn
		AltGhost:
			MUMM A 1 A_PlaySound("mummy/sight",CHAN_VOICE)
			goto Spawn
		Death:
			MUMM I 0 A_JumpIfInventory("InvisOn",1,"SetGhost")
			MUMM I 5
    		MUMM J 5 A_PlayerScream
			MUMM K 5 A_SpawnItemEx("MummySoul", 0, 0, 10, 0, 0, 1)
			MUMM L 5
    		MUMM M 5 A_NoBlocking
    		MUMM NO 5
    		MUMM P -1
    		Stop
		SetGhost:
			MUMM I 0 A_SetTranslucent(0.4)
			goto Death+1
	}
}

actor GolemWeapon : BaseMonsterWeapon
{
	tag "Mighty fist"
	weapon.sisterweapon "GolemWeaponPowered"
	+WEAPON.NOALERT
	+WEAPON.MELEEWEAPON
	states
	{
		Select:
			WPH0 I 1 A_Raise
			wait
		Deselect:
			WPH0 I 1 A_Lower
			wait
		Ready:
			WPH0 I 1 A_WeaponReady
			wait
		Fire:
			WPH0 F 4 A_PlaySound("mummy/attack1",CHAN_WEAPON)
			WPH0 G 4
			WPH0 H 0 A_FireCustomMissile("GolemMeleeProj",0,0,0,9)
			WPH0 H 6 A_JumpIfInventory("BerserkTracker",1,1)
			goto FireDone
			WPH0 H 6 A_FireCustomMissile("GolemMeleeProj",0,0,0,9)
		FireDone:
			WPH0 H 1 offset(0,42) A_TakeInventory("GolemCounter",3)
			WPH0 H 1 offset(0,52)
			WPH0 H 1 offset(0,62)
			WPH0 H 1 offset(0,72)
			WPH0 H 1 offset(0,82)
			WPH0 H 1 offset(0,92)
			WPH0 H 1 offset(0,102)
			WPH0 H 1 offset(0,112)
			WPH0 H 1 offset(0,122)
			TNT1 A 1 offset(0,32)
			TNT1 A 8 A_ReFire
			WPH0 I 1 offset(0,72)
			WPH0 I 1 offset(0,64)
			WPH0 I 1 offset(0,56)
			WPH0 I 1 offset(0,48)
			WPH0 I 1 offset(0,40)
			WPH0 I 1 offset(0,32)
			goto Ready
		AltFire:
			WPH0 I 0 A_GiveInventory("DoingInvis",1)
			WPH0 I 0 A_JumpIfInventory("PowerGhost",1,"TakeInvis")
			WPH0 I 0 A_GiveInventory("InvisOn",1)
			WPH0 I 12 A_GiveInventory("MonGhost",1)
			WPH0 I 1 A_TakeInventory("DoingInvis",1)
			WPH0 IIIIIIIIIIIIIIIIIII 1 A_WeaponReady(WRF_NOSECONDARY)
			goto Ready
		TakeInvis:
			WPH0 I 0 A_TakeInventory("PowerGhost",1)
			WPH0 I 12 A_TakeInventory("InvisOn",1)
			WPH0 I 1 A_TakeInventory("DoingInvis",1)
			WPH0 IIIIIIIIIIIIIIIIIII 1 A_WeaponReady(WRF_NOSECONDARY)
			goto Ready
	}
}

actor GolemCounter : inventory { inventory.maxamount 3 }

actor GolemWeaponPowered : GolemWeapon
{
	weapon.sisterweapon "GolemWeapon"
	+WEAPON.POWERED_UP
	states
	{
		Fire:
			WPH0 F 4
			WPH0 G 4 A_JumpIfInventory("GolemCounter",2,"Fire2")
			WPH0 H 0 A_FireCustomMissile("GolemMeleeProj3",0,0,0,9)
			WPH0 H 4 A_JumpIfInventory("BerserkTracker",1,4)
			WPH0 G 4
			WPH0 F 4 A_ReFire
			WPH0 F 0 A_TakeInventory("GolemCounter",3)
			goto Ready
			WPH0 H 4 A_FireCustomMissile("GolemMeleeProj3",0,0,0,9)
			WPH0 G 4
			WPH0 F 4 A_ReFire
			WPH0 F 0 A_TakeInventory("GolemCounter",3)
			goto Ready
		Fire2:
			WPH0 G 4
			WPH0 H 0 A_PlaySound("*taunt",CHAN_WEAPON)
			WPH0 H 0 A_FireCustomMissile("GolemMeleeProj2",0,0,0,9)
			WPH0 H 6 A_JumpIfInventory("BerserkTracker",1,1)
			goto FireDone
			WPH0 H 6 A_FireCustomMissile("GolemMeleeProj2",0,0,0,9)
		FireDone:
			WPH0 H 1 offset(0,42) A_TakeInventory("GolemCounter",3)
			WPH0 H 1 offset(0,52)
			WPH0 H 1 offset(0,62)
			WPH0 H 1 offset(0,72)
			WPH0 H 1 offset(0,82)
			WPH0 H 1 offset(0,92)
			WPH0 H 1 offset(0,102)
			WPH0 H 1 offset(0,112)
			WPH0 H 1 offset(0,122)
			TNT1 A 12 offset(0,32)
			TNT1 A 1 A_ReFire
			WPH0 I 1 offset(0,72)
			WPH0 I 1 offset(0,64)
			WPH0 I 1 offset(0,56)
			WPH0 I 1 offset(0,48)
			WPH0 I 1 offset(0,40)
			WPH0 I 1 offset(0,32)
			goto Ready
	}
}

actor GolemMeleeProj : ImpMeleeProj
{
	damage (7 * random(1,8) * ACS_ExecuteWithResult(455,5) / 100)
	deathsound "mummy/attack2"
	obituary "%o was smashed by %k."
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,315 * ACS_ExecuteWithResult(455,4) / 100)
			goto Heretic1
	}
}

actor GolemMeleeProj3 : ImpMeleeProj
{
	damage (random(1,8) * ACS_ExecuteWithResult(455,5) / 10)
	obituary "%o was smashed by %k."
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_PlaySound("FighterPunchMiss")
			TNT1 A 0 A_TakeFromTarget("GolemCounter",3)
			stop
		Death:
			TNT1 A 0 A_PlaySound("FighterPunchHitWall")
			TNT1 A 1 A_TakeFromTarget("GolemCounter",3)
			stop
		Crash:
		XDeath:
			TNT1 A 0 A_PlaySound("FighterPunchHitThing")
			TNT1 A 0 A_GiveToTarget("GolemCounter",1)
			TNT1 A 0 A_SetArg(3,45 * ACS_ExecuteWithResult(455,4) / 10)
			goto Heretic1
	}
}

actor GolemMeleeProj2 : GolemMeleeProj
{
	+EXTREMEDEATH
	deathsound "FighterHammerHitThing"
	damage (25 * random(1,8) * ACS_ExecuteWithResult(455,5) / 100)
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,1125 * ACS_ExecuteWithResult(455,4) / 100)
			goto Heretic1
	}
}

actor GolemMorph : MonsterMorph
{
	tag "Golem"
	inventory.icon "ARTIMUMM"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("GolemHitboxCheck",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(450,0,2)
			stop
	}
}

actor GolemMorphDirect : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,2)
			stop
	}
}

actor GolemHitboxCheck : BaseHitboxCheck
{
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_GiveToTarget("GolemMorphDirect",1)
			stop
	}
}

actor GolemMorpher : MonsterMorpher { powerup.type MorphMumm }

actor PowerMorphMumm : PowerMorphHeretic { powermorph.playerclass "GolemClass" }