
actor NitroGolemClass : GolemClass
{
	player.morphweapon "NitroGolemWeapon"
	health 100
	player.maxhealth 100
	speed 0.95
	states
	{
		Melee:
			MUMM X 5
			MUMM Y 7 bright
			goto Spawn
	}
}

actor NitroGolemWeapon : GolemWeapon
{
	tag "Flame head summon and mighty fist"
	weapon.sisterweapon "NitroGolemWeaponPowered"
	weapon.ammotype1 "MonsterManaGreen"
	states
	{
		Fire:
			WPH0 I 0 offset(0,32) A_JumpIfTargetInLOS("Punch",0,JLOSF_ALLYNOJUMP,150)
			WPH0 I 5 A_GunFlash
			WPH0 J 5 bright
			WPH0 I 5 A_GunFlash
			WPH0 J 5 bright A_AlertMonsters
			WPH0 I 5 A_GunFlash
			WPH0 J 0 A_TakeInventory("MonsterManaGreen",1)
			WPH0 J 0 A_JumpIfTargetInLOS("Homing",0,JLOSF_ALLYNOJUMP)
			WPH0 J 5 bright A_FireCustomMissile("MummyFX02",0,0,0,8)
			WPH0 J 10 A_JumpIfInventory("MonsterManaGreen",1,"Ready")
			WPH0 JJJJJ 10 A_WeaponReady(WRF_NOFIRE)
			goto Ready
		Homing:
			WPH0 J 5 bright A_FireCustomMissile("MummyFX01",0,0,0,8)
			WPH0 I 10 A_JumpIfInventory("MonsterManaGreen",1,"Ready")
			WPH0 IIIII 10 A_WeaponReady(WRF_NOFIRE)
			goto Ready
		Punch:
			goto Super::Fire
	}
}

actor NitroGolemWeaponPowered : GolemWeaponPowered
{
	tag "Flame head summon and mighty fist"
	weapon.sisterweapon "NitroGolemWeapon"
	weapon.ammotype1 "MonsterManaGreen"
	states
	{
		Fire:
			WPH0 I 0 offset(0,32) A_JumpIfTargetInLOS("Punch",0,JLOSF_ALLYNOJUMP,150)
			WPH0 I 5 A_GunFlash
			WPH0 J 5 bright
			WPH0 I 5 A_GunFlash
			WPH0 J 5 bright A_AlertMonsters
			WPH0 I 5 A_GunFlash
			WPH0 J 0 A_TakeInventory("MonsterManaGreen",2)
			WPH0 J 5 bright A_FireCustomMissile("MummyFX03",0,0,0,8)
			WPH0 II 12 A_WeaponReady(WRF_NOFIRE)
			WPH0 I 0 A_JumpIfInventory("MonsterManaGreen",2,"Ready")
			WPH0 I 4 A_WeaponReady(WRF_NOFIRE)
			WPH0 IIIIIIIIII 10 A_WeaponReady(WRF_NOFIRE)
			goto Ready
		Punch:
			goto Super::Fire
	}
}

actor MummyFX01 : MummyFX1
{
	+MTHRUSPECIES
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	fastspeed 22
	obituary "%o was shrieked to death by %k."
	damage (5 * random(1,8) * ACS_ExecuteWithResult(455,5) / 100)
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,225 * ACS_ExecuteWithResult(455,4) / 100)
			goto Heretic1
		Heretic1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Heretic4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Heretic2")
			TNT1 A 0 A_GiveToTarget("HereticDamagePoint",args[3])
			goto Super::Death
		Heretic2:
			TNT1 A 0 A_GiveToTarget("HereticDamagePoint",args[3] * 2)
			goto Super::Death
		Heretic4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Heretic8")
			TNT1 A 0 A_GiveToTarget("HereticDamagePoint",args[3] * 4)
			goto Super::Death
		Heretic8:
			TNT1 A 0 A_GiveToTarget("HereticDamagePoint",args[3] * 8)
			goto Super::Death
	}
}

actor MummyFX02 : MummyFX01 { -SEEKERMISSILE }

actor MummyFX03 : MummyFX01
{
	reactiontime 1
	scale 2
	damage 0
	states
	{
		Spawn:
			FX15 A 0 A_Countdown
			FX15 A 0 A_PlaySound("mummy/head")
			FX15 ABCB 5 bright A_SeekerMissile(8,15,SMF_LOOK)
			loop
		Crash:
		Death:
		XDeath:
			FX15 A 0 A_SpawnItemEx("MummyFX01",0,0,0,9 * cos(angle+15),9 * sin(angle+15),0,angle+15,SXF_TRANSFERPOINTERS|SXF_ABSOLUTEVELOCITY)
			FX15 A 0 A_SpawnItemEx("MummyFX01",0,0,0,9 * cos(angle+45),9 * sin(angle+45),0,angle+45,SXF_TRANSFERPOINTERS|SXF_ABSOLUTEVELOCITY)
			FX15 A 0 A_SpawnItemEx("MummyFX01",0,0,0,9 * cos(angle-15),9 * sin(angle-15),0,angle-15,SXF_TRANSFERPOINTERS|SXF_ABSOLUTEVELOCITY)
			FX15 A 0 A_SpawnItemEx("MummyFX01",0,0,0,9 * cos(angle-45),9 * sin(angle-45),0,angle-45,SXF_TRANSFERPOINTERS|SXF_ABSOLUTEVELOCITY)
			stop
	}
}

actor NitroGolemMorph : MonsterMorph
{
	tag "Nitro Golem"
	inventory.icon "ARTIMUM2"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("NitroGolemHitboxCheck",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(450,0,3)
			stop
	}
}

actor NitroGolemMorphDirect : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,3)
			stop
	}
}

actor NitroGolemHitboxCheck : BaseHitboxCheck
{
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_GiveToTarget("NitroGolemMorphDirect",1)
			stop
	}
}

actor NitroGolemMorpher : MonsterMorpher { powerup.type MorphMum2 }

actor PowerMorphMum2 : PowerMorphHeretic { powermorph.playerclass "NitroGolemClass" }