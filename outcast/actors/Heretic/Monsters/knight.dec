
actor KnightClass : MonsterPawnHeretic
{
	player.morphweapon "KnightWeapon"
	health 200
	player.maxhealth 200
	speed 0.85
	mass 300
	renderstyle translucent
	player.soundclass "knig"
	states
	{
		Spawn:
			KNIG AB 10
			loop
		See:
			KNIG ABCD 4
			loop
		Pain:
			KNIG E 6 A_PlaySound("hknight/pain",2)
			goto Spawn
		Melee:
		Missile:
			KNIG EE 1 A_JumpIfInventory("shooting",1,"FastMissile")
			KNIG EF 8
			KNIG G 10
			goto Spawn
		FastMissile:
			KNIG E 5
			KNIG F 4
			KNIG G 6
			goto Spawn
		Death:
			KNIG I 6
    		KNIG J 6 A_PlayerScream
			KNIG K 6
			KNIG L 6 A_NoBlocking
    		KNIG MN 6
    		KNIG O -1
    		Stop
	}
}

actor KnightClassHitbox : KnightClass
{
	height 78
	radius 24
	player.viewheight 70
}

actor KnightWeapon : BaseMonsterWeapon
{
	+WEAPON.NOALERT
	tag "Axe of the undead"
	weapon.sisterweapon "KnightWeaponPowered"
	weapon.ammotype1 "MonsterManaGreen"
	states
	{
		Select:
			WPH1 D 1 A_Raise
			wait
		Deselect:
			WPH1 D 1 A_Lower
			wait
		Ready:
			WPH1 D 0 A_TakeInventory("shooting",1)
			WPH1 D 0 A_CheckFlag("SHADOW","RedReady")
			WPH1 DDDDDEEEFFF 1 A_WeaponReady
			loop
		RedReady:
			WPH1 JJJJJKKKLLL 1 A_WeaponReady
			goto Ready
		NotReady:
			WPH1 D 4 A_CheckFlag("SHADOW","RedNotReady")
			WPH1 EF 3 
			WPH1 D 4
			WPH1 EF 3
			WPH1 D 4
			WPH1 EF 3
			WPH1 D 4
			WPH1 EF 3
			WPH1 D 4
			WPH1 EF 3
			goto Ready
		RedNotReady:
			WPH1 J 4
			WPH1 KL 3
			WPH1 J 4
			WPH1 KL 3
			WPH1 J 4
			WPH1 KL 3
			WPH1 J 4
			WPH1 KL 3
			WPH1 J 4
			WPH1 KL 3
			goto Ready
		Fire:
			WPH1 A 0 A_CheckFlag("SHADOW","RedAxe")
			WPH1 A 9 A_Jump(40,"RedAxe")	// as per the source code
			WPH1 A 1 A_PlaySound("hknight/axe",CHAN_WEAPON)
			WPH1 B 8 A_AlertMonsters
			WPH1 C 0 A_TakeInventory("MonsterManaGreen",2)
			WPH1 C 8 A_FireCustomMissile("KnightAxe0",0,0,0,9)
			WPH1 A 0 A_GunFlash
			WPH1 A 0 A_CheckFlag("SHADOW","RedAxe2")
			WPH1 A 9 A_Jump(40,"RedAxe2")
			WPH1 A 1 A_PlaySound("hknight/axe",CHAN_WEAPON)
			WPH1 B 8 A_AlertMonsters
			WPH1 C 0 A_TakeInventory("MonsterManaGreen",2)
			WPH1 C 8 A_FireCustomMissile("KnightAxe0",0,0,0,9)
			WPH1 C 0 A_JumpIfInventory("MonsterManaGreen",4,"Ready")
			goto NotReady
		RedAxe:
			WPH1 G 9
			WPH1 G 1 A_PlaySound("hknight/axe",CHAN_WEAPON)
			WPH1 H 8 A_AlertMonsters
			WPH1 C 0 A_TakeInventory("MonsterManaGreen",2)
			WPH1 I 8 A_FireCustomMissile("RedAxe0",0,0,0,9)
			WPH1 A 0 A_GunFlash
			WPH1 A 0 A_CheckFlag("SHADOW","RedAxe2")
			WPH1 A 9 A_Jump(40,"RedAxe2")
			WPH1 A 1 A_PlaySound("hknight/axe",CHAN_WEAPON)
			WPH1 B 8 A_AlertMonsters
			WPH1 C 0 A_TakeInventory("MonsterManaGreen",2)
			WPH1 C 8 A_FireCustomMissile("KnightAxe0",0,0,0,9)
			WPH1 C 0 A_JumpIfInventory("MonsterManaGreen",4,"Ready")
			goto NotReady
		RedAxe2:
			WPH1 G 9
			WPH1 G 1 A_PlaySound("hknight/axe",CHAN_WEAPON)
			WPH1 H 8 A_AlertMonsters
			WPH1 I 0 A_TakeInventory("MonsterManaGreen",2)
			WPH1 I 8 A_FireCustomMissile("RedAxe0",0,0,0,9)
			WPH1 I 0 A_JumpIfInventory("MonsterManaGreen",4,"Ready")
			goto NotReady
		AltFire:
			WPH1 A 10 A_CheckFlag("SHADOW","RedMelee")
			WPH1 B 8 A_PlaySound("hknight/melee",CHAN_WEAPON)
			WPH1 C 0 A_FireCustomMissile("KnightMeleeProj",0,0,0,9)
			WPH1 C 8 A_JumpIfInventory("BerserkTracker",1,1)
			goto Ready
			WPH1 C 8 A_FireCustomMissile("KnightMeleeProj",0,0,0,9)
			goto Ready
		RedMelee:
			WPH1 G 10
			WPH1 H 8 A_PlaySound("hknight/melee",CHAN_WEAPON)
			WPH1 I 0 A_FireCustomMissile("KnightMeleeProj",0,0,0,9)
			WPH1 I 8 A_JumpIfInventory("BerserkTracker",1,1)
			goto Ready
			WPH1 I 8 A_FireCustomMissile("KnightMeleeProj",0,0,0,9)
			goto Ready
	}
}

actor KnightWeaponPowered : KnightWeapon
{
	+WEAPON.POWERED_UP
	weapon.sisterweapon "KnightWeapon"
	states
	{
		Fire:
			WPH1 A 0 A_GiveInventory("shooting",1)
			WPH1 A 0 A_CheckFlag("SHADOW","RedAxe")
			WPH1 A 0 A_Jump(40,"RedAxe")	// as per the source code
			WPH1 A 5 A_PlaySound("hknight/axe",CHAN_WEAPON)
			WPH1 B 4 A_AlertMonsters
			WPH1 C 0 A_TakeInventory("MonsterManaGreen",2)
			WPH1 C 4 A_FireCustomMissile("KnightAxe02",0,0,0,9)
			WPH1 A 0 A_GunFlash
			WPH1 A 0 A_CheckFlag("SHADOW","RedAxe2")
			WPH1 A 0 A_Jump(40,"RedAxe2")
			WPH1 A 5 A_PlaySound("hknight/axe",CHAN_WEAPON)
			WPH1 B 4 A_AlertMonsters
			WPH1 C 0 A_TakeInventory("MonsterManaGreen",2)
			WPH1 C 4 A_FireCustomMissile("KnightAxe02",0,0,0,9)
			WPH1 C 0 A_JumpIfInventory("MonsterManaGreen",4,"Ready")
			goto NotReady
		RedAxe:
			WPH1 G 5 A_PlaySound("hknight/axe",CHAN_WEAPON)
			WPH1 H 4 A_AlertMonsters
			WPH1 C 0 A_TakeInventory("MonsterManaGreen",2)
			WPH1 I 4 A_FireCustomMissile("RedAxe02",0,0,0,9)
			WPH1 A 0 A_GunFlash
			WPH1 A 0 A_CheckFlag("SHADOW","RedAxe2")
			WPH1 A 0 A_Jump(40,"RedAxe2")
			WPH1 A 5 A_PlaySound("hknight/axe",CHAN_WEAPON)
			WPH1 B 4 A_AlertMonsters
			WPH1 C 0 A_TakeInventory("MonsterManaGreen",2)
			WPH1 C 4 A_FireCustomMissile("KnightAxe02",0,0,0,9)
			WPH1 C 0 A_JumpIfInventory("MonsterManaGreen",4,"Ready")
			goto NotReady
		RedAxe2:
			WPH1 G 5 A_PlaySound("hknight/axe",CHAN_WEAPON)
			WPH1 H 4 A_AlertMonsters
			WPH1 I 0 A_TakeInventory("MonsterManaGreen",2)
			WPH1 I 4 A_FireCustomMissile("RedAxe02",0,0,0,9)
			WPH1 I 0 A_JumpIfInventory("MonsterManaGreen",4,"Ready")
			goto NotReady
		AltFire:
			WPH1 A 0 A_GiveInventory("shooting",1)
			WPH1 A 1 A_CheckFlag("SHADOW","RedMelee")
			WPH1 A 4 A_PlaySound("hknight/melee",CHAN_WEAPON)
			WPH1 B 4
			WPH1 C 0 A_FireCustomMissile("KnightMeleeProj",0,0,0,9)
			WPH1 C 4 A_JumpIfInventory("BerserkTracker",1,1)
			goto Ready
			WPH1 C 4 A_FireCustomMissile("KnightMeleeProj",0,0,0,9)
			goto Ready
		RedMelee:
			WPH1 G 1
			WPH1 G 4 A_PlaySound("hknight/melee",CHAN_WEAPON)
			WPH1 H 4
			WPH1 I 0 A_FireCustomMissile("KnightMeleeProj",0,0,0,9)
			WPH1 I 4 A_JumpIfInventory("BerserkTracker",1,1)
			goto Ready
			WPH1 I 4 A_FireCustomMissile("KnightMeleeProj",0,0,0,9)
			goto Ready
	}
}

actor KnightMeleeProj : ImpMeleeProj
{
	obituary "%o was slain by undead warrior %k."
	damage (9 * random(1,8) * ACS_ExecuteWithResult(455,5) / 100)
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,405 * ACS_ExecuteWithResult(455,4) / 100)
			goto Heretic1
	}
}

actor KnightAxe0 : KnightAxe
{
	+MTHRUSPECIES
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	obituary "%o was axed by %k."
	fastspeed 25
	damage (9 * random(1,8) * ACS_ExecuteWithResult(455,5) / 100)
	states
	{
		Spawn:
			SPAX A 0
			SPAX A 3 bright A_PlaySound("hknight/axe",CHAN_VOICE)
			SPAX BC 3 bright
			goto Spawn+1
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,405 * ACS_ExecuteWithResult(455,4) / 100)
			goto Heretic1
		Heretic1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Heretic4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Heretic2")
			TNT1 A 0 A_GiveToTarget("HereticDamagePoint",args[3])
			goto Super::Death
		Heretic2:
			TNT1 A 0 A_GiveToTarget("HereticDamagePoint",args[3] * 2)
			goto Super::Death
		Heretic4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Heretic8")
			TNT1 A 0 A_GiveToTarget("HereticDamagePoint",args[3] * 4)
			goto Super::Death
		Heretic8:
			TNT1 A 0 A_GiveToTarget("HereticDamagePoint",args[3] * 8)
			goto Super::Death
	}
}

actor KnightAxe02 : KnightAxe0
{
	speed 25
	fastspeed 32
	+DONTREFLECT
}

actor RedAxe0 : RedAxe
{
	+MTHRUSPECIES
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	obituary "%o was axed by %k."
	fastspeed 25
	damage (11 * random(1,8) * ACS_ExecuteWithResult(455,5) / 100)
	states
	{
		Spawn:
			RAXE A 0
			RAXE A 0 A_JumpIf(ACS_ExecuteWithResult(455,6) > 0,"NoDrip") // sv_fastweapons, I'll spare your fps
			RAXE A 0 A_JumpIfInTargetInventory("RuneDoubleFiringSpeed",1,"HalfDrip")
			RAXE A 0 A_JumpIfInTargetInventory("RuneSpread",1,"HalfDrip")		 // halve blood if the player has Rage or Spread
			RAXE AB 5 bright A_SpawnItemEx("BloodClientHtic",random(-8,8),random(-8,8),0,random(-4,4),random(-4,4),0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			goto Spawn+4
		HalfDrip:
			RAXE A 5 bright A_SpawnItemEx("BloodClientHtic",random(-8,8),random(-8,8),0,random(-4,4),random(-4,4),0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			RAXE B 5 bright
			loop
		NoDrip:
			RAXE AB 5 bright
			loop
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,495 * ACS_ExecuteWithResult(455,4) / 100)
			goto Heretic1
		Heretic1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Heretic4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Heretic2")
			TNT1 A 0 A_GiveToTarget("HereticDamagePoint",args[3])
			goto Super::Death
		Heretic2:
			TNT1 A 0 A_GiveToTarget("HereticDamagePoint",args[3] * 2)
			goto Super::Death
		Heretic4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Heretic8")
			TNT1 A 0 A_GiveToTarget("HereticDamagePoint",args[3] * 4)
			goto Super::Death
		Heretic8:
			TNT1 A 0 A_GiveToTarget("HereticDamagePoint",args[3] * 8)
			goto Super::Death
	}
}

actor RedAxe02 : RedAxe0
{
	speed 25
	fastspeed 32
	+DONTREFLECT
}

actor BloodClientHtic
{
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+NOTELEPORT
	gravity 0.125
	states
	{
		Spawn:
			BLD1 CBA 8 bright
			stop
	}
}

actor KnightMorph : MonsterMorph
{
	tag "Undead Warrior"
	inventory.icon "ARTIKNIG"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("KnightHitboxCheck",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(450,0,6)
			stop
	}
}

actor KnightMorphDirect : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,6)
			stop
	}
}

actor KnightHitboxCheck : BaseHitboxCheck
{
	height 78
	radius 24
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_GiveToTarget("KnightMorphDirect",1)
			stop
	}
}

actor KnightMorpher : MonsterMorpher { powerup.type MorphKnig }

actor PowerMorphKnig : PowerMorphHeretic { powermorph.playerclass "KnightClass" }

actor KnightMorpherHitbox : MonsterMorpher { powerup.type MorphKnigHitbox }

actor PowerMorphKnigHitbox : PowerMorphHeretic { powermorph.playerclass "KnightClassHitbox" }