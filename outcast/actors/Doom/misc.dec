
actor IsDoom : inventory { inventory.maxamount 1 }

actor PowerMorphDoom : PowerMorph
{
	powermorph.playerclass "ZombiemanClass"
	powermorph.unmorphflash "NoTelefog"
	powermorph.morphflash "DoomTelefog"
	powermorph.morphstyle (MRF_FULLHEALTH|MRF_FAILNOTELEFRAG|MRF_WHENINVULNERABLE)
}

actor DoomDamagePoint : Inventory
{ inventory.maxamount 3000000 +INVENTORY.PERSISTENTPOWER }

actor DoomDamagePoint0 : DoomDamagePoint
{ inventory.maxamount 30000 }

actor DoomTelefog
{
  	+NOBLOCKMAP
  	+NOGRAVITY
  	renderstyle add
	alpha 0.9
	seesound "unmorph/hexen"
  	states
  	{
  		Spawn:
			DFOG ABABCDEFGHIJ 6 bright
			stop
  	}
}

actor DoomBlood
{
	+NOBLOCKMAP
	+NOTELEPORT
	vspeed 2
	states
	{
		Spawn:
			BLD0 CBA 8 bright
			stop
	}
}

actor MonsterPawnDoom : MonsterPawn
{
	bloodtype "DoomBlood"
}

actor DoomSpawnClass : PlayerPawn
{
	limitedtoteam 0
	player.scoreicon "DOOMICON"
	player.displayname "Doom"
	player.soundclass "doom"
	player.startitem "SpawnForm"
	//player.portrait "P_DOOM"
	height 24
	species "MonsterMorph"
	+NOBLOOD
	+NOICEDEATH
	+THRUSPECIES
	states
	{
		Spawn:
		See:
			GAM0 A -1
			stop
		Missile:
			GAM0 A 4
			goto Spawn
		Pain:
			GAM0 A 2
			GAM0 A 4 A_Pain
			goto Spawn
		Death:
			"----" C 2
    		FDMN T 0 A_PlayerScream
    		FDMN TTTT 0 A_SpawnItemEx("SpawnClassExplosion",random(-8,8),random(-8,8),random(24,32),frandom(-5.0,5.0),frandom(-5.0,5.0),frandom(1.0,3.0),0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
    		FDMN T -1 A_Fall
			stop
		XDeath:
			"----" C 2
			FDMN TTTT 0 A_SpawnItemEx("SpawnClassExplosion",random(-8,8),random(-8,8),random(24,32),frandom(-5.0,5.0),frandom(-5.0,5.0),frandom(1.0,3.0),0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			goto Death+1
		Burn:
			"----" C 2
			FDMN TT 0 A_PlaySound("world/largefire",0)
			FDMN TTTTTT 0 A_SpawnItemEx("SpawnClassFire",random(-4,4),random(-4,4),random(0,8),frandom(-3.0,3.0),frandom(-3.0,3.0),frandom(0.1,1.5),0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			FDMN T -1 A_Fall
			stop
	}
}

actor SpawnClassExplosion
{
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+NOINTERACTION
	renderstyle add
	states
	{
		Spawn:
			MISL B 8 bright
			MISL C 6 bright A_ScaleVelocity(0.8)
			MISL D 4 bright A_ScaleVelocity(0.8)
			stop
	}
}

actor SpawnClassFire : SpawnClassExplosion
{
	states
	{
		Spawn:
			FLBE ABC 2 bright
			FLBE DEFGDEFG 3 bright
			FLBE HI 2 bright A_ScaleVelocity(0.8)
			FLBE J 0 A_ScaleVelocity(0.75)
			FLBE JJ 1 bright A_FadeOut(0.2)
			FLBE K 0 A_ScaleVelocity(0.7)
			FLBE KK 1 bright A_FadeOut(0.2)
			stop
	}
}