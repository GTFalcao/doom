
actor PainElementalClass : MonsterPawnDoom
{
	player.morphweapon "PainElementalWeapon"
	health 400
	player.maxhealth 400
	speed 0.6
	mass 400
	player.soundclass "pain"
	states
	{
		Spawn:
			PAI0 A -1
			stop
		See:
			PAI0 ABCB 6
			loop
		Pain:
			PAI0 G 12 A_PlaySound("pain/pain",CHAN_VOICE)
			goto Spawn
		Missile:
			PAI0 DE 5
			PAI0 F 5 bright
			goto Spawn
		Death:
			PAI0 H 8 bright
			PAI0 I 8 bright A_PlayerScream
			PAI0 JK 8 bright
			PAI0 L 0 A_SpawnItemEx("LostSoul0",0,0,0,20 * cos(angle+90),20 * sin(angle+90),0,90,SXF_ABSOLUTEVELOCITY|SXF_SETMASTER)
			PAI0 L 0 A_SpawnItemEx("LostSoul0",0,0,0,20 * cos(angle+180),20 * sin(angle+180),0,180,SXF_ABSOLUTEVELOCITY|SXF_SETMASTER)
			PAI0 L 8 bright A_SpawnItemEx("LostSoul0",0,0,0,20 * cos(angle+270),20 * sin(angle+270),0,270,SXF_ABSOLUTEVELOCITY|SXF_SETMASTER)
			PAI0 M 8 bright
			FDMN T 10 A_CheckPlayerDone
			wait
	}
}

actor PainElementalClassHitbox : PainElementalClass { height 64 radius 30 }

actor soullimit : inventory { inventory.maxamount 20 }

actor LostSoul0 : LostSoul
{
	species "MonsterMorph"
	-COUNTKILL
	+NEVERFAST
	+THRUSPECIES
	+SKULLFLY
	states
	{
		Spawn:
			SKU0 A 0
			SKU0 A 0 A_JumpIf(z - floorz < 0,"Die")
			SKU0 A 0 A_JumpIf(ceilingz - z < 60,"Die")
			SKU0 A 0 A_PlaySound("0skull/attack",CHAN_VOICE)
		Idle:
			SKU0 AB 10 bright A_Look
			loop
		Die:
			SKU0 A 0 A_Stop
			SKU0 A 0 A_Die
			goto Super::Death
			
	}
}

actor PainElementalWeapon : BaseMonsterWeaponFlight
{
	tag "Lost Soul spitter"
	+WEAPON.NOALERT
	states
	{
		Ready:
			TNT1 A 10
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,1) == 0,2)
			TNT1 A 0 A_GiveInventory("MonFlight",1)
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,20) > 0,2)
			TNT1 A 1 A_GiveInventory("soullimit",20)
			TNT1 A 1 A_WeaponReady
			wait
		Fire:
			TNT1 A 15
			TNT1 A 0 A_JumpIfInventory("soullimit",1,2)
			TNT1 A 15
			goto Ready+5
			TNT1 A 0 A_TakeInventory("soullimit",1)
			TNT1 A 35 A_SpawnItemEx("LostSoul0",20 * cos(angle),20 * sin(angle),0,20 * cos(angle) * cos(pitch),20 * sin(angle) * cos(pitch),20 * sin(-pitch),0,SXF_ABSOLUTEVELOCITY|SXF_SETMASTER)
			goto Ready+5
	}
}

actor PainElementalMorph : MonsterMorph
{
	tag "Pain Elemental"
	inventory.icon "ARTIPAIN"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("PainElementalHitboxCheck",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(450,0,15)
			stop
	}
}

actor PainElementalMorphDirect : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,15)
			stop
	}
}

actor PainElementalHitboxCheck : BaseHitboxCheck
{
	height 64
	radius 30
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_GiveToTarget("PainElementalMorphDirect",1)
			stop
	}
}

actor PainElementalMorpher : MonsterMorpher { powerup.type MorphPain }

actor PainElementalMorpherHitbox : MonsterMorpher { powerup.type MorphPainHitbox }

actor PowerMorphPain : PowerMorphDoom { powermorph.playerclass "PainElementalClass" }

actor PowerMorphPainHitbox : PowerMorphDoom { powermorph.playerclass "PainElementalClassHitbox" }