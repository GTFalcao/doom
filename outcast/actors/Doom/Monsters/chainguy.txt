
actor ChainguyClass : MonsterPawnDoom
{
	player.morphweapon "ChainguyWeapon"
	health 70
	player.maxhealth 70
	gibhealth 70
	speed 0.85
	player.soundclass "zombie"
	states
	{
		Spawn:
			CP0S AB 10
			loop
		See:
			CP0S ABCD 6
			loop
		Pain:
			CP0S G 6 A_PlaySound("0chainguy/pain",2)
			goto Spawn
		Missile:
			CP0S EE 1 A_JumpIfInventory("shooting",1,"Missile2")
			CP0S E 10
			goto Spawn
		Missile2:
			CP0S E 32
			goto Spawn
		Melee:
			CP0S F 4 bright
			CP0S E 7 bright
			goto Spawn
		Death:
			CP0S H 5
    		CP0S I 5 A_PlayerScream
    		CP0S J 5 A_NoBlocking
    		CP0S KLM 5
    		CP0S N -1	
    		Stop
  		XDeath:
    		CP0S O 5
    		CP0S P 5 A_PlaySound("0misc/gibbed",CHAN_VOICE)
    		CP0S Q 5 A_NoBlocking
    		CP0S RS 5
    		CP0S T -1
			stop
	}
}

actor ChainguyClassHitbox : ChainguyClass { radius 20 }

actor ChainguyWeapon : BaseMonsterWeapon
{
	+WEAPON.NOAUTOFIRE
	obituary "%o was perforated by %k's chaingun."
	weapon.sisterweapon "ChainguyWeaponPowered"
	weapon.ammotype1 "MonsterManaBlue"
	tag "Chaingun"
	states
	{
		Ready:
			WPD0 K 0 ACS_ExecuteAlways(451,0,6,85)
			WPD0 K 1 A_WeaponReady
			wait
		Deselect:
			WPD0 K 1 A_Lower
			Loop
		Select:
			WPD0 K 1 A_Raise
			Loop
		Fire:
			WPD0 KK 5 ACS_ExecuteAlways(451,0,6,55)
			WPD0 K 0 A_JumpIfInventory("MonsterManaBlue",1,1)
			goto Burst
			WPD0 K 0 A_ReFire
			goto Ready
		Burst:
			WPD0 K 0 A_GunFlash
			WPD0 K 0 ACS_ExecuteAlways(451,0,6,55)
			WPD0 K 0 A_FireCustomMissile("StandardBullet",random(-11,11),0,0,9)
			WPD0 K 5 A_PlaySound ("0chainguy/attack", CHAN_WEAPON)
			WPD0 L 0 A_FireCustomMissile("StandardBullet",random(-11,11),0,0,9)
			WPD0 L 4 A_PlaySound ("0chainguy/attack", CHAN_WEAPON)
			WPC0 C 0 A_GunFlash
			WPD0 K 0 A_FireCustomMissile("StandardBullet",random(-11,11),0,0,9)
			WPD0 K 5 A_PlaySound ("0chainguy/attack", CHAN_WEAPON)
			WPD0 L 0 A_FireCustomMissile("StandardBullet",random(-11,11),0,0,9)
			WPD0 L 4 A_PlaySound ("0chainguy/attack", CHAN_WEAPON)
			WPC0 C 0 A_GunFlash
			WPD0 K 0 A_FireCustomMissile("StandardBullet",random(-11,11),0,0,9)
			WPD0 K 5 A_PlaySound ("0chainguy/attack", CHAN_WEAPON)
			WPD0 L 0 A_FireCustomMissile("StandardBullet",random(-11,11),0,0,9)
			WPD0 L 4 A_PlaySound ("0chainguy/attack", CHAN_WEAPON)
			WPC0 C 0 A_GunFlash
			WPD0 K 0 A_FireCustomMissile("StandardBullet",random(-11,11),0,0,9)
			WPD0 K 5 A_PlaySound ("0chainguy/attack", CHAN_WEAPON)
			WPD0 L 0 A_FireCustomMissile("StandardBullet",random(-11,11),0,0,9)
			WPD0 L 4 A_PlaySound ("0chainguy/attack", CHAN_WEAPON)
			WPD0 K 10 ACS_ExecuteAlways(451,0,6,85)
			WPD0 KKKKK 10 A_WeaponReady(WRF_NOFIRE)
			goto Ready
		Hold:
			WPD0 K 0 A_GunFlash
			WPD0 K 0 A_JumpIfInventory("MonsterManaBlue",1,1)
			goto Burst
			WPD0 K 0 A_TakeInventory("MonsterManaBlue",1)
			WPD0 K 0 ACS_ExecuteAlways(451,0,6,55)
			WPD0 K 0 A_FireCustomMissile("StandardBullet",random(-11,11),0,0,9)
			WPD0 K 5 A_PlaySound ("0chainguy/attack", CHAN_WEAPON)
			WPD0 L 0 A_FireCustomMissile("StandardBullet",random(-11,11),0,0,9)
			WPD0 L 4 A_PlaySound ("0chainguy/attack", CHAN_WEAPON)
			WPD0 K 1 A_ReFire
			goto Ready
		Flash:
			WPD0 MN 5 bright
			stop
	}
}

actor ChainguyWeaponPowered : ChainguyWeapon
{
	weapon.sisterweapon "ChainguyWeapon"
	+WEAPON.POWERED_UP
	states
	{
		Fire:
			WPD0 K 0 A_GiveInventory("shooting",1)
			WPD0 K 0 A_PlaySound("weapons/mauler2charge",CHAN_WEAPON)
			WPD0 KKK 10 ACS_ExecuteAlways(451,0,6,55)
			WPD0 K 0 A_JumpIfInventory("MonsterManaBlue",1,1)
			goto Burst
			WPD0 K 0 A_ReFire
			goto Ready
		Burst:
			WPD0 K 0 A_GunFlash
			WPD0 K 0 ACS_ExecuteAlways(451,0,6,55)
			WPD0 KK 0 A_FireCustomMissile("StandardBullet",random(-11,11),0,0,9)
			WPD0 K 4 A_PlaySound ("0grunt/attack2", CHAN_WEAPON)
			WPD0 LL 0 A_FireCustomMissile("StandardBullet",random(-11,11),0,0,9)
			WPD0 L 4 A_PlaySound ("0grunt/attack2", CHAN_WEAPON)
			WPD0 KK 0 A_FireCustomMissile("StandardBullet",random(-11,11),0,0,9)
			WPD0 K 4 A_PlaySound ("0grunt/attack2", CHAN_WEAPON)
			WPD0 LL 0 A_FireCustomMissile("StandardBullet",random(-11,11),0,0,9)
			WPD0 L 4 A_PlaySound ("0grunt/attack2", CHAN_WEAPON)
			WPD0 KK 0 A_FireCustomMissile("StandardBullet",random(-11,11),0,0,9)
			WPD0 K 4 A_PlaySound ("0grunt/attack2", CHAN_WEAPON)
			WPD0 LL 0 A_FireCustomMissile("StandardBullet",random(-11,11),0,0,9)
			WPD0 L 4 A_PlaySound ("0grunt/attack2", CHAN_WEAPON)
			WPD0 K 50
			goto Ready
		Hold:
			WPD0 K 0 A_GunFlash
			WPD0 K 0 A_JumpIfInventory("MonsterManaBlue",1,1)
			goto Burst
			WPD0 K 0 A_TakeInventory("MonsterManaBlue",1)
			WPD0 K 0 ACS_ExecuteAlways(451,0,6,55)
			WPD0 KK 0 A_FireCustomMissile("StandardBullet",random(-11,11),0,0,9)
			WPD0 K 4 A_PlaySound ("0grunt/attack2", CHAN_WEAPON)
			WPD0 LL 0 A_FireCustomMissile("StandardBullet",random(-11,11),0,0,9)
			WPD0 L 4 A_PlaySound ("0grunt/attack2", CHAN_WEAPON)
			WPD0 K 1 A_ReFire
			goto Ready
		Flash:
			WPD0 M 4 bright
			WPD0 N 5 bright
			stop
	}
}

actor StandardBulletNoPain : StandardBullet { +PAINLESS }

actor ChainguyMorph : MonsterMorph
{
	tag "Chaingun Guy"
	inventory.icon "ARTICPOS"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("ChainguyHitboxCheck",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(450,0,4)
			stop
	}
}

actor ChainguyMorphDirect : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,4)
			stop
	}
}

actor ChainguyHitboxCheck : BaseHitboxCheck
{
	radius 20
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_GiveToTarget("ChainguyMorphDirect",1)
			stop
	}
}

actor ChainguyMorpher : MonsterMorpher { powerup.type MorphCpos }

actor ChainguyMorpherHitbox : MonsterMorpher { powerup.type MorphCposHitbox }

actor PowerMorphCpos : PowerMorphDoom { powermorph.playerclass "ChainguyClass" }

actor PowerMorphCposHitbox : PowerMorphDoom { powermorph.playerclass "ChainguyClassHitbox" }