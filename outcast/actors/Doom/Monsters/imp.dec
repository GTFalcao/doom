
actor ImpClass : MonsterPawnDoom
{
	player.morphweapon "ImpWeapon"
	health 60
	player.maxhealth 60
	gibhealth 60
	player.soundclass "imp"
	states
	{
		Spawn:
			TRO0 AB 10
			loop
		See:
			TRO0 ABCD 6
			loop
		Pain:
			TRO0 H 4 A_PlaySound("0imp/pain",2)
			goto Spawn
		Missile:
			TRO0 EE 1 A_JumpIfInventory("shooting",1,"Missile2")
			TRO0 E 4
			TRO0 F 6
			TRO0 G 8
			goto Spawn
		Missile2:
			TRO0 EF 3
			TRO0 G 4
			goto Spawn
		Melee:
			TRO0 EF 7
			TRO0 G 8
			goto Spawn
		Death:
			TRO0 I 8
    		TRO0 J 8 A_PlayerScream
    		TRO0 K 6 A_NoBlocking
    		TRO0 L 6
    		TRO0 M -1
    		Stop
  		XDeath:
    		TRO0 N 5
    		TRO0 O 5 A_PlaySound("0misc/gibbed",CHAN_VOICE)
			TRO0 P 5
    		TRO0 Q 5 A_NoBlocking
    		TRO0 RST 5
    		TRO0 U -1
			stop
	}
}

actor ImpWeapon : BaseMonsterWeapon
{
	tag "Imp claws"
	weapon.ammotype1 "MonsterManaGreen"
	weapon.sisterweapon "ImpWeaponPowered"
	+WEAPON.NOALERT
	states
	{
		Ready:
			WPD4 A 1 A_WeaponReady
			wait
		Deselect:
			WPD4 A 1 A_Lower
			wait
		Select:
			WPD4 A 1 A_Raise
			wait
		Fire:
			WPD4 B 7 A_GunFlash
			WPD4 C 7 A_AlertMonsters
			WPD4 D 0 A_TakeInventory("MonsterManaGreen",1)
			WPD4 D 6 A_FireCustomMissile("DoomImpBall0",0,1)
			WPD4 D 0 A_JumpIfInventory("MonsterManaGreen",1,1)
			goto Delay
			WPD4 D 0 A_ReFire
			WPD4 D 1 offset(8,40)
			WPD4 D 1 offset(16,48)
			WPD4 D 1 offset(24,56)
			WPD4 D 1 offset(32,64)
			WPD4 D 1 offset(40,72)
			goto Ready
		Delay:
			WPD4 D 1 offset(4,36)
			WPD4 D 1 offset(8,40)
			WPD4 D 1 offset(12,44)
			WPD4 D 1 offset(16,48)
			WPD4 D 1 offset(20,52)
			WPD4 D 1 offset(24,56)
			WPD4 D 1 offset(28,60)
			WPD4 D 1 offset(32,64)
			WPD4 D 1 offset(36,68)
			WPD4 D 1 offset(40,72)
			WPD4 A 30
			goto Ready
		AltFire:
			WPD4 B 6 A_PlaySound("0imp/melee",CHAN_WEAPON)
			WPD4 C 6
			WPD4 D 0 A_FireCustomMissile("ImpMeleeProj",0,0,0,9)
			WPD4 D 6 A_JumpIfInventory("BerserkTracker",1,1)
			goto Ready
			WPD4 D 6 A_FireCustomMissile("ImpMeleeProj",0,0,0,9)
			goto Ready
	}
}

actor ImpWeaponPowered : ImpWeapon
{
	weapon.sisterweapon "ImpWeapon"
	+WEAPON.POWERED_UP
	states
	{
		Fire:
			WPD4 B 5 A_GunFlash
			WPD4 B 7 A_GunFlash
			WPD4 C 7 A_AlertMonsters
			WPD4 D 0 A_JumpIfInventory("MonsterManaGreen",5,"BigShot")
			WPD4 D 0 A_SetArg(4,7)
			WPD4 D 0 A_SetArg(3,45)
			goto ShootMore
		BigShot:
			WPD4 D 0 A_SetArg(4,23)
			WPD4 D 0 A_SetArg(3,15)
		ShootMore:
			WPD4 D 0 A_FireCustomMissile("DoomImpBall0",args[3] * args[4])
			WPD4 D 0 A_SetArg(4,args[4] - 1)
			WPD4 D 0 A_JumpIf(args[4] >= 1,"ShootMore")
			WPD4 D 0 A_SetArg(3,0)
			WPD4 D 0 A_SetArg(4,0)
			WPD4 D 6 A_FireCustomMissile("DoomImpBall0")
			WPD4 D 1 offset(8,40) A_TakeInventory("MonsterManaGreen",5)
			WPD4 D 1 offset(16,48)
			WPD4 D 1 offset(24,56)
			WPD4 D 1 offset(32,64)
			WPD4 D 1 offset(40,72)
			WPD4 A 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH|WRF_NOBOB)
			WPD4 A 150 A_JumpIfInventory("MonsterManaGreen",5,1)
			WPD4 A 55
			goto Ready
		AltFire:
			WPD4 B 0 A_GiveInventory("shooting",1)
			WPD4 B 3 A_PlaySound("0imp/melee2",CHAN_WEAPON)
			WPD4 C 3 A_TakeInventory("shooting",1)
			WPD4 D 0 A_FireCustomMissile("ImpMeleeProj",0,0,0,9)
			WPD4 D 3 A_JumpIfInventory("BerserkTracker",1,1)
			goto Ready
			WPD4 D 3 A_FireCustomMissile("ImpMeleeProj",0,0,0,9)
			goto Ready
	}
}

actor DoomImpBall0 : DoomImpBall
{
	seesound "0imp/attack"
	deathsound "0imp/shotx"
	obituary "%o was burned by imp %k."
	fastspeed 25
	damage (3 * random(1,8) * ACS_ExecuteWithResult(455,5) / 100)
	+MTHRUSPECIES
	states
	{
		Spawn:
			BL01 AB 4 bright
			loop
		Death:
			BL01 CDE 6 bright
			stop
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,135 * ACS_ExecuteWithResult(455,4) / 100)
			goto Doom1
		Doom1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Doom4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Doom2")
			TNT1 A 0 A_GiveToTarget("DoomDamagePoint",args[3])
			goto Death
		Doom2:
			TNT1 A 0 A_GiveToTarget("DoomDamagePoint",args[3] * 2)
			goto Death
		Doom4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Doom8")
			TNT1 A 0 A_GiveToTarget("DoomDamagePoint",args[3] * 4)
			goto Death
		Doom8:
			TNT1 A 0 A_GiveToTarget("DoomDamagePoint",args[3] * 8)
			goto Death
	}
}

actor ImpMeleeProj : StandardMeleeProj
{
	obituary "%o was slashed by imp %k."
}

actor ImpMorph : MonsterMorph
{
	tag "Imp"
	inventory.icon "ARTITROO"
	states
	{
		Use:
			TNT1 A 0 ACS_ExecuteAlways(450,0,2)
			stop
	}
}

actor ImpMorpher : MonsterMorpher { powerup.type MorphTroo }

actor PowerMorphTroo : PowerMorphDoom { powermorph.playerclass "ImpClass" }