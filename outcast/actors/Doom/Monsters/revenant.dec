
actor RevenantClass : MonsterPawnDoom
{
	player.morphweapon "RevenantWeapon"
	health 300
	player.maxhealth 300
	mass 500
	player.soundclass "skel"
	states
	{
		Spawn:
			SKE0 AB 10
			loop
		See:
			SKE0 ABCDEF 4
			loop
		Pain:
			SKE0 L 10 A_PlaySound("0skeleton/pain",2)
			goto Spawn
		Missile:
			SKE0 J 10 bright
			SKE0 K 22
			goto Spawn
		Melee:
			SKE0 GHI 6
			SKE0 I 2
			goto Spawn
		Death:
			SKE0 LM 7
    		SKE0 N 7 A_PlayerScream
			SKE0 O 7 A_NoBlocking
    		SKE0 P 7
    		SKE0 Q -1
    		Stop
	}
}

actor RevenantClassHitbox : RevenantClass
{
	height 78
	radius 20
	player.viewheight 64
	player.attackzoffset 14
}

actor RevenantWeapon : BaseMonsterWeapon
{
	weapon.ammotype1 "MonsterManaBlue"
	weapon.sisterweapon "RevenantWeaponPowered"
	tag "Missile launcher and fists"
	+WEAPON.NOALERT
	states
	{
		Select:
			WPD3 F 1 A_Raise
			wait
		Deselect:
			WPD3 F 1 A_Lower
			wait
		Ready:
			WPD3 F 1 A_WeaponReady
			wait
		Fire:	
			WPD3 F 10 A_AlertMonsters
			WPD3 G 0 A_TakeInventory("MonsterManaBlue",2)
			WPD3 G 0 A_JumpIfTargetInLOS("Homing",0,JLOSF_ALLYNOJUMP)
			WPD3 G 10 A_FireCustomMissile("RevenantTracer02",0,0,0,9)
			WPD3 F 60 A_JumpIfInventory("MonsterManaBlue",2,1)
			WPD3 F 10
			goto Ready
		Homing:
			WPD3 G 10 A_FireCustomMissile("RevenantTracer0",0,0,0,9)
			WPD3 F 60 A_JumpIfInventory("MonsterManaBlue",2,1)
			WPD3 F 10
			goto Ready
		AltFire:
			WPD3 F 0 A_GunFlash
			WPD3 F 6 A_PlaySound("0skeleton/swing",CHAN_WEAPON)
			WPD3 F 6
			WPD3 F 0 A_FireCustomMissile("RevMeleeProj",0,0,0,9)
			WPD3 F 6 A_JumpIfInventory("BerserkTracker",1,1)
			goto Ready
			WPD3 F 6 A_FireCustomMissile("RevMeleeProj",0,0,0,9)
			goto Ready
		Flash:
			TNT1 A 6
			WPD3 HI 6
			stop
	}
}

actor RevenantWeaponPowered : RevenantWeapon
{
	weapon.sisterweapon "RevenantWeapon"
	+WEAPON.POWERED_UP
	states
	{
		Fire:
			WPD3 F 10 A_AlertMonsters
			WPD3 G 0 A_TakeInventory("MonsterManaBlue",5)
			WPD3 G 20 A_FireCustomMissile("RevenantTracer03",0,0,0,9)
			WPD3 F 140 A_JumpIfInventory("MonsterManaBlue",5,1)
			WPD3 F 40
			goto Ready
		AltFire:
			WPD3 F 0 A_GunFlash
			WPD3 F 6 A_PlaySound("0skeleton/swing",CHAN_WEAPON)
			WPD3 F 6
			WPD3 F 0 A_FireCustomMissile("RevMeleeProj2",0,0,0,9)
			WPD3 F 24 A_JumpIfInventory("BerserkTracker",1,1)
			goto Ready
			WPD3 F 24 A_FireCustomMissile("RevMeleeProj2",0,0,0,9)
			goto Ready
	}
}

actor RevMeleeProj : ImpMeleeProj
{
	deathsound "0skeleton/melee"
	obituary "%o was punched by %k."
	damage (6 * random(1,10) * ACS_ExecuteWithResult(455,5) / 100)
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,33 * ACS_ExecuteWithResult(455,4) / 10)
			goto Doom1
	}
}	

actor RevMeleeProj2 : RevMeleeProj
{
	scale 0.8
	-BLOODSPLATTER
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,13 * ACS_ExecuteWithResult(455,4))
			goto Doom1
		Death:
			MIS0 B 0 A_PlaySound("0skeleton/tracex",CHAN_ITEM)
			MIS0 B 8 bright A_Explode(100,100,0)
			MIS0 C 6 bright
			MIS0 D 4 bright
			stop
		Doom1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Doom4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Doom2")
			TNT1 A 0 A_GiveToTarget("DoomDamagePoint",args[3])
			goto Death
		Doom2:
			TNT1 A 0 A_GiveToTarget("DoomDamagePoint",args[3] * 2)
			goto Death
		Doom4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Doom8")
			TNT1 A 0 A_GiveToTarget("DoomDamagePoint",args[3] * 4)
			goto Death
		Doom8:
			TNT1 A 0 A_GiveToTarget("DoomDamagePoint",args[3] * 8)
			goto Death
	}
}

actor RevenantTracer0 : RevenantTracer
{
	seesound "0skeleton/attack"
	deathsound "0skeleton/tracex"
	obituary "%o couldn't evade %k's fireball."
	damage (10 * random(1,8) * ACS_ExecuteWithResult(455,5) / 100)
	fastspeed 22
	+MTHRUSPECIES
	states
	{
		Spawn:
			FTB0 AB 2 bright A_Tracer
			loop
		Death:
			FTB0 C 8 bright
			FTB0 D 6 bright
			FTB0 E 4 bright
			stop
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,45 * ACS_ExecuteWithResult(455,4) / 10)
			goto Doom1
		Doom1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Doom4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Doom2")
			TNT1 A 0 A_GiveToTarget("DoomDamagePoint",args[3])
			goto Death
		Doom2:
			TNT1 A 0 A_GiveToTarget("DoomDamagePoint",args[3] * 2)
			goto Death
		Doom4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Doom8")
			TNT1 A 0 A_GiveToTarget("DoomDamagePoint",args[3] * 4)
			goto Death
		Doom8:
			TNT1 A 0 A_GiveToTarget("DoomDamagePoint",args[3] * 8)
			goto Death
	}
}

actor RevenantTracer02 : RevenantTracer0 { -SEEKERMISSILE }

actor RevenantTracer03 : RevenantTracer0
{
	deathsound "0skeleton/tracex2"
	damage (random(220,260) * ACS_ExecuteWithResult(455,5) / 100)
	speed 7
	fastspeed 16
	states
	{
		Spawn:
			FTB0 A 0
			FTB0 A 0 A_JumpIfTargetInLOS("Seek",0,JLOSF_PROJECTILE|JLOSF_DEADNOJUMP)
			FTB0 A 0 A_RearrangePointers(AAPTR_DEFAULT,AAPTR_DEFAULT,AAPTR_NULL)
			FTB0 AABB 1 bright A_SeekerMissile(2,3,SMF_LOOK|SMF_PRECISE)
			FTB0 A 0 A_SpawnItemEx("BulletPuff",0,0,0,0,0,frandom(0.8,1.25),0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			FTB0 A 0 A_SpawnItemEx("RevenantTracerSmoke",3 * cos(angle),3 * sin(angle),0,0,0,frandom(0.8,1.25),0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			loop
		Seek:
			FTB0 AABB 1 bright A_SeekerMissile(2,3,SMF_PRECISE)
			FTB0 A 0 A_SpawnItemEx("BulletPuff",0,0,0,0,0,frandom(0.8,1.25),0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			FTB0 A 0 A_SpawnItemEx("RevenantTracerSmoke",3 * cos(angle),3 * sin(angle),0,0,0,frandom(0.8,1.25),0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			FTB0 AABB 1 bright A_SeekerMissile(2,3,SMF_PRECISE)
			FTB0 A 0 A_SpawnItemEx("BulletPuff",0,0,0,0,0,frandom(0.8,1.25),0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			FTB0 A 0 A_SpawnItemEx("RevenantTracerSmoke",3 * cos(angle),3 * sin(angle),0,0,0,frandom(0.8,1.25),0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE)
			FTB0 A 0 A_PlaySound("0skeleton/tick",CHAN_ITEM)
			goto Spawn
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,24 * ACS_ExecuteWithResult(455,4))
			goto Doom1
	}
}

actor RevenantMorph : MonsterMorph
{
	tag "Revenant"
	inventory.icon "ARTISKEL"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("RevenantHitboxCheck",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(450,0,6)
			stop
	}
}

actor RevenantMorphDirect : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,6)
			stop
	}
}

actor RevenantHitboxCheck : BaseHitboxCheck
{
	height 78
	radius 20
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_GiveToTarget("RevenantMorphDirect",1)
			stop
	}
}

actor RevenantMorpher : MonsterMorpher { powerup.type MorphSkel }

actor RevenantMorpherHitbox : MonsterMorpher { powerup.type MorphSkelHitbox }

actor PowerMorphSkel : PowerMorphDoom { powermorph.playerclass "RevenantClass" }

actor PowerMorphSkelHitbox : PowerMorphDoom { powermorph.playerclass "RevenantClassHitbox" }