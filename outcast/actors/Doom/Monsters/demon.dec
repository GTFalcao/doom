
actor DemonClass : MonsterPawnDoom
{
	player.morphweapon "DemonWeapon"
	health 150
	player.maxhealth 150
	speed 1.666
	mass 300
	player.soundclass "demon"
	states
	{
		Spawn:
			SAR0 AB 10
			loop
		See:
			SAR0 ABCD 4
			loop
		Pain:
			SAR0 H 4 A_PlaySound("0demon/pain",2)
			goto Spawn
		Missile:
			SAR0 EE 1 A_JumpIfInventory("shooting",1,"Slower")
			SAR0 E 4
			SAR0 F 6
			SAR0 G 8
			goto Spawn
		Slower:
			SAR0 EFG 8
			goto Spawn
		Melee:
			SAR0 A 1 A_PlaySound("0demon/sight",CHAN_VOICE)
			goto Spawn
		Death:
			SAR0 I 0 A_JumpIfInventory("InvisOn",1,"SetFuzz")
			SAR0 I 8
    		SAR0 J 8 A_PlayerScream
			SAR0 K 4
    		SAR0 L 4 A_NoBlocking
    		SAR0 M 4
    		SAR0 N -1
    		Stop
		SetFuzz:
			SAR0 I 0 A_SetTranslucent(1.0,2)
			goto Death+1
	}
}

actor DemonClassHitbox : DemonClass { radius 24 }

actor DemonWeapon : BaseMonsterWeapon
{
	obituary "%o was eaten by %k."
	tag "Demon jaws"
	weapon.sisterweapon "DemonWeaponPowered"
	+WEAPON.NOALERT
	+WEAPON.MELEEWEAPON
	states
	{
		Select:
			WPD4 E 1 A_Raise
			wait
		Deselect:
			WPD4 E 1 A_Lower
			wait
		Ready:
			WPD4 E 1 A_WeaponReady
			wait
		Fire:
			WPD4 F 12 A_PlaySound("0demon/melee",CHAN_WEAPON)
			WPD4 F 0 A_FireCustomMissile("DemonMeleeProj",0,0,0,9)
			WPD4 F 6 A_JumpIfInventory("BerserkTracker",1,1)
			goto Ready
			WPD4 F 6 A_FireCustomMissile("DemonMeleeProj",0,0,0,9)
			goto Ready
		AltFire:
			WPD4 E 0 A_GunFlash
			WPD4 E 0 A_JumpIfInventory("PowerInvisibility",1,"TakeInvis")
			WPD4 E 0 A_GiveInventory("InvisOn",1)
			WPD4 E 8 A_GiveInventory("MonBlur",1)
			WPD4 EEEEEEEEEEEEEEEEEEEEEEEE 1 A_WeaponReady(WRF_NOSECONDARY)
			goto Ready
		TakeInvis:
			WPD4 E 0 A_TakeInventory("InvisOn",1)
			WPD4 E 8 A_TakeInventory("PowerInvisibility",1)
			WPD4 EEEEEEEEEEEEEEEEEEEEEEEE 1 A_WeaponReady(WRF_NOSECONDARY)
			goto Ready
	}
}

actor DemonWeaponPowered : DemonWeapon
{
	weapon.sisterweapon "DemonWeapon"
	+WEAPON.POWERED_UP
	states
	{
		Fire:
			WPD4 F 0 A_GiveInventory("shooting",1)
			WPD4 F 12 A_PlaySound("0demon/melee2",CHAN_WEAPON)
			WPD4 F 4 A_TakeInventory("shooting",1)
			WPD4 F 0 A_FireCustomMissile("DemonMeleeProj2",0,0,0,9)
			WPD4 F 38 A_JumpIfInventory("BerserkTracker",1,1)
			goto Ready
			WPD4 F 38 A_FireCustomMissile("DemonMeleeProj2NoSound",0,0,0,9)
			goto Ready
	}
}

actor DemonMeleeProj : ImpMeleeProj
{
	obituary "%o was eaten by %k."
	damage (4 * random(1,10) * ACS_ExecuteWithResult(455,5) / 100)
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,22 * ACS_ExecuteWithResult(455,4) / 10)
			goto Doom1
	}
}

actor DemonMeleeProj2 : DemonMeleeProj
{
	+EXTREMEDEATH
	-BLOODSPLATTER
	damage (random(120,150) * ACS_ExecuteWithResult(455,5) / 100)
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,135 * ACS_ExecuteWithResult(455,4) / 10)
			TNT1 A 0 A_PlaySound("0demon/ouchbite")
			TNT1 A 0 A_SpawnItemEx("DoomBloodClient",0,0,0,random(-5,5),random(-5,5),frandom(2.0,6.0),0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE,0)
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,6) > 0, 9) // sv_fastweapons, I'll spare your fps
			TNT1 AAAAA 0 A_SpawnItemEx("DoomBloodClient",0,0,0,random(-5,5),random(-5,5),frandom(2.0,6.0),0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE,0)
			TNT1 AA 0 A_SpawnItemEx("DoomBloodClient",0,0,0,random(-5,5),random(-5,5),frandom(2.0,6.0),0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE,64)
			TNT1 AA 0 A_SpawnItemEx("DoomBloodClient",0,0,0,random(-5,5),random(-5,5),frandom(2.0,6.0),0,SXF_NOCHECKPOSITION|SXF_CLIENTSIDE,128)
			goto Doom1
	}
}

actor DemonMeleeProj2NoSound : DemonMeleeProj2
{
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,135 * ACS_ExecuteWithResult(455,4) / 10)
			goto Super::XDeath+2
	}
}

actor DoomBloodClient : DoomBlood { vspeed 0 +CLIENTSIDEONLY gravity 0.35 }

actor DemonMorph : MonsterMorph
{
	tag "Demon"
	inventory.icon "ARTISARG"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("DemonHitboxCheck",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(450,0,3)
			stop
	}
}

actor DemonMorphDirect : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,3)
			stop
	}
}

actor DemonHitboxCheck : BaseHitboxCheck
{
	radius 24
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_GiveToTarget("DemonMorphDirect",1)
			stop
	}
}

actor DemonMorpher : MonsterMorpher { powerup.type MorphSarg }

actor DemonMorpherHitbox : MonsterMorpher { powerup.type MorphSargHitbox }

actor PowerMorphSarg : PowerMorphDoom { powermorph.playerclass "DemonClass" }

actor PowerMorphSargHitbox : PowerMorphDoom { powermorph.playerclass "DemonClassHitbox" }