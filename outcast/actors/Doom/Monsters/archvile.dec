
actor ArchvileClass : MonsterPawnDoom
{
	player.morphweapon "ArchvileWeapon"
	health 700
	player.maxhealth 700
	mass 600
	painchance 32
	player.soundclass "vile"
	states
	{
		Spawn:
			VIL0 AB 10
			loop
		See:
			VIL0 ABCDEF 4
			loop
		Pain:
			VIL0 Q 10 A_PlaySound("0vile/pain",2)
			goto Spawn
		Missile:
			VIL0 AA 1 A_JumpIfInventory("shooting",1,"Spell")
			goto Spawn
		Spell:
			VIL0 G 9 bright
			VIL0 HIJKLMNO 7 bright
			VIL0 P 16 bright
			goto Spawn
		Melee:
			VLR0 ABC 10 bright
			goto Spawn
		Death:
    		VIL0 Q 7
    		VIL0 R 7 A_PlayerScream
    		VIL0 S 7 A_NoBlocking
    		VIL0 TUVWXY 7
    		VIL0 Z -1
    		stop
	}
}

actor ArchvileClassHitbox : ArchvileClass { height 72 player.viewheight 60 }

actor ArchvileWeapon : BaseMonsterWeapon
{
	+WEAPON.NOALERT
	weapon.sisterweapon "ArchvileWeaponPowered"
	weapon.ammotype1 "MonsterManaBlue"
	weapon.ammotype2 "MonsterManaGreen"
	tag "Fire summoning spell"
	obituary "%o was incinerated by %k."
	states
	{
		Ready:
			TNT1 A 10
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455),3)
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,20) > 0,2)
			TNT1 A 1 A_GiveInventory("ReviveAttempt",50)
			TNT1 A 1 A_WeaponReady
			wait
		Fire:
			TNT1 A 2 A_JumpIfTargetInLOS("Cast",0,JLOSF_ALLYNOJUMP,896)
			goto Ready+4
		Cast:
			WPD1 D 0 ACS_ExecuteAlways(451,0,6,15)
			WPD1 D 0 A_GiveInventory("shooting",1)
			WPD1 D 0 A_PlaySound("0vile/start",0)
			WPD1 D 0 A_AlertMonsters
			WPD1 D 9 bright A_FireCustomMissile("VileWeaponFire")
			WPD1 E 7 bright A_TakeInventory("MonsterManaBlue",1)
			WPD1 F 7 bright A_TakeInventory("MonsterManaGreen",1)
			WPD1 F 1 bright offset(0,52) A_TakeInventory("MonsterManaBlue",1)
			WPD1 F 1 bright offset(0,72) A_TakeInventory("MonsterManaGreen",1)
			WPD1 F 1 bright offset(0,92)
			WPD1 F 1 bright offset(0,112)
			WPD1 F 1 bright offset(0,132)
			WPD1 F 1 bright offset(0,152)
			WPD1 F 1 bright offset(0,172)
			WPD1 F 1 bright offset(0,192) A_TakeInventory("MonsterManaBlue",1)
			TNT1 A 6 offset(0,32) A_TakeInventory("MonsterManaGreen",1)
			TNT1 A 7 A_TakeInventory("MonsterManaBlue",1)
			TNT1 A 7 A_TakeInventory("MonsterManaGreen",1)
			WPD1 G 7 bright A_TakeInventory("MonsterManaBlue",1)
			WPD1 H 7 bright A_TakeInventory("MonsterManaGreen",1)
			WPD1 I 16 bright A_TakeInventory("shooting",1)
			TNT1 A 0 ACS_ExecuteAlways(451,0,6,100)
			TNT1 A 6 A_JumpIfInventory("MonsterManaBlue",5,1)
			goto Delay
			TNT1 A 6 A_JumpIfInventory("MonsterManaGreen",5,1)
			goto Delay
			TNT1 A 6
			goto Ready+4
		Delay:
			TNT1 A 160
			goto Ready+4
		AltFire:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455),"Fire")
			TNT1 A 0 A_JumpIfInventory("ReviveAttempt",1,"Revive")
			goto Fire
		Revive:
			TNT1 A 0 A_GunFlash
			TNT1 A 0 SetPlayerProperty(0,1,0)
			TNT1 A 0 A_Stop
			WPD1 J 1 bright offset(0,80) A_SpawnItemEx("InvisResurrector",64 * cos(angle),64 * sin(angle),0,0,0,0,angle,SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY)
			WPD1 J 1 bright offset(0,68)
			WPD1 J 1 bright offset(0,56)
			WPD1 J 1 bright offset(0,44)
			WPD1 J 22 bright offset(0,32) A_TakeInventory("ReviveAttempt",1)
			WPD1 J 1 bright offset(0,44)
			WPD1 J 1 bright offset(0,56)
			WPD1 J 1 bright offset(0,68)
			WPD1 J 1 bright offset(0,80) 
			TNT1 A 20 bright offset(0,32) SetPlayerProperty(0,0,0)
			goto Ready+4
	}
}

actor ArchvileWeaponPowered : ArchvileWeapon
{
	weapon.sisterweapon "ArchvileWeapon"
	+WEAPON.POWERED_UP
	states
	{
		Fire:
  	TNT1 A 2 A_JumpIfTargetInLOS("Cast",0,JLOSF_ALLYNOJUMP,896)
	goto Ready+4
		Cast:
	WPD1 D 0 ACS_ExecuteAlways(451,0,6,20)
  	WPD1 D 0 A_GiveInventory("shooting",1)
	WPD1 D 0 A_PlaySound("0vile/start",0)
	WPD1 D 0 A_AlertMonsters
	WPD1 D 9 bright A_FireCustomMissile("VileWeaponFire2")
	WPD1 E 0 A_TakeInventory("MonsterManaGreen",1)
	WPD1 E 4 bright A_TakeInventory("MonsterManaBlue",1)
	WPD1 E 0 A_TakeInventory("MonsterManaGreen",1)
	WPD1 E 3 bright A_TakeInventory("MonsterManaBlue",1)
	WPD1 E 0 A_TakeInventory("MonsterManaBlue",1)
	WPD1 F 4 bright A_TakeInventory("MonsterManaGreen",1)
	WPD1 E 0 A_TakeInventory("MonsterManaBlue",1)
	WPD1 F 3 bright A_TakeInventory("MonsterManaGreen",1)
	WPD1 E 0 A_TakeInventory("MonsterManaGreen",1)
	WPD1 F 1 bright offset(0,52) A_TakeInventory("MonsterManaBlue",1)
	WPD1 F 1 bright offset(0,72) 
	WPD1 F 1 bright offset(0,92)
	WPD1 F 1 bright offset(0,112) A_TakeInventory("MonsterManaGreen",1)
	WPD1 F 1 bright offset(0,132) A_TakeInventory("MonsterManaBlue",1)
	WPD1 F 1 bright offset(0,152)
	WPD1 F 1 bright offset(0,172)
	WPD1 F 1 bright offset(0,192) A_TakeInventory("MonsterManaBlue",1)
	TNT1 A 4 offset(0,32) A_TakeInventory("MonsterManaGreen",1)
	TNT1 A 0 A_TakeInventory("MonsterManaGreen",1)
	TNT1 A 4 A_TakeInventory("MonsterManaBlue",1)
	TNT1 A 0 A_TakeInventory("MonsterManaGreen",1)
	TNT1 A 5 A_TakeInventory("MonsterManaBlue",1)
	TNT1 A 0 A_TakeInventory("MonsterManaGreen",1)
	TNT1 A 7 A_TakeInventory("MonsterManaBlue",1)
	WPD1 GH 7 bright
	WPD1 I 16 bright A_TakeInventory("shooting",1)
	TNT1 A 0 ACS_ExecuteAlways(451,0,6,100)
	TNT1 A 16 A_JumpIfInventory("MonsterManaBlue",10,1)
	goto Delay
	TNT1 A 16 A_JumpIfInventory("MonsterManaGreen",10,1)
	goto Delay
	TNT1 A 16
	goto Ready+4
  Delay:
	TNT1 A 180
	goto Ready+4
	}
}

actor InvisResurrector
{
	+ISMONSTER
	+SLIDESONWALLS
	+THRUACTORS
	+NOTARGET
	+FLOORHUGGER
	species "MonsterMorph"
	radius 1
	height 1
	speed 4
	states
	{
		Spawn:
			TNT1 A 0
			TNT1 AAAAAAAAAAAA 1 A_Chase(0,0,CHF_RESURRECT)
			stop
		Heal:
			TNT1 A 1
			stop
	}
}

actor ReviveAttempt : Inventory { inventory.maxamount 50 }

actor VileWeaponFire
{
	PROJECTILE
	+SEEKERMISSILE
	+FORCERADIUSDMG
	+DONTREFLECT
	+STEPMISSILE
	+MTHRUSPECIES
	+NOCLIP
	renderstyle add
	alpha 1
	speed 0
	radius 1
	height 32
	damagetype "fire"
	obituary "%o was incinerated by %k."
	States
  {
  Spawn:
  	FIR0 A 0
    FIR0 A 2 bright A_Fire
    FIR0 BAB 2 bright A_Fire
	FIR0 C 0 A_PlaySound("0vile/firecrkl")
    FIR0 C 2 bright A_Fire
    FIR0 BCBCDCDCDEDED 2 bright A_Fire
	FIR0 E 0 A_PlaySound("0vile/firecrkl")
    FIR0 E 2 bright A_Fire
    FIR0 FEFEFGHGHG 2 bright A_Fire
	TNT1 A 0 A_JumpIfTargetInLOS("Explode",360,JLOSF_DEADNOJUMP)
	stop
  Explode:
	TNT1 A 0 A_PlaySound("0vile/stop")
	TNT1 A 0 A_Explode(200 * ACS_ExecuteWithResult(455,5) / 100,70,0)
	TNT1 A 0 A_SetArg(3,20 * ACS_ExecuteWithResult(455,4))
	goto Doom1
		Doom1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Doom4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Doom2")
			TNT1 A 1 A_GiveToTarget("DoomDamagePoint",args[3])
			stop
		Doom2:
			TNT1 A 1 A_GiveToTarget("DoomDamagePoint",args[3] * 2)
			stop
		Doom4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Doom8")
			TNT1 A 1 A_GiveToTarget("DoomDamagePoint",args[3] * 4)
			stop
		Doom8:
			TNT1 A 1 A_GiveToTarget("DoomDamagePoint",args[3] * 8)
			stop
  }
}

actor VileWeaponFire2 : VileWeaponFire
{
	+FOILINVUL
	states
	{
		Spawn:
			FIR0 A 0
			FIR0 A 2 bright A_Fire
			FIR0 A 0 A_Explode(random(14,18) * ACS_ExecuteWithResult(455,5) / 100,70,0)
			FIR0 BAB 2 bright A_Fire
			FIR0 A 0 A_Explode(random(14,18) * ACS_ExecuteWithResult(455,5) / 100,70,0)
			FIR0 C 0 A_PlaySound("0vile/firecrkl")
			FIR0 C 2 bright A_Fire
			FIR0 BC 2 bright A_Fire
			FIR0 A 0 A_Explode(random(14,18) * ACS_ExecuteWithResult(455,5) / 100,70,0)
			FIR0 BCD 2 bright A_Fire
			FIR0 A 0 A_Explode(random(14,18) * ACS_ExecuteWithResult(455,5) / 100,70,0)
			FIR0 CDC 2 bright
			FIR0 A 0 A_Explode(random(14,18) * ACS_ExecuteWithResult(455,5) / 100,70,0)
			FIR0 DED 2 bright
			FIR0 A 0 A_Explode(random(14,18) * ACS_ExecuteWithResult(455,5) / 100,70,0)
			FIR0 ED 2 bright
			FIR0 E 0 A_PlaySound("0vile/firecrkl")
			FIR0 E 2 bright A_Fire
			FIR0 A 0 A_Explode(random(14,18) * ACS_ExecuteWithResult(455,5) / 100,70,0)
			FIR0 FEF 2 bright A_Fire
			FIR0 A 0 A_Explode(random(14,18) * ACS_ExecuteWithResult(455,5) / 100,70,0)
			FIR0 EFGHGHG 2 bright A_Fire
			TNT1 A 0 A_JumpIfTargetInLOS("Explode",360,JLOSF_DEADNOJUMP)
			stop
		Explode:
			TNT1 A 0 A_PlaySound("0vile/stop")
			TNT1 A 8 A_Explode(random(212,240) * ACS_ExecuteWithResult(455,5) / 100,70,0)
			TNT1 A 0 A_CustomMissile("VileWeaponFire3",0,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER,4)
			TNT1 A 0 A_CustomMissile("VileWeaponFire3",0,0,45,CMF_AIMDIRECTION|CMF_TRACKOWNER,4)
			TNT1 A 0 A_CustomMissile("VileWeaponFire3",0,0,90,CMF_AIMDIRECTION|CMF_TRACKOWNER,4)
			TNT1 A 0 A_CustomMissile("VileWeaponFire3",0,0,135,CMF_AIMDIRECTION|CMF_TRACKOWNER,4)
			TNT1 A 0 A_CustomMissile("VileWeaponFire3",0,0,180,CMF_AIMDIRECTION|CMF_TRACKOWNER,4)
			TNT1 A 0 A_CustomMissile("VileWeaponFire3",0,0,-135,CMF_AIMDIRECTION|CMF_TRACKOWNER,4)
			TNT1 A 0 A_CustomMissile("VileWeaponFire3",0,0,-90,CMF_AIMDIRECTION|CMF_TRACKOWNER,4)
			TNT1 A 0 A_CustomMissile("VileWeaponFire3",0,0,-45,CMF_AIMDIRECTION|CMF_TRACKOWNER,4)
			TNT1 A 0 A_SetArg(3,35 * ACS_ExecuteWithResult(455,4))
			goto Doom1
	}
}

actor VileWeaponFire3
{
	PROJECTILE
	+MTHRUSPECIES
	+STEPMISSILE
	radius 16
	height 32
	damagetype fire
	damage (random(20,30))
	deathsound "beast/attack"
	renderstyle add
	speed 4
	obituary "%o was incinerated by %k."
	states
	{
		Spawn:
			FIR0 ABAB 2 bright
			FIR0 C 2 bright A_PlaySound("0vile/firecrkl",chan_voice,0.35)
			FIR0 BCBCDCDCDEDED 2 bright
			FIR0 E 2 bright A_PlaySound("0vile/firecrkl",chan_voice,0.35)
			FIR0 FEFEFGHGHGH 2 bright
			stop
		Crash:
		Death:
			"----" AAAAAAAAAAAAAAAA 1 A_FadeOut(0.06)
			stop
		XDeath:
			"----" A 0 A_SetArg(3,25 * ACS_ExecuteWithResult(455,4) / 10)
		Doom1:
			"----" A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			"----" A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Doom4")
			"----" A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			"----" A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Doom2")
			"----" A 0 A_GiveToTarget("DoomDamagePoint",args[3])
			goto Death
		Doom2:
			"----" A 0 A_GiveToTarget("DoomDamagePoint",args[3] * 2)
			goto Death
		Doom4:
			"----" A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			"----" A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Doom8")
			"----" A 0 A_GiveToTarget("DoomDamagePoint",args[3] * 4)
			goto Death
		Doom8:
			"----" A 0 A_GiveToTarget("DoomDamagePoint",args[3] * 8)
			goto Death
	}
}

actor ArchvileMorph : MonsterMorph
{
	tag "Arch-Vile"
	inventory.icon "ARTIVILE"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("ArchvileHitboxCheck",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(450,0,11)
			stop
	}
}

actor ArchvileMorphDirect : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,11)
			stop
	}
}

actor ArchvileHitboxCheck : BaseHitboxCheck
{
	height 72
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_GiveToTarget("ArchvileMorphDirect",1)
			stop
	}
}

actor ArchvileMorpher : MonsterMorpher { powerup.type MorphVile }

actor ArchvileMorpherHitbox : ArchvileMorpher { powerup.type MorphVileHitbox }

actor PowerMorphVile : PowerMorphDoom { powermorph.playerclass "ArchvileClass" }

actor PowerMorphVileHitbox : PowerMorphDoom { powermorph.playerclass "ArchvileClassHitbox" }