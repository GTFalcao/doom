
actor WyvernClass : MonsterPawnHexen
{
	player.morphweapon "WyvernWeapon"
	health 640
	mass 1500
	player.maxhealth 640
	//speed 0.8
	painchance 128
	player.forwardmove 0, 0
  	player.sidemove 0, 0
	player.jumpz 0
	player.soundclass "drag"
	bloodtype "HereticNoBlood"
	states
	{
		Spawn:
			DRAG AAAA 1 ThrustThing(angle*256/360, args[0], 0, 0)
			DRAG A 0 A_PlaySound("DragonWingFlap",CHAN_BODY)
			DRAG BBBBCCCCDDDDCCCCBBBB 1 ThrustThing(angle*256/360, args[0], 0, 0)
			loop
		See:
			DRAG A 1
			goto Spawn
		Melee:
			DRAG E 1
			DRAG EEE 1 ThrustThing(angle*256/360, args[0], 0, 0)
			goto Missile
		Missile:
			DRAG EEEE 1 ThrustThing(angle*256/360, args[0], 0, 0)
			goto Spawn
		Pain:
			DRAG G 0 A_Stop
			DRAG G 4 A_PlaySound("DragonPain",2)
			goto Spawn
		Death:
			DRAG G 5 A_PlaySound("DragonDeath", CHAN_BODY, 1, 0, ATTN_NONE)
			DRAG H 4 A_Fall
			DRAG IJ 4
			DRAG J 1 A_JumpIf(velz >= 0,"Fall")
			wait
		Fall:
			DRAG KL 5
			DRAG M -1
			stop
	}
}

actor WyvernClassHitbox : WyvernClass
{
	height 64
	radius 40
	player.viewheight 52
	player.attackzoffset 4
}

actor WyvernWeapon : BaseMonsterWeapon
{
	tag "Explosive fire spit"
	obituary "%o was incinerated by %k."
	weapon.sisterweapon "WyvernWeaponPowered"
	weapon.ammotype1 "MonsterManaBlue"
	weapon.ammotype2 "MonsterManaGreen"
	states
	{
	Ready:
		TNT1 A 10 A_SetArg(0,1)
		TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,1) == 0,2)
		TNT1 A 0 A_GiveInventory("MonFlight",1)
		TNT1 A 1 A_WeaponReady
		wait
  	Fire:
		TNT1 A 0 A_TakeInventory("MonsterManaBlue",6)
		TNT1 A 0 A_TakeInventory("MonsterManaGreen",6)
		TNT1 A 8 A_FireCustomMissile("DragonFireball0",0,0,0,9)
		TNT1 A 56 A_JumpIfInventory("MonsterManaBlue",6,2)
		TNT1 A 20
		goto Ready+3
		TNT1 A 56 A_JumpIfInventory("MonsterManaGreen",6,1)
		TNT1 A 20
		goto Ready+3
	AltFire:
		TNT1 A 0 A_JumpIf(args[0] == 2,"SpeedUp")
		TNT1 A 12 A_SetArg(0,2)
		TNT1 AAAAAAAAAAAA 1 A_WeaponReady(WRF_NOSECONDARY)
		goto Ready+3
	SpeedUp:
		TNT1 A 12 A_SetArg(0,1)
		TNT1 AAAAAAAAAAAA 1 A_WeaponReady(WRF_NOSECONDARY)
		goto Ready+3
	}
}

actor WyvernWeaponPowered : WyvernWeapon
{
	tag "Extra explosive fire spit"
	weapon.sisterweapon "WyvernWeapon"
	+WEAPON.POWERED_UP
	states
	{
		Fire:
			TNT1 A 0 A_GunFlash
			TNT1 A 0 A_TakeInventory("MonsterManaBlue",15)
			TNT1 A 0 A_TakeInventory("MonsterManaGreen",15)
			TNT1 A 6 A_FireCustomMissile("DragonFireball02",0,0,0,9)
			TNT1 A 64 A_JumpIfInventory("MonsterManaBlue",15,2)
			TNT1 A 26
			goto Ready+3
			TNT1 A 64 A_JumpIfInventory("MonsterManaGreen",15,1)
			TNT1 A 26
			goto Ready+3
	}
}

actor DragonFireball0 : DragonFireball
{
	radius 10
	damage (3 * random(1,8) * ACS_ExecuteWithResult(455,5) / 10)
	fastspeed 28
	seesound "DragonAttack"
	obituary "%o was incinerated by %k."
	+MTHRUSPECIES
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,24 * ACS_ExecuteWithResult(455,4))
			goto Hexen1
		Hexen1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Hexen4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Hexen2")
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",args[3])
			goto Death
		Hexen2:
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",args[3] * 2)
			goto Death
		Hexen4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Hexen8")
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",args[3] * 4)
			goto Death
		Hexen8:
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",args[3] * 8)
			goto Death
		Death:
    		DRFX G 4 bright
			DRFX H 4 bright A_Explode(random(85,132) * ACS_ExecuteWithResult(455,5) / 100,100,0)
			DRFX IIII 0 A_SpawnItemEx("DragonExplosion0",frandom(-4.0,4.0),frandom(-4.0,4.0),frandom(-3.0,3.0),frandom(-1.5,1.5),frandom(-1.5,1.5),frandom(0.1,0.5),0,SXF_ABSOLUTEVELOCITY|SXF_CLIENTSIDE,85)
    		DRFX I 4 bright A_SpawnItemEx("DragonExplosion0",frandom(-4.0,4.0),frandom(-4.0,4.0),frandom(-3.0,3.0),frandom(-1.5,1.5),frandom(-1.5,1.5),frandom(0.1,0.5),0,SXF_ABSOLUTEVELOCITY|SXF_CLIENTSIDE,0)
    		DRFX J 4 bright
			DRFX KL 3 bright
    		stop
	}
}

actor DragonFireball02 : DragonFireball0
{
	speed 36
	fastspeed 36
	damage (4 * random(1,8) * ACS_ExecuteWithResult(455,5) / 10)
	+FORCERADIUSDMG
	+DONTREFLECT
	+FOILINVUL
	states
	{
		Spawn:
			DRFX AAAAAABBBBBBCCCCCCDDDDDDEEEEEEFFFFFF 1 bright A_SpawnItemEx("DragonFireball02Smoke",random(-4,4),random(-4,4),frandom(-2.0,2.0),frandom(-1.0,1.0),frandom(-1.0,1.0),frandom(-1.0,1.0),angle,SXF_CLIENTSIDE|SXF_ABSOLUTEVELOCITY|SXF_ABSOLUTEANGLE)
			loop
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,50 * ACS_ExecuteWithResult(455,4))
			goto Hexen1
		Hexen1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Hexen4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Hexen2")
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",args[3])
			goto Death
		Hexen2:
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",args[3] * 2)
			goto Death
		Hexen4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Hexen8")
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",args[3] * 4)
			goto Death
		Hexen8:
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",args[3] * 8)
			goto Death
		Death:
    		DRFX G 4 bright
			DRFX H 0 A_RadiusThrust(5120,256,0)
			DRFX H 0 A_SetArg(1,7)
			DRFX H 0 A_JumpIf(z - floorz < 12,"Ground")
		Explosion:
			DRFX H 0 A_SetArg(1,args[1] - 1)
			DRFX H 0 A_SetArg(2,random(8,15))
			DRFX H 0 A_SpawnItemEx("DragonExplosion02",0,0,0,args[2] * cos(angle + (args[1] * 45) + random(8,15)) / 2,args[2] * sin(angle + (args[1] * 45) + random(8,15)) / 2,frandom(-0.3,0.3),0,SXF_ABSOLUTEVELOCITY|SXF_TRANSFERPOINTERS)
			DRFX H 0 A_JumpIf(args[1] >= 0,"Explosion")
		Finish:
			DRFX H 4 bright A_Explode(random(110,150) * ACS_ExecuteWithResult(455,5) / 100,100,0)
    		DRFX IJ 4 bright
			DRFX KL 3 bright
    		stop
		Ground:
			DRFX H 0 A_SetArg(1,args[1] - 1)
			DRFX H 0 A_SetArg(2,random(8,15))
			DRFX H 0 A_SpawnItemEx("DragonExplosion02Floor",0,0,0,args[2] * cos(angle + (args[1] * 45) + random(8,15)) / 2,args[2] * sin(angle + (args[1] * 45) + random(8,15)) / 2,0,0,SXF_ABSOLUTEVELOCITY|SXF_TRANSFERPOINTERS)
			DRFX H 0 A_JumpIf(args[1] >= 0,"Ground")
			goto Finish
	}
}

actor DragonFireball02Smoke
{
	+NOBLOCKMAP
	+NOINTERACTION
	+CLIENTSIDEONLY
	renderstyle translucent
	alpha 0.35
	states
	{
		Spawn:
			DRFX M 4
			DRFX M 0 A_SpawnItemEx("DragonFireball02Smoke2",0,0,0,velx,vely,velz,angle,SXF_CLIENTSIDE|SXF_ABSOLUTEVELOCITY|SXF_ABSOLUTEANGLE)
			stop
	}
}

actor DragonFireball02Smoke2 : DragonFireball02Smoke
{
	alpha 0.3
	scale 0.75
	states
	{
		Spawn:
			DRFX M 4
			DRFX M 0 A_SpawnItemEx("DragonFireball02Smoke3",0,0,0,velx,vely,velz,angle,SXF_CLIENTSIDE|SXF_ABSOLUTEVELOCITY|SXF_ABSOLUTEANGLE)
			stop
	}
}

actor DragonFireball02Smoke3 : DragonFireball02Smoke
{
	alpha 0.25
	scale 0.5
	states
	{
		Spawn:
			DRFX M 4
			DRFX M 0 A_SpawnItemEx("DragonFireball02Smoke4",0,0,0,velx,vely,velz,angle,SXF_CLIENTSIDE|SXF_ABSOLUTEVELOCITY|SXF_ABSOLUTEANGLE)
			stop
	}
}

actor DragonFireball02Smoke4 : DragonFireball02Smoke
{
	alpha 0.2
	scale 0.25
	states
	{
		Spawn:
			DRFX M 4
			stop
	}
}

actor DragonExplosion0 : DragonFireball02Smoke
{
	renderstyle add
	states
	{
		Spawn:
    		CFCF Q 4 bright
    		CFCF R 3 bright
    		CFCF S 4 bright A_ScaleVelocity(0.875)
    		CFCF T 3 bright 
    		CFCF U 4 bright
    		CFCF V 3 bright A_ScaleVelocity(0.875)
    		CFCF W 4 bright
    		CFCF X 3 bright
    		CFCF Y 4 bright A_ScaleVelocity(0.75)
    		CFCF Z 3 bright
    		stop
	}
}

actor DragonExplosion02
{
	height 12
	radius 12
	speed 10
	renderstyle add
	damagetype "fire"
	obituary "%o was incinerated by %k."
	PROJECTILE
	+THRUACTORS
	+DONTREFLECT
	+FOILINVUL
	states
	{
		Spawn:
			CFCF Q 4 bright
    		CFCF R 3 bright A_Explode(random(14,22) * ACS_ExecuteWithResult(455,5) / 100,100,0)
    		CFCF S 4 bright A_ScaleVelocity(0.9)
    		CFCF T 3 bright A_Explode(random(14,22) * ACS_ExecuteWithResult(455,5) / 100,100,0)
    		CFCF U 4 bright A_ScaleVelocity(0.9)
    		CFCF V 3 bright A_Explode(random(14,22) * ACS_ExecuteWithResult(455,5) / 100,100,0)
    		CFCF W 4 bright A_ScaleVelocity(0.9)
    		CFCF X 3 bright
    		CFCF Y 4 bright A_ScaleVelocity(0.75)
    		CFCF Z 3 bright
    		stop
	}
}

actor DragonExplosion02Floor : DragonExplosion02 { +FLOORHUGGER }

actor WyvernMorph : MonsterMorph
{
	tag "Death Wyvern"
	inventory.icon "ARTIDRAG"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("WyvernHitboxCheck",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(450,0,10)
			stop
	}
}

actor WyvernMorphDirect : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,10)
			stop
	}
}

actor WyvernHitboxCheck : BaseHitboxCheck
{
	height 64
	radius 40
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_GiveToTarget("WyvernMorphDirect",1)
			stop
	}
}

actor WyvernMorpher : MonsterMorpher { powerup.type MorphDrag }

actor PowerMorphDrag : PowerMorphHexen { powermorph.playerclass "WyvernClass" }

actor WyvernMorpherHitbox : MonsterMorpher { powerup.type MorphDragHitbox }

actor PowerMorphDragHitbox : PowerMorphHexen { powermorph.playerclass "WyvernClassHitbox" }