
actor StalkerClass : MonsterPawnHexen
{
	player.morphweapon "StalkerWeapon"
	health 90
	player.maxhealth 90
	gibhealth 45
	speed 0.5
	mass 250
	player.soundclass "serp"
	states
	{
		Spawn:
			SSPT I 3 A_JumpIfInventory("submerged",1,"Spawn2")
			SSPT I 3 A_JumpIf((velx > 0) || (vely > 0), "See")
			loop
		Spawn2:
			SSPT H 3 A_JumpIf((velx > 0) || (vely > 0), "See2")
			SSPT H 3 A_JumpIfInventory("submerged",1,"Spawn2")
			goto Spawn
		See:
			SSPT I 0 A_JumpIfInventory("submerged",1,"See2")
			SSPT IIJJ 4 A_JumpIf((velx == 0) && (vely == 0), "Spawn")
			loop
		See2:
			SSPT E 0 A_Jump(200,2)
			SSPT E 0 A_PlaySound("SerpentActive",CHAN_VOICE)
			SSPT EFGEFG 5 A_JumpIf((velx == 0) && (vely == 0), "Spawn2")
			loop
		Pain:
			SSPT L 8 A_PlaySound("SerpentPain",2)
			goto Spawn
		Missile:
			SSPT K 8
			SSPT LM 4
			SSPT N 6
			goto Spawn
		Melee:
			SSPT A 3
			SSPT A 4 A_PlaySound("SerpentBirth",6)
			SSPT B 5
			SSPT C 5
			SSPT D 6
			goto Spawn
		Dive:
			SSDV ABC 5
    		SSDV D 5 A_PlaySound("SerpentBirth",6)
    		SSDV EFGHIJ 5
    		Goto Spawn2
		Death:
    		SSPT O 4
    		SSPT P 4 A_PlayerScream
    		SSPT Q 4 A_NoBlocking
    		SSPT RSTUV 4
			SSPT W -1
    		stop
  		XDeath:
    		SSXD A 4
			SSXD B 0 A_PlayerScream
    		SSXD B 4 A_SpawnItemEx("SerpentHead0",0,0,56,frandom(-3.0,3.0),frandom(-3.0,3.0),frandom(1.0,2.0),0,32)
    		SSXD C 4 A_NoBlocking
    		SSXD DE 4
    		SSXD FG 3
    		SSXD H 0 A_SpawnItemEx("SerpentGib01",random(-8,8),random(-8,8),1,frandom(-0.125,0.125),frandom(-0.125,0.125),0,0,32)
    		SSXD H 0 A_SpawnItemEx("SerpentGib02",random(-8,8),random(-8,8),1,frandom(-0.125,0.125),frandom(-0.125,0.125),0,0,32)
    		SSXD H 3 A_SpawnItemEx("SerpentGib03",random(-8,8),random(-8,8),1,frandom(-0.125,0.125),frandom(-0.125,0.125),0,0,32)
			SSXD T -1
    		stop
	}
}

actor StalkerClassHitbox : StalkerClass { radius 28 }

actor SerpentHead0
{
	radius 6
	height 10
	+NOBLOCKMAP
	gravity 0.125
	states
  	{
  		Spawn:
    		SSXD IJKLMNOP 4 A_CheckFloor("Stay")
    		loop
  		Stay:
			SSXD S -1
    		loop
  }
}

actor SerpentGib01 : SerpentGib1
{
	states
	{
		Spawn:
			goto Super::Spawn+5
	}
}

actor SerpentGib02 : SerpentGib2
{
	states
	{
		Spawn:
			goto Super::Spawn+5
	}
}

actor SerpentGib03 : SerpentGib3
{
	states
	{
		Spawn:
			goto Super::Spawn+5
	}
}

actor submerged : inventory { inventory.maxamount 1 }

actor StalkerWeapon : BaseMonsterWeapon
{
	weapon.ammotype1 "MonsterManaBlue"
	weapon.ammotype2 "MonsterManaGreen"
	weapon.sisterweapon "StalkerWeaponPowered"
	tag "Slimy Claws"
	+WEAPON.NOALERT
	states
	{
		Select:
			WPX1 A 1 A_Raise
			wait
		Deselect:
			WPX1 A 1 A_Lower
			wait
		Ready:
			WPX1 A 0 A_SetArg(0,1)
			WPX1 A 1 A_WeaponReady
			wait
		Ready2:
			TNT1 A 0 A_TakeInventory("MonsterManaBlue",1)
			TNT1 A 0 A_JumpIfInventory("MonsterManaBlue",1,1)
			goto Rise
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_WeaponReady(WRF_NOPRIMARY)
			loop
		RaiseAttack:
			WPX1 B 1 offset(-87,113)
			WPX1 B 1 offset(-75,110)
			WPX1 B 1 offset(-63,97)
			WPX1 B 1 offset(-51,84)
			goto Fire+1
  		Fire:
			WPX1 B 0 A_JumpIfInventory("shooting",1,"RaiseAttack")
			WPX1 B 0 A_JumpIfTargetInLOS("Claw",0,JLOSF_ALLYNOJUMP,150)
			WPX1 B 1 offset(-39,71)
			WPX1 B 1 offset(-26,58)
			WPX1 B 1 offset(-13,45)
			WPX1 B 7 offset(0,32) A_GiveInventory("shooting",1)
			WPX1 B 1 offset(-16, 44) A_AlertMonsters
			WPX1 B 1 offset(-32, 56)
			WPX1 B 1 offset(-48, 68)
			WPX1 B 1 offset(-64, 80) A_TakeInventory("MonsterManaGreen",2)
			WPX1 B 1 offset(-80, 92) A_FireCustomMissile("SerpentFX0",0,0,0,9)
			WPX1 B 1 offset(-96, 104)
			WPX1 B 1 offset(-112, 116)
			WPX1 B 1 offset(-128, 128)
			TNT1 A 4 offset(0,32)
			TNT1 A 0 A_JumpIfInventory("MonsterManaGreen",2,1)
			goto FireDone
			TNT1 AAAAAAAA 1 A_WeaponReady(WRF_NOPRIMARY)
			TNT1 A 1 A_ReFire
		FireDone:
			WPX1 A 1 offset(0,80) A_TakeInventory("shooting",1)
			WPX1 A 1 offset(0,72)
			WPX1 A 1 offset(0,64)
			WPX1 A 1 offset(0,56)
			WPX1 A 1 offset(0,48)
			WPX1 A 1 offset(0,40)
			WPX1 A 1 offset(0,32)
			WPX1 A 30 A_JumpIfInventory("MonsterManaGreen",2,"Ready")
			WPX1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_WeaponReady(WRF_NOPRIMARY)
			goto Ready
		Claw:
			WPX1 B 1 offset(-39,71)
			WPX1 B 1 offset(-26,58)
			WPX1 B 1 offset(-13,45)
			WPX1 B 3 offset(0,32) A_GiveInventory("shooting",1)
			WPX1 B 5 A_PlaySound("SerpentAttack",CHAN_WEAPON)
			WPX1 B 1 offset(-16, 44)
			WPX1 B 1 offset(-32, 56)
			WPX1 B 1 offset(-48, 68)
			WPX1 B 1 offset(-64, 80)
			WPX1 B 1 offset(-80, 92)
			WPX1 B 0 A_FireCustomMissile("StalkerMeleeProj",0,0,0,9)
			WPX1 B 1 offset(-96, 104) A_JumpIfInventory("BerserkTracker",1,1)
			goto ContinueClaw
			WPX1 B 1 offset(-96, 104) A_FireCustomMissile("StalkerMeleeProj",0,0,0,9)
		ContinueClaw:
			WPX1 B 1 offset(-112, 116)
			WPX1 B 1 offset(-128, 128)
			TNT1 A 4 offset(0,32)
			TNT1 AAAAA 1 A_WeaponReady(WRF_NOPRIMARY)
			TNT1 A 1 A_ReFire
			WPX1 A 1 offset(0,80)
			WPX1 A 1 offset(0,72)
			WPX1 A 1 offset(0,64)
			WPX1 A 1 offset(0,56)
			WPX1 A 1 offset(0,48)
			WPX1 A 1 offset(0,40)
			WPX1 A 1 offset(0,32)
			goto Ready
		AltFire:
			TNT1 A 0 A_JumpIfInventory("submerged",1,"Rise")
			TNT1 A 25 A_JumpIfInventory("MonsterManaBlue",2,1)
			goto Ready
			TNT1 A 0 A_Stop
			TNT1 A 0 SetPlayerProperty(0,1,4)
			TNT1 A 0 ACS_ExecuteAlways(451,0,3)
			TNT1 A 0 A_GiveInventory("submerged",1)
			TNT1 A 0 A_UnSetShootable
			TNT1 A 0 A_JumpIfInventory("shooting",1,1)
			goto LowerHands
			TNT1 A 55
			TNT1 A 25 SetPlayerProperty(0,0,4)
			goto Ready2
		LowerHands:
			WPX1 A 1 offset(0,32)
			WPX1 A 1 offset(0,36)
			WPX1 A 1 offset(0,40)
			WPX1 A 1 offset(0,44)
			WPX1 A 1 offset(0,48)
			WPX1 A 1 offset(0,52)
			WPX1 A 1 offset(0,56)
			WPX1 A 1 offset(0,60)
			WPX1 A 1 offset(0,64)
			WPX1 A 1 offset(0,68)
			WPX1 A 1 offset(0,72)
			WPX1 A 1 offset(0,76)
			WPX1 A 1 offset(0,80)
			TNT1 A 42 offset(0,32)
			TNT1 A 25 SetPlayerProperty(0,0,4)
			goto Ready2
		Rise:
			TNT1 A 0 A_GunFlash
			TNT1 A 0 A_Stop
			TNT1 A 0 A_TakeInventory("submerged",1)
			WPX1 A 1 offset(0,80) SetPlayerProperty(0,1,4)
			WPX1 A 1 offset(0,76)
			WPX1 A 1 offset(0,72)
			WPX1 A 1 offset(0,68)
			WPX1 A 1 offset(0,64)
			WPX1 A 1 offset(0,60)
			WPX1 A 1 offset(0,56)
			WPX1 A 1 offset(0,52)
			WPX1 A 1 offset(0,48)
			WPX1 A 1 offset(0,44)
			WPX1 A 1 offset(0,40)
			WPX1 A 1 offset(0,36)
			WPX1 A 1 offset(0,32)
			WPX1 A 10
			WPX1 A 0 A_SetShootable
			WPX1 A 8 SetPlayerProperty(0,0,4)
			goto Ready
	}
}

actor StalkerWeaponPowered : StalkerWeapon
{
	weapon.sisterweapon "StalkerWeapon"
	+WEAPON.POWERED_UP
	states
	{
		Ready2:
			TNT1 A 0 A_TakeInventory("MonsterManaBlue",1)
			TNT1 A 0 A_JumpIfInventory("MonsterManaBlue",1,1)
			goto Rise
			TNT1 A 0 HealThing(1)
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_WeaponReady(WRF_NOPRIMARY)
			loop
		RaiseAttack:
			WPX1 B 1 offset(-87,113)
			WPX1 B 1 offset(-75,110)
			WPX1 B 1 offset(-63,97)
			WPX1 B 1 offset(-51,84)
			goto Fire+1
  		Fire:
			WPX1 B 0 A_JumpIfInventory("shooting",1,"RaiseAttack")
			WPX1 B 1 offset(-39,71)
			WPX1 B 1 offset(-26,58)
			WPX1 B 1 offset(-13,45)
			WPX1 B 7 offset(0,32) A_GiveInventory("shooting",1)
			WPX1 B 1 offset(-16, 44) A_AlertMonsters
			WPX1 B 1 offset(-32, 56)
			WPX1 B 1 offset(-48, 68)
			WPX1 B 1 offset(-64, 80) A_TakeInventory("MonsterManaGreen",5)
			WPX1 B 1 offset(-80, 92) A_FireCustomMissile("SerpentFX02",0,0,0,9)
			WPX1 B 1 offset(-96, 104)
			WPX1 B 1 offset(-112, 116)
			WPX1 B 1 offset(-128, 128)
			TNT1 A 15 offset(0,32)
			TNT1 A 0 A_JumpIfInventory("MonsterManaGreen",2,1)
			goto FireDone
			TNT1 AAAAAAAA 1 A_WeaponReady(WRF_NOPRIMARY)
			TNT1 A 1 A_ReFire
		FireDone:
			WPX1 A 1 offset(0,80) A_TakeInventory("shooting",1)
			WPX1 A 1 offset(0,72)
			WPX1 A 1 offset(0,64)
			WPX1 A 1 offset(0,56)
			WPX1 A 1 offset(0,48)
			WPX1 A 1 offset(0,40)
			WPX1 A 1 offset(0,32)
			WPX1 A 30 A_JumpIfInventory("MonsterManaGreen",5,"Ready")
			WPX1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_WeaponReady(WRF_NOPRIMARY)
			goto Ready
		AltFire:
			TNT1 A 0 A_JumpIfInventory("submerged",1,"Rise")
			TNT1 A 25 A_JumpIfInventory("MonsterManaBlue",2,1)
			goto Ready
			TNT1 A 0 A_Stop
			TNT1 A 0 SetPlayerProperty(0,1,4)
			TNT1 A 0 ACS_ExecuteAlways(451,0,3)
			TNT1 A 0 A_GiveInventory("submerged",1)
			TNT1 A 0 A_UnSetShootable
			TNT1 A 0 A_JumpIfInventory("shooting",1,1)
			goto LowerHands
			TNT1 A 55
			TNT1 A 25 SetPlayerProperty(0,0,4)
			goto Ready2
		LowerHands:
			WPX1 A 1 offset(0,32)
			WPX1 A 1 offset(0,36)
			WPX1 A 1 offset(0,40)
			WPX1 A 1 offset(0,44)
			WPX1 A 1 offset(0,48)
			WPX1 A 1 offset(0,52)
			WPX1 A 1 offset(0,56)
			WPX1 A 1 offset(0,60)
			WPX1 A 1 offset(0,64)
			WPX1 A 1 offset(0,68)
			WPX1 A 1 offset(0,72)
			WPX1 A 1 offset(0,76)
			WPX1 A 1 offset(0,80)
			TNT1 A 42 offset(0,32)
			TNT1 A 25 SetPlayerProperty(0,0,4)
			goto Ready2
	}
}

actor StalkerMeleeProj : ImpMeleeProj
{
	deathsound "SerpentMeleeHit"
	obituary "%o was mauled by %k."
	damage (11 * random(1,8) * ACS_ExecuteWithResult(455,5) / 100)
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,495 * ACS_ExecuteWithResult(455,4) / 100)
			goto Hexen1
	}
}

actor SerpentFX0 : SerpentFX
{
	obituary "%o was melted by %k."
	fastspeed 30
	damage (9 * random(1,8) * ACS_ExecuteWithResult(455,5) / 100)
	+MTHRUSPECIES
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,405 * ACS_ExecuteWithResult(455,4) / 100)
			goto Hexen1
		Hexen1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Hexen4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Hexen2")
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",args[3])
			goto Super::Death
		Hexen2:
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",args[3] * 2)
			goto Super::Death
		Hexen4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Hexen8")
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",args[3] * 4)
			goto Super::Death
		Hexen8:
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",args[3] * 8)
			goto Super::Death
	}
}

actor SerpentFX02 : SerpentFX0
{
	speed 20
	damage (16 * random(1,8) * ACS_ExecuteWithResult(455,5) / 100)
	gravity 0.4
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,72 * ACS_ExecuteWithResult(455,4) / 10)
			goto Hexen1
		Spawn:
			SSFX A 0
			SSFX A 0 A_SetArg(0,8)
			SSFX A 0 A_PlaySound("SerpentFXContinuous",CHAN_VOICE,1.0,1)
			SSXF A 0 A_JumpIf(args[0] <= 0,"Fall")
			SSFX AAABBB 1 bright A_SpawnItemEx("SerpentFXDrop",random(-4,4),random(-4,4),frandom(-2.0,2.0),frandom(-0.25,0.25),frandom(-0.25,0.25),0,random(-180,180),SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY)
			SSFX A 0 A_SetArg(0,args[0] - 1)
			Goto Spawn+3
		Fall:
			SSFX A 0 A_Gravity
			SSFX AAABBB 1 bright A_SpawnItemEx("SerpentFXDrop",random(-4,4),random(-4,4),frandom(-2.0,2.0),frandom(-0.25,0.25),frandom(-0.25,0.25),0,random(-180,180),SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY)
			goto Fall+1
	}
}

actor SerpentFXDrop
{
	+NOBLOCKMAP
	+NOTELEPORT
	+DROPOFF
	scale 0.2
	height 1
	radius 1
	renderstyle add
	states
	{
		Spawn:
			SSFX AAABBB 1 bright A_CheckFloor("Blow")
			loop
		Blow:
			SSFX A 35 bright A_PlaySound("SerpentFXDrip")
			SSFX A 0 A_SpawnItemEx("SerpentFXExplosion",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
			stop
	}
}

actor SerpentFXExplosion
{
	PROJECTILE
	+NODAMAGETHRUST
	+FLOORHUGGER
	+THRUACTORS
	speed 0
	height 12
	radius 6
	renderstyle add
	states
	{
		Spawn:
			SSFX L 0
			SSFX L 0 A_CustomMissile("SerpentFXExplosionScore",0,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER)
			SSFX L 0 A_PlaySound("SerpentFXHit",chan_voice,0.5)
			SSFX L 2 bright A_Explode(4,64,0)
			SSFX K 0 A_ChangeFlag("PAINLESS",1)
			SSFX KJKLMNOP 2 bright A_Explode(4,64,0)
			stop
	}
}

actor SerpentFXExplosionScore : SerpentFXExplosion
{
	-THRUACTORS
	+MTHRUSPECIES
	radius 16
	speed 1
	states
	{
		Spawn:
			TNT1 A 1
			stop
		Death:
			TNT1 A 1
			stop
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,ACS_ExecuteWithResult(455,4))
			goto Hexen1
		Hexen1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Hexen4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Hexen2")
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",args[3])
			goto Death
		Hexen2:
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",args[3] * 2)
			goto Death
		Hexen4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Hexen8")
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",args[3] * 4)
			goto Death
		Hexen8:
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",args[3] * 8)
			goto Death
	}
}

actor StalkerMorph : MonsterMorph
{
	tag "Stalker"
	inventory.icon "ARTISSPT"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("StalkerHitboxCheck",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(450,0,5)
			stop
	}
}

actor StalkerMorphDirect : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,5)
			stop
	}
}

actor StalkerHitboxCheck : BaseHitboxCheck
{
	radius 32
	height 88
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_GiveToTarget("StalkerMorphDirect",1)
			stop
	}
}

actor StalkerMorpher : MonsterMorpher { powerup.type MorphSspt }

actor PowerMorphSspt : PowerMorphHexen { powermorph.playerclass "StalkerClass" }

actor StalkerMorpherHitbox : MonsterMorpher { powerup.type MorphSsptHitbox }

actor PowerMorphSsptHitbox : PowerMorphHexen { powermorph.playerclass "StalkerClassHitbox" }