
actor AfritClass : MonsterPawnHexen
{
	player.morphweapon "AfritWeapon"
	health 80
	player.maxhealth 80
	speed 0.4
	painchance 8
	player.soundclass "afrit"
	bloodtype "FireDemonRock1"
	player.sidemove 1.5, 2.25
	states
	{
		Spawn:
			FDMN ABCB 10 bright
			loop
		Init:
			FDMN X 5
			FDMN EFG 10 bright
			FDMN E 8 bright A_PlaySound("FireDemonSight",2)
			FDMN F 6 bright
			FDMN G 5 bright
			FDMN F 8 bright
			FDMN E 6 bright
			FDMN G 0 A_SpawnItemEx("FireDemonRock1",random(0,8),random(0,8),random(0,8),frandom(-2.0,2.0),frandom(-2.0,2.0),frandom(0.3,2.0),0,32)
			FDMN G 0 A_SpawnItemEx("FireDemonRock2",random(0,8),random(0,8),random(0,8),frandom(-2.0,2.0),frandom(-2.0,2.0),frandom(0.3,2.0),0,32)
			FDMN G 0 A_SpawnItemEx("FireDemonRock3",random(0,8),random(0,8),random(0,8),frandom(-2.0,2.0),frandom(-2.0,2.0),frandom(0.3,2.0),0,32)
			FDMN G 0 A_SpawnItemEx("FireDemonRock4",random(0,8),random(0,8),random(0,8),frandom(-2.0,2.0),frandom(-2.0,2.0),frandom(0.3,2.0),0,32)
			FDMN G 7 bright A_SpawnItemEx("FireDemonRock5",random(0,8),random(0,8),random(0,8),frandom(-2.0,2.0),frandom(-2.0,2.0),frandom(0.3,2.0),0,32)
			FDMN HIJ 5 bright
			goto Spawn
		See:
			FDMN A 5 bright
			FDMN B 4 bright
			FDMN C 5 bright
			FDMN B 4 bright
			loop
		Pain:
			FDMN D 6 bright A_PlaySound("FireDemonPain",2)
			goto Spawn
		Missile:
			FDMN A 1 bright
			goto Spawn
		Melee:
			FDMN K 7 bright
			goto Spawn
		Death:
   		 	FDMN D 4 bright A_FaceTarget
    		FDMN L 4 bright A_PlayerScream
    		FDMN L 4 bright A_NoBlocking
    		FDMN M 5 bright
			FDMN N 5 bright
			FDMN O 0 A_SpawnItemEx("FireDemonSplotch1",0,0,0,random(-4,4),random(-4,4),random(3,7),0,32)
			FDMN O 5 bright A_SpawnItemEx("FireDemonSplotch2",0,0,0,random(-4,4),random(-4,4),random(3,7),0,32)
			FDMN P 5 bright
			FDMN T -1
			stop
	}
}

actor AfritClassHitbox : AfritClass
{
	radius 22
	height 44
	player.attackzoffset 14
}

actor AfritWeapon : BaseMonsterWeapon
{
	obituary "%o tasted %k's fire."
	tag "Fire spitter"
	weapon.ammotype1 "MonsterManaBlue"
	+WEAPON.DONTBOB
	states
	{
		Select:
			WPX0 A 1 bright A_Raise
			wait
		Deselect:
			WPX0 A 1 bright A_Lower
			wait
		Ready:
			WPX0 ABCABCABCABCABCABCABCABC 4 bright
			WPX0 A 0 SetPlayerProperty(0,0,0)
			WPX0 A 0 SetPlayerProperty(0,0,1)
			WPX0 A 0 A_JumpIf(ACS_ExecuteWithResult(455,1) == 0,2)
			WPX0 A 0 A_GiveInventory("MonFlight",1)
		IsReady:
			WPX0 AAAABBBBCCCC 1 bright A_WeaponReady
			loop
		Fire:
			WPX0 D 0 A_JumpIfInventory("PowerWeaponLevel2",1,"FirePowered")
			WPX0 D 3 bright A_GunFlash
			WPX0 D 0 A_GunFlash
			WPX0 D 1 bright A_FireCustomMissile("FireDemonMissile0",0,0,0,9)
			WPX0 E 4 bright A_TakeInventory("MonsterManaBlue",1)
			WPX0 F 0 A_GunFlash
			WPX0 F 4 bright A_FireCustomMissile("FireDemonMissile0",0,0,0,9)
			WPX0 D 1 bright
			WPX0 D 0 A_GunFlash
			WPX0 D 3 bright A_FireCustomMissile("FireDemonMissile0",0,0,0,9)
			WPX0 E 2 bright
			WPX0 B 2 bright
			WPX0 CABC 4 bright
			WPX0 A 4 bright A_JumpIfInventory("MonsterManaBlue",1,"IsReady")
			WPX0 BCABCABCABCABCABC 4 bright
			goto IsReady
		FirePowered:
			WPX0 D 0 A_GunFlash
			WPX0 D 4 bright A_JumpIfInventory("MonsterManaBlue",1,1)
			goto Burst
			WPX0 D 4 bright A_SetArg(4,4)
		Hold:
			WPX0 D 0 A_JumpIfInventory("PowerWeaponLevel2",1,1)
			goto Fire
			WPX0 D 0 A_SetArg(4,args[4] - 1)
			WPX0 D 0 A_JumpIf(args[4] > 0,3)
			WPX0 D 0 A_SetArg(4,4)
			WPD0 D 0 A_GiveInventory("MonsterManaBlue",1) // 3 mana used per 4 attacks
			WPX0 D 0 A_TakeInventory("MonsterManaBlue",1)
			WPX0 E 0 A_GunFlash
			WPX0 E 4 bright A_FireCustomMissile("FireDemonMissile02",0,0,0,9)
			WPX0 F 0 A_GunFlash
			WPX0 F 4 bright A_FireCustomMissile("FireDemonMissile02",0,0,0,9)
			WPX0 D 0 A_GunFlash
			WPX0 D 4 bright A_FireCustomMissile("FireDemonMissile02",0,0,0,9)
			WPX0 E 0 A_JumpIfInventory("MonsterManaBlue",1,1)
			goto Burst
			WPX0 E 4 bright A_ReFire
			WPX0 C 4 bright
			goto IsReady
		Burst:
			WPX0 D 0 A_GunFlash
			WPX0 D 1 bright A_FireCustomMissile("FireDemonMissile0",0,0,0,9)
			WPX0 E 4 bright
			WPX0 F 0 A_GunFlash
			WPX0 F 4 bright A_FireCustomMissile("FireDemonMissile0",0,0,0,9)
			WPX0 D 1 bright
			WPX0 D 0 A_GunFlash
			WPX0 D 3 bright A_FireCustomMissile("FireDemonMissile0",0,0,0,9)
			WPX0 E 2 bright
			WPX0 B 2 bright
			WPX0 CABCABC 4 bright
			goto IsReady
	}
}

actor AfritWeaponPowered : AfritWeapon
{
	weapon.sisterweapon "AfritWeapon"
	+WEAPON.POWERED_UP
	states
	{
		Ready:
			goto IsReady
		Fire:
			WPX0 D 0 A_GunFlash
			WPX0 D 4 bright A_JumpIfInventory("MonsterManaBlue",1,1)
			goto Burst
			WPX0 D 4 bright A_SetArg(4,4)
		Hold:
			WPX0 D 0 A_SetArg(4,args[4] - 1)
			WPX0 D 0 A_JumpIf(args[4] > 0,3)
			WPX0 D 0 A_SetArg(4,4)
			WPD0 D 0 A_GiveInventory("MonsterManaBlue",1) // 3 mana used per 4 attacks
			WPX0 D 0 A_TakeInventory("MonsterManaBlue",1)
			WPX0 E 0 A_GunFlash
			WPX0 E 4 bright A_FireCustomMissile("FireDemonMissile02",0,0,0,9)
			WPX0 F 0 A_GunFlash
			WPX0 F 4 bright A_FireCustomMissile("FireDemonMissile02",0,0,0,9)
			WPX0 D 0 A_GunFlash
			WPX0 D 4 bright A_FireCustomMissile("FireDemonMissile02",0,0,0,9)
			WPX0 E 0 A_JumpIfInventory("MonsterManaBlue",1,1)
			goto Burst
			WPX0 E 4 bright A_ReFire
			WPX0 C 4 bright
			goto IsReady
		Burst:
			WPX0 D 0 A_GunFlash
			WPX0 D 1 bright A_FireCustomMissile("FireDemonMissile0",0,0,0,9)
			WPX0 E 4 bright
			WPX0 F 0 A_GunFlash
			WPX0 F 4 bright A_FireCustomMissile("FireDemonMissile0",0,0,0,9)
			WPX0 D 1 bright
			WPX0 D 0 A_GunFlash
			WPX0 D 3 bright A_FireCustomMissile("FireDemonMissile0",0,0,0,9)
			WPX0 E 2 bright
			WPX0 B 2 bright
			WPX0 CABCABC 4 bright
			goto IsReady
	}
}

actor FireDemonMissile0 : FireDemonMissile
{
	obituary "%o tasted %k's fire."
	damage (random(1,8) * ACS_ExecuteWithResult(455,5) / 100)
	seesound "FireDemonAttack"
	fastspeed 25
	+MTHRUSPECIES
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Hexen4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Hexen2")
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",45 * ACS_ExecuteWithResult(455,4) / 100)
			goto Super::Death
		Hexen2:
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",9 * ACS_ExecuteWithResult(455,4) / 10)
			goto Super::Death
		Hexen4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Hexen8")
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",18 * ACS_ExecuteWithResult(455,4) / 10)
			goto Super::Death
		Hexen8:
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",36 * ACS_ExecuteWithResult(455,4) / 10)
			goto Super::Death
	}
}

actor FireDemonMissile02 : FireDemonMissile0
{
	damage (random(1,5) * ACS_ExecuteWithResult(455,5) / 100)
	states
	{
		Crash:
		XDeath:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Hexen4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Hexen2")
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",3 * ACS_ExecuteWithResult(455,4) / 10)
			goto Super::Death
		Hexen2:
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",6 * ACS_ExecuteWithResult(455,4) / 10)
			goto Super::Death
		Hexen4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Hexen8")
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",12 * ACS_ExecuteWithResult(455,4) / 10)
			goto Super::Death
		Hexen8:
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",24 * ACS_ExecuteWithResult(455,4) / 10)
			goto Super::Death
	}
}

actor AfritMorph : MonsterMorph
{
	tag "Afrit"
	inventory.icon "ARTIFDMN"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("AfritHitboxCheck",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(450,0,0)
			stop
	}
}

actor AfritMorphDirect : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,0)
			stop
	}
}

actor AfritHitboxCheck : BaseHitboxCheck
{
	radius 22
	height 44
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_GiveToTarget("AfritMorphDirect",1)
			stop
	}
}

actor AfritMorpher : MonsterMorpher { powerup.type MorphFdmn }

actor PowerMorphFdmn : PowerMorphHexen { powermorph.playerclass "AfritClass" }

actor AfritMorpherHitbox : MonsterMorpher { powerup.type MorphFdmnHitbox }

actor PowerMorphFdmnHitbox : PowerMorphHexen { powermorph.playerclass "AfritClassHitbox" }