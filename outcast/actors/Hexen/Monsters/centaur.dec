
actor CentaurClass : MonsterPawnHexen
{
	player.morphweapon "CentaurWeapon"
	health 200
	player.maxhealth 200
	gibhealth 100
	speed 0.7
	mass 250
	player.soundclass "cent"
	player.invulnerabilitymode "reflective"
	+SHIELDREFLECT
	states
	{
		Spawn:
			CENT AB 10
			loop
		See:
			CENT ABCD 4
			loop
		Pain:
			CENT G 12 A_PlaySound("CentaurPain",2)
			goto Spawn
		Melee:
			CENT E 8
			goto Spawn
		Missile:
			CENT H 5
			CENT I 4
			CENT J 9
			goto Spawn
		Death:
			CENT K 4
    		CENT L 4 A_PlayerScream
    		CENT MN 4
    		CENT O 4 A_NoBlocking
    		CENT PQRS 4
    		CENT T -1
    		Stop
  		XDeath:
    		CTXD A 4
    		CTXD B 4 A_PlayerScream
    		CTXD C 0 A_SpawnItemEx("CentaurSword", 0, 0, 45, 1 + random[CentaurDrop](-128,127)*0.03125,1 + random[CentaurDrop](-128,127)*0.03125, 8 + random[CentaurDrop](0,255)*0.015625, 270)
    		CTXD C 4 A_SpawnItemEx("CentaurShield", 0, 0, 45, 1 + random[CentaurDrop](-128,127)*0.03125,1 + random[CentaurDrop](-128,127)*0.03125, 8 + random[CentaurDrop](0,255)*0.015625, 90)
    		CTXD D 3 A_Fall
    		CTXD E 4
			CTXD F 3
    		CTXD G 4
    		CTXD H 3
    		CTXD I 4
    		CTXD J 3
    		CTXD K -1
			stop
	}
}

actor CentaurClassHitbox : CentaurClass
{
	height 66
	radius 24
	player.viewheight 58
}

actor CentaurShieldTimer : inventory { inventory.maxamount 8 }

actor CentaurDoingShield : inventory { inventory.maxamount 1 }

actor CentaurWeapon : BaseMonsterWeapon
{
	obituary "%o felt the power of %k's reflective shield."
	tag "Sword and Shield"
	weapon.sisterweapon "CentaurWeaponPowered"
	weapon.ammotype2 "MonsterManaGreen"
	+WEAPON.NOALERT
	+WEAPON.MELEEWEAPON
	states
	{
		Select:
			WPX0 Q 1 A_Raise
			wait
		Deselect:
			WPX0 Q 1 A_Lower
			wait
		Ready:
			WPX0 Q 1 A_WeaponReady
			wait
		Fire:
			WPX0 K 0 A_JumpIf(args[0] <= 0, 6)
			WPX0 K 0 A_TakeInventory("PowerInvulnerable",1)
			WPX0 K 0 A_TakeInventory("CentaurDoingShield",1)
			WPX0 K 0 A_TakeInventory("CentaurShieldTimer",8)
			WPX0 K 0 A_SetArg(0,0)
			WPX0 K 0 SetPlayerProperty(0,0,0)
			WPX0 K 4 offset(0,32)
			WPX0 L 5 A_PlaySound("EttinAttack",CHAN_WEAPON)
			WPX0 P 0 A_FireCustomMissile("CentaurMeleeProj",0,0,0,9)
			WPX0 P 7 A_JumpIfInventory("BerserkTracker",1,2)
			WPX0 P 0 A_ReFire
			goto FireDone
			WPX0 P 7 A_FireCustomMissile("CentaurMeleeProj",0,0,0,9)
			WPX0 P 0 A_ReFire
		FireDone:
			WPX0 P 1 offset(0,45)
			WPX0 P 1 offset(0,58)
			WPX0 P 1 offset(0,71)
			WPX0 P 1 offset(0,84)
			WPX0 P 1 offset(0,97)
			WPX0 P 7 offset(0,110)
			WPX0 Q 1 offset(0,57)
			WPX0 Q 1 offset(0,50)
			WPX0 Q 1 offset(0,44)
			WPX0 Q 1 offset(0,38)
			WPX0 Q 0 offset(0,32)
			goto Ready
		AltFire:
			WPX0 R 0 A_JumpIfInventory("MonsterManaGreen",1,1)
			goto Fire
			WPX0 R 0 A_JumpIfInventory("PowerInvulnerable",1,"Fire")
			WPX0 R 0 A_TakeInventory("MonsterManaGreen",1)
			WPX0 R 0 A_GunFlash
			WPX0 R 0 SetPlayerProperty(0,1,0)
			WPX0 R 1 offset(0,110) A_GiveInventory("MonTempInvul",1)
			WPX0 R 1 offset(0,98)
			WPX0 R 1 offset(0,86)
			WPX0 R 1 offset(0,74)
			WPX0 R 1 offset(0,62) 
			WPX0 R 1 offset(0,51) A_SetArg(0,1)
			WPX0 R 0 A_GiveInventory("CentaurDoingShield",1)
			WPX0 R 0 A_GunFlash
			WPX0 R 6 offset(0,40) A_GiveInventory("CentaurShieldTimer",8)
		HoldShield:
			WPX0 R 0 A_GunFlash
			WPX0 R 0 A_TakeInventory("CentaurShieldTimer",1)
			WPX0 RRRRRR 1 A_WeaponReady(WRF_NOSECONDARY|WRF_NOBOB)
			WPX0 R 2 A_JumpIfInventory("CentaurShieldTimer",1,"HoldShield")
			WPX0 R 0 A_TakeInventory("PowerInvulnerable",1)
			WPX0 K 0 A_TakeInventory("CentaurDoingShield",1)
			WPX0 R 0 A_SetArg(0,0)
			WPX0 R 1 SetPlayerProperty(0,0,0)
			WPX0 R 1 offset(0,49) A_WeaponReady(WRF_NOSECONDARY|WRF_NOBOB)
			WPX0 R 1 offset(0,58) A_WeaponReady(WRF_NOSECONDARY|WRF_NOBOB)
			WPX0 R 1 offset(0,67) A_WeaponReady(WRF_NOSECONDARY|WRF_NOBOB)
			WPX0 R 1 offset(0,76) A_WeaponReady(WRF_NOSECONDARY|WRF_NOBOB)
			WPX0 R 1 offset(0,85) A_WeaponReady(WRF_NOSECONDARY|WRF_NOBOB)
			WPX0 R 1 offset(0,94) A_WeaponReady(WRF_NOSECONDARY|WRF_NOBOB)
			WPX0 R 1 offset(0,103) A_WeaponReady(WRF_NOSECONDARY|WRF_NOBOB)
			WPX0 R 1 offset(0,112) A_WeaponReady(WRF_NOSECONDARY|WRF_NOBOB)
			WPX0 Q 0 offset(0,32)
			WPX0 QQQQQQQQQQQQQQQQQQQQ 1 A_WeaponReady(WRF_NOSECONDARY)
			goto Ready
	}
}

actor CentaurWeaponPowered : CentaurWeapon
{
	weapon.sisterweapon "CentaurWeapon"
	+WEAPON.POWERED_UP
	states
	{
		Fire:
			WPX0 K 0 A_JumpIf(args[0] <= 0, 6)
			WPX0 K 0 A_TakeInventory("PowerInvulnerable",1)
			WPX0 K 0 A_TakeInventory("CentaurDoingShield",1)
			WPX0 K 0 A_TakeInventory("CentaurShieldTimer",8)
			WPX0 K 0 A_SetArg(0,0)
			WPX0 K 0 SetPlayerProperty(0,0,0)
			WPX0 K 5 offset(0,32)
			WPX0 L 7 A_PlaySound("EttinAttack",CHAN_WEAPON)
			WPX0 P 8 A_FireCustomMissile("CentaurSwordThrown",0,0,0,9)
		FireDone:
			WPX0 P 1 offset(0,42)
			WPX0 P 1 offset(0,52)
			WPX0 P 1 offset(0,62)
			WPX0 P 1 offset(0,72)
			WPX0 P 1 offset(0,82)
			WPX0 P 1 offset(0,92)
			WPX0 P 1 offset(0,102)
			WPX0 P 8 offset(0,112)
			TNT1 A 0 offset(0,32) A_ReFire
			WPX0 Q 1 offset(0,57)
			WPX0 Q 1 offset(0,50)
			WPX0 Q 1 offset(0,44)
			WPX0 Q 1 offset(0,38)
			WPX0 Q 0 offset(0,32)
			goto Ready
	}
}

actor CentaurMeleeProj : EttinMeleeProj { obituary "%o was cut up by %k." }

actor CentaurSwordThrown
{
	obituary "%o was sliced in half by %k's thrown sword."
	speed 15
	fastspeed 25
	radius 10
	height 12
	renderstyle translucent
	deathsound "FighterHammerHitThing"
	damage (10 * random(1,8) * ACS_ExecuteWithResult(455,5) / 100)
	PROJECTILE
	+MTHRUSPECIES
	+EXTREMEDEATH
	+BLOODSPLATTER
	states
	{
		Spawn:
			CTDP K 2
			CTDP L 2 A_PlaySound("FighterHammerContinuous",CHAN_VOICE)
			CTDP UVWXYZ 2
			loop
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,45 * ACS_ExecuteWithResult(455,4) / 10)
			goto Hexen1
		Hexen1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Hexen4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Hexen2")
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",args[3])
			goto Death
		Hexen2:
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",args[3] * 2)
			goto Death
		Hexen4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Hexen8")
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",args[3] * 4)
			goto Death
		Hexen8:
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",args[3] * 8)
			goto Death
		Death:
			CTDP R 4 A_Gravity
			CTDP S 4
			CTDP T 150
			CTDP TTTTTTTTTTTTTTTTTTT 1 A_FadeOut(0.05)
			stop
	}
}

actor CentaurMorph : MonsterMorph
{
	tag "Centaur"
	inventory.icon "ARTICENT"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("CentaurHitboxCheck",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(450,0,2)
			stop
	}
}

actor CentaurMorphDirect : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,2)
			stop
	}
}

actor CentaurHitboxCheck : BaseHitboxCheck
{
	radius 24
	height 66
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_GiveToTarget("CentaurMorphDirect",1)
			stop
	}
}

actor CentaurMorpher : MonsterMorpher { powerup.type MorphCent }

actor PowerMorphCent : PowerMorphHexen { powermorph.playerclass "CentaurClass" }

actor CentaurMorpherHitbox : MonsterMorpher { powerup.type MorphCentHitbox }

actor PowerMorphCentHitbox : PowerMorphHexen { powermorph.playerclass "CentaurClassHitbox" }