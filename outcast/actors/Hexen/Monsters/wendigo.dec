
actor WendigoClass : MonsterPawnHexen
{
	player.morphweapon "WendigoWeapon"
	health 120
	mass 500
	player.maxhealth 120
	speed 0.6
	painchance 0
	player.soundclass "icey"
	bloodtype "IceChunk"
	states
	{
		Spawn:
			ICEY A 10
			loop
		See:
			ICEY ABCD 4
			ICEY A 0 A_Jump(128,"See")
			ICEY A 0 A_Jump(128,2)
			ICEY A 0 A_SpawnItemEx("IceGuyWisp01",random(-22,22),random(-22,22),60,velx,vely,velz,0,SXF_CLIENTSIDE|SXF_ABSOLUTEVELOCITY)
			loop
			ICEY A 0 A_SpawnItemEx("IceGuyWisp02",random(-22,22),random(-22,22),60,velx,vely,velz,0,SXF_CLIENTSIDE|SXF_ABSOLUTEVELOCITY)
			loop
		Melee:
			ICEY F 4
			ICEY G 8 bright
			goto Missile+1
		Missile:
			ICEY EFF 4
			goto Spawn
		Death:
			ICEC A 1 A_PlayerScream
			ICEC AAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("IceChunkClient",random(-22,22),random(-22,22),random(0,64),frandom(-2.0,2.0),frandom(-2.0,2.0),frandom(2.25,3.25),0,SXF_CLIENTSIDE|SXF_ABSOLUTEVELOCITY|SXF_NOCHECKPOSITION)
			ICEC A 1 A_SpawnItemEx("IceChunkClient",random(-22,22),random(-22,22),random(0,64),frandom(-2.0,2.0),frandom(-2.0,2.0),frandom(2.25,3.25),0,40)
			ICEC AAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("IceChunkClient",random(-22,22),random(-22,22),random(0,64),frandom(-2.0,2.0),frandom(-2.0,2.0),frandom(2.25,3.25),0,SXF_CLIENTSIDE|SXF_ABSOLUTEVELOCITY|SXF_NOCHECKPOSITION)
			ICEC A 1 A_SpawnItemEx("IceChunkClient",random(-22,22),random(-22,22),random(0,64),frandom(-2.0,2.0),frandom(-2.0,2.0),frandom(2.25,3.25),0,40)
			ICEC AAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("IceChunkClient",random(-22,22),random(-22,22),random(0,64),frandom(-2.0,2.0),frandom(-2.0,2.0),frandom(2.25,3.25),0,SXF_CLIENTSIDE|SXF_ABSOLUTEVELOCITY|SXF_NOCHECKPOSITION)
			ICEC A -1 A_Fall
			stop
	}
}

actor WendigoClassHitbox : WendigoClass
{
	height 68
	radius 28
	player.viewheight 60
	player.attackzoffset 12
}

actor IceGuyWisp01
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	+NOBLOCKMAP
	renderstyle translucent
	alpha 0.4
	states
	{
		Spawn:
			ICWS ABCDEFGHI 2
			stop
	}
}

actor IceGuyWisp02 : IceGuyWisp01
{
	states
	{
		Spawn:
			ICWS JKLMNOPQR 2
			stop
	}
}

actor IceChunkClient : SerpentGib1
{
	+CLIENTSIDEONLY
	-NOGRAVITY
	+CANNOTPUSH
	+NOTELEPORT
	gravity 0.125
	states
	{
		Spawn:
			ICEC A 0
			ICEC A 1 A_SetArg(0,7)
			ICEC A 2 A_CheckFloor("Fade")
			wait
		Fade:
			ICEC A 4 A_Jump(random(16,32),1)
			ICEC A 4 A_SetArg(0,args[0] - 1)
			ICEC B 4 A_JumpIf(args[0] > 0,"Fade")
			ICEC B 4 A_SetArg(0,8)
		Fade2:
			ICEC B 4 A_Jump(random(16,32),1)
			ICEC B 4 A_SetArg(0,args[0] - 1)
			ICEC C 4 A_JumpIf(args[0] > 0,"Fade2")
			ICEC C 4 A_SetArg(0,8)
		Fade3:
			ICEC C 4 A_Jump(random(16,32),1)
			ICEC C 4 A_SetArg(0,args[0] - 1)
			ICEC D 4 A_JumpIf(args[0] > 0,"Fade3")
			ICEC D 4 A_SetArg(0,8)
		Fade4:
			ICEC D 4 A_Jump(random(16,32),1)
			ICEC D 4 A_SetArg(0,args[0] - 1)
			ICEC D 4 A_JumpIf(args[0] > 0,"Fade4")
			ICEC DDDDDD 4 A_SinkGib
			stop
	}
}

actor WendigoWeapon : BaseMonsterWeapon
{
	obituary "%o was frozen solid by %k."
	tag "Frosty Hands"
	weapon.sisterweapon "WendigoWeaponPowered"
	weapon.ammotype1 "MonsterManaBlue"
	states
	{
		Ready:
			WPX1 D 1 A_WeaponReady
			wait
		Select:
			WPX1 D 1 A_Raise
			wait
		Deselect:
			WPX1 D 1 A_Lower
			wait
		Fire:
			WPX1 E 4
		Hold:
			WPX1 F 4 A_GunFlash
			WPX1 G 0 A_PlaySound("IceGuyAttack",CHAN_WEAPON)
			WPX1 G 0 A_TakeInventory("MonsterManaBlue",2)
			WPX1 G 0 A_FireCustomMissile("IceGuyFX0",0,0,11,9)
			WPX1 G 8 bright A_FireCustomMissile("IceGuyFX0",0,0,-11,9)
			WPX1 F 4
			WPX1 F 0 A_JumpIfInventory("MonsterManaBlue",2,1)
			goto Delay
			WPX1 F 0 A_ReFire
			WPX1 FED 4
			goto Ready
		Delay:
			WPX1 FED 4
			WPX1 DD 8 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			goto Ready
	}
}

actor WendigoWeaponPowered : WendigoWeapon
{
	weapon.sisterweapon "WendigoWeapon"
	+WEAPON.POWERED_UP
	states
	{
		Fire:
			WPX1 E 8
			WPX1 F 4 A_GunFlash
			WPX1 G 8 bright A_FireCustomMissile("IceGuyFX40")
			WPX1 F 8 A_TakeInventory("MonsterManaBlue",10)
			WPX1 E 8
			WPX1 D 16
			WPX1 D 4 A_JumpIfInventory("MonsterManaBlue",10,"Ready")
			WPX1 DDDDDDDDDD 10 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
			goto Ready
	}
}

actor IceGuyFX0 : IceGuyFX
{
	fastspeed 22
	obituary "%o was frozen solid by %k."
	damagetype "normal"
	damage (1)
	+MTHRUSPECIES
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	states
	{
		Spawn:
			ICPR ABC 3 bright A_SpawnItemEx("IceFXPuffClient",0,0,2,0,0,0,0,SXF_CLIENTSIDE)
			loop
		Death:
    		ICPR D 4 bright
			ICPR E 0 A_CustomMissile("IceGuyFX20",0,0,-45,CMF_AIMDIRECTION|CMF_TRACKOWNER)
			ICPR E 0 A_CustomMissile("IceGuyFX20",0,0,-90,CMF_AIMDIRECTION|CMF_TRACKOWNER)
			ICPR E 0 A_CustomMissile("IceGuyFX20",0,0,-135,CMF_AIMDIRECTION|CMF_TRACKOWNER)
			ICPR E 0 A_CustomMissile("IceGuyFX20",0,0,180,CMF_AIMDIRECTION|CMF_TRACKOWNER)
			ICPR E 0 A_CustomMissile("IceGuyFX20",0,0,135,CMF_AIMDIRECTION|CMF_TRACKOWNER)
			ICPR E 0 A_CustomMissile("IceGuyFX20",0,0,90,CMF_AIMDIRECTION|CMF_TRACKOWNER)
			ICPR E 0 A_CustomMissile("IceGuyFX20",0,0,45,CMF_AIMDIRECTION|CMF_TRACKOWNER)
    		ICPR E 4 bright A_CustomMissile("IceGuyFX20",0,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER)
    		ICPR FG 4 bright
    		ICPR H 3 bright
    		Stop
	}
}

actor IceFXPuffClient : IceFXPuff
{
	alpha 0.4
	renderstyle translucent
	+CLIENTSIDEONLY
	+NOINTERACTION
	+NOBLOCKMAP
	states
	{
		Spawn:
			ICPR IJK 3
			ICPR LM 2
			stop
	}
}

actor IceGuyFX20 : IceGuyFX2
{
	obituary "%o was frozen solid by %k."
	damage (3 * random(1,4) * ACS_ExecuteWithResult(455,5) / 100)
	-STRIFEDAMAGE
	+MTHRUSPECIES
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	states
	{
		Spawn:
			ICPR N 0
			ICPR N 0 A_ChangeVelocity(0,0,-0.3)
			ICPR NOP 3 bright
			goto Spawn+2
		Crash:
		XDeath:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Hexen4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Hexen2")
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",75 * ACS_ExecuteWithResult(455,4) / 100)
			goto Death
		Hexen2:
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",ACS_ExecuteWithResult(455,4))
			goto Death
		Hexen4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Hexen8")
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",2 * ACS_ExecuteWithResult(455,4))
			goto Death
		Hexen8:
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",4 * ACS_ExecuteWithResult(455,4))
			goto Death
		Death:
			ICPR N 1 bright
			stop
	}
}

actor IceGuyFX30
{
	obituary "%o was frozen solid by %k."
	damage (7 * random(1,8) * ACS_ExecuteWithResult(455,5) / 100)
	speed 0
	radius 12
	height 32
	scale 0.65
	PROJECTILE
	+FLOORHUGGER
	+MTHRUSPECIES
	+DONTREFLECT
	+FOILINVUL
	states
	{
		Spawn:
			ICEY M 1
			ICEY LKJ 2
			ICEY I 12
			ICEY JKL 2
			ICEY M 1
			stop
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,315 * ACS_ExecuteWithResult(455,4) / 100)
			goto Hexen1
		Hexen1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Hexen4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Hexen2")
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",args[3])
			goto Death
		Hexen2:
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",args[3] * 2)
			goto Death
		Hexen4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Hexen8")
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",args[3] * 4)
			goto Death
		Hexen8:
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",args[3] * 8)
			goto Death
		Death:
			ICEC AAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("IceChunkClient",random(-8,8),random(-8,8),random(0,32),frandom(-2.0,2.0),frandom(-2.0,2.0),frandom(2.25,3.25),0,SXF_CLIENTSIDE|SXF_ABSOLUTEVELOCITY|SXF_NOCHECKPOSITION)
			ICEC A 1 A_PlaySound("misc/icebreak",CHAN_BODY,0.75)
			stop
	}
}

actor IceGuyFX40
{
	radius 8
	height 1
	speed 10
	seesound ""
	deathsound ""
	reactiontime 40
	PROJECTILE
	+FLOORHUGGER
	+DONTREFLECT
	+THRUACTORS
	states
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_PlaySound("IceGuyIceMove",CHAN_VOICE,1.0,1)
			TNT1 A 0 A_Countdown
			TNT1 A 3 A_CustomMissile("IceGuyFX30",0,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER)
			goto Spawn+2
		XDeath:
		Crash:
		Death:
			TNT1 A 1 A_PlaySound("none",CHAN_VOICE)
			stop
	}
}

actor WendigoMorph : MonsterMorph
{
	tag "Wendigo"
	inventory.icon "ARTIICEY"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("WendigoHitboxCheck",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(450,0,7)
			stop
	}
}

actor WendigoMorphDirect : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,7)
			stop
	}
}

actor WendigoHitboxCheck : BaseHitboxCheck
{
	height 68
	radius 28
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_GiveToTarget("WendigoMorphDirect",1)
			stop
	}
}

actor WendigoMorpher : MonsterMorpher { powerup.type MorphIcey }

actor PowerMorphIcey : PowerMorphHexen { powermorph.playerclass "WendigoClass" }

actor WendigoMorpherHitbox : MonsterMorpher { powerup.type MorphIceyHitbox }

actor PowerMorphIceyHitbox : PowerMorphHexen { powermorph.playerclass "WendigoClassHitbox" }