
actor MenelkirClass : MonsterPawnHexen
{
	player.morphweapon "MenelkirWeapon"
	health 800
	player.maxhealth 800
	gibhealth 400
	speed 0.4
	mass 250
	player.soundclass "mageb"
	player.sidemove 0.5, 0.5
	states
	{
		Spawn:
			MAGE A -1
			loop
		See:
			MAGE ABCD 4
			loop
		Pain:
			MAGE G 8 A_PlaySound("PlayerMagePain",2)
			goto Spawn
		Missile:
			MAGE A 4
			goto Spawn
		Melee:
			MAGE E 8
			MAGE F 8 bright
			goto Spawn
		Death:
    		MAGE H 6
    		MAGE I 6 A_PlayerScream
    		MAGE JK 6
    		MAGE L 6 A_NoBlocking
    		MAGE M 6
			MAGE N -1
    		stop
		XDeath:
    		MAGE O 5
    		MAGE P 5 A_PlaySound("PlayerMageExtremeDeathPicker",2)
    		MAGE R 5 A_NoBlocking
    		MAGE STUVW 5
    		MAGE W -1
    		stop
		 Burn:
			FDTH E 1 bright
    		FDTH E 4 bright A_PlaySound("PlayerMageBurnDeath",1)
   	 		FDTH F 4 bright
    		FDTH G 5 bright A_PlaySound("PlayerMageCrazyDeath",2)
    		FDTH H 4 bright
    		FDTH I 5 bright
    		FDTH J 4 bright
    		FDTH K 5 bright
    		FDTH L 4 bright
    		FDTH M 5 bright
   		 	FDTH N 4 bright
    		FDTH O 5 bright
    		FDTH P 4 bright
    		FDTH Q 5 bright
    		FDTH R 4 bright
    		FDTH S 5 bright A_NoBlocking
			FDTH T 4 bright
    		FDTH U 5 bright
    		FDTH V 4 bright
			FDMN T -1
    		stop
	}
}

actor MenelkirClassHitbox : MenelkirClass
{
	height 64
	radius 22
	player.viewheight 52
}

actor MenelkirWeapon : BaseMonsterWeapon
{
	obituary "%o couldn't absorb %k's mana."
	tag "Bloodscourge"
	weapon.sisterweapon "MenelkirWeaponPowered"
	weapon.ammotype1 "MonsterManaBlue"
	weapon.ammotype2 "MonsterManaGreen"
	+WEAPON.NOALERT
	states
	{
  		Select:
    		WPX4 A 1 A_Raise
    		loop
  		Deselect:
    		WPX4 A 1 A_Lower
    		loop
  		Ready:
    		WPX4 AAAAAABBBBBBCCCCCCDDDDDDEEEEEEFFFFF 1 A_WeaponReady
    		loop
  		Fire:
			WPX4 G 0 A_GunFlash
   			WPX4 G 4 Offset(0, 40) A_AlertMonsters
			WPX4 H 0 A_PlaySound("MageStaffFire",CHAN_WEAPON)
    		WPX4 H 3 bright Offset(0, 48) ACS_ExecuteAlways(451,0,5,1)
			WPX4 H 0 A_FireCustomMissile("MageStaffFX20",-5,0,0,17)
			WPX4 H 0 A_FireCustomMissile("MageStaffFX20",5,0,0,17)
    		WPX4 H 3 bright Offset(0, 48) A_FireCustomMissile("MageStaffFX20",0,0,0,17)
    		WPX4 I 4 Offset(0, 48) A_TakeInventory("MonsterManaBlue",4)
    		WPX4 I 2 Offset(0, 40) A_TakeInventory("MonsterManaGreen",4)
    		WPX4 J 4 Offset(0, 36)
			WPX4 J 3 Offset (0, 32)
			WPX4 AABB 2
			WPX4 A 0 A_JumpIfInventory("MonsterManaBlue",4,1)
			goto Delay
			WPX4 A 0 A_JumpIfInventory("MonsterManaGreen",4,1)
			goto Delay
			WPX4 DDEE 2 A_WeaponReady(WRF_NOPRIMARY)
			goto Ready
		Delay:
			WPX4 CDEFABCDEFABCD 5 A_WeaponReady(WRF_NOFIRE)
			WPX4 EEFF 2 A_WeaponReady(WRF_NOPRIMARY)
			goto Ready
  		AltFire:
			WPX4 A 0 A_Stop
			WPX4 A 0 SetPlayerProperty(0,1,0)
			WPX4 A 0 A_Jump(128,"Alt2")
  			WPX4 AA 0 ThrustThing((angle*256/360)+64, 3, 0, 0)
			WPX4 AAA 1 ThrustThing((angle*256/360)+64, 2, 0, 0)
			WPX4 AAA 1 ThrustThing((angle*256/360)+64, 1, 0, 0)
			WPX4 B 2
			//WPX4 B 0 A_Stop
			WPX4 B 2 SetPlayerProperty(0,0,0)
			WPX4 BCCCDDDEEEFF 2 A_WeaponReady(WRF_NOSECONDARY)
			goto Ready
		Alt2:
  			WPX4 AA 0 ThrustThing((angle*256/360)+192, 3, 0, 0)
			WPX4 AAA 1 ThrustThing((angle*256/360)+192, 2, 0, 0)
			WPX4 AAA 1 ThrustThing((angle*256/360)+192, 1, 0, 0)
			goto AltFire+11
	}
}

actor MenelkirWeaponPowered : MenelkirWeapon
{
	weapon.sisterweapon "MenelkirWeapon"
	+WEAPON.POWERED_UP
	states
	{
		Fire:
			WPX4 G 0 A_GunFlash
   			WPX4 G 4 Offset(0, 40) A_AlertMonsters
			WPX4 H 0 A_PlaySound("MageStaffFire",CHAN_WEAPON)
    		WPX4 H 3 bright Offset(0, 48) ACS_ExecuteAlways(451,0,5,1)
			WPX4 H 0 A_FireCustomMissile("MageStaffFX21",-5,0,0,17)
			WPX4 H 0 A_FireCustomMissile("MageStaffFX21",5,0,0,17)
    		WPX4 H 1 bright Offset(0, 48) A_FireCustomMissile("MageStaffFX21",0,0,0,17)
			WPX4 H 0 A_FireCustomMissile("MageStaffFX21",-9,0,0,17)
			WPX4 H 0 A_FireCustomMissile("MageStaffFX21",1,0,0,17)
    		WPX4 H 1 bright Offset(0, 48) A_FireCustomMissile("MageStaffFX21",-4,0,0,17)
			WPX4 H 0 A_FireCustomMissile("MageStaffFX21",-1,0,0,17)
			WPX4 H 0 A_FireCustomMissile("MageStaffFX21",9,0,0,17)
    		WPX4 H 1 bright Offset(0, 48) A_FireCustomMissile("MageStaffFX21",4,0,0,17)
    		WPX4 I 4 Offset(0, 48) A_TakeInventory("MonsterManaBlue",10)
    		WPX4 I 2 Offset(0, 40) A_TakeInventory("MonsterManaGreen",10)
    		WPX4 J 4 Offset(0, 36)
			WPX4 J 3 Offset (0, 32)
			WPX4 ABCDE 5 A_WeaponReady(WRF_NOFIRE)
			WPX4 A 0 A_JumpIfInventory("MonsterManaBlue",10,1)
			goto Delay
			WPX4 A 0 A_JumpIfInventory("MonsterManaGreen",10,1)
			goto Delay
			WPX4 EFFF 2 A_WeaponReady(WRF_NOPRIMARY)
			goto Ready
		Delay:
			WPX4 FABCDEFABCDABCDEFABCDEFAB 5 A_WeaponReady(WRF_NOFIRE)
			WPX4 EEFF 2 A_WeaponReady(WRF_NOPRIMARY)
			goto Ready
	}
}

actor MageStaffFX20
{
	PROJECTILE
	+EXTREMEDEATH
	+SEEKERMISSILE
	+SCREENSEEKER
	+MTHRUSPECIES
	reactiontime 10
	speed 17
	height 12
	radius 8
	damage (2 * random(1,8) * ACS_ExecuteWithResult(455,5) / 100)
	obituary "%o couldn't absorb %k's mana."
	DeathSound "MageStaffExplode"
	states
	{
		Spawn:
			MSP2 A 0 A_Countdown
			MSP2 ABCD 3 bright A_SeekerMissile(2,10,3,50,10)
			loop
		Crash:
		XDeath:
			TNT1 A 0 A_SetArg(3,4 * ACS_ExecuteWithResult(455,4))
			goto Hexen1
		Hexen1:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,3),2)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuadDamage",1,"Hexen4")
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Hexen2")
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",args[3])
			goto Death
		Hexen2:
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",args[3] * 2)
			goto Death
		Hexen4:
			TNT1 A 0 A_JumpIf(!ACS_ExecuteWithResult(455,2),2)
			TNT1 A 0 A_JumpIfInTargetInventory("RuneDoubleDamage",1,"Hexen8")
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",args[3] * 4)
			goto Death
		Hexen8:
			TNT1 A 0 A_GiveToTarget("HexenDamagePoint",args[3] * 8)
			goto Death
		Death:
			MSP1 G 2 bright
			MSP1 H 2 bright A_Explode(random(26,37) * ACS_ExecuteWithResult(455,5) / 100,128,0)
			MSP1 IJKLMNOP 2 bright
			stop
	}
}

actor MageStaffFX21 : MageStaffFX20
{
	+DONTREFLECT
	+FOILINVUL
	deathsound "MageStaffExplodeQuiet"
	reactiontime 6
	states
	{
		Spawn:
			MSP3 A 0 A_Countdown
			MSP3 ABCDEFG 3 bright A_SeekerMissile(2,10,3,50,10)
			loop
	}
}

actor MenelkirMorph : MonsterMorph
{
	tag "Menelkir, Arch-Mage of the Arcanum"
	inventory.icon "ARTIMAGE"
	states
	{
		Use:
			TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(455,7) > 0,"Check")
			goto DoMorph
		Check:
			TNT1 A 0 A_SpawnItemEx("MenelkirHitboxCheck",0,0,0,cos(angle),sin(angle),1)
			fail
		DoMorph:
			TNT1 A 0 ACS_ExecuteAlways(450,0,9,0,3)
			stop
	}
}

actor MenelkirMorphDirect : MonsterMorphDirect
{
	states
	{
		Pickup:
			TNT1 A 0 ACS_ExecuteAlways(450,0,9,0,3)
			stop
	}
}

actor MenelkirHitboxCheck : BaseHitboxCheck
{
	height 64
	radius 22
	states
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 A_TakeFromTarget("MenelkirMorph",1)
			TNT1 A 0 A_GiveToTarget("MenelkirMorphDirect",1)
			stop
	}
}

actor MenelkirMorpher : MonsterMorpher { powerup.type MorphMage }

actor PowerMorphMage : PowerMorphHexen { powermorph.playerclass "MenelkirClass" }

actor MenelkirMorpherHitbox : MonsterMorpher { powerup.type MorphMageHitbox }

actor PowerMorphMageHitbox : PowerMorphHexen { powermorph.playerclass "MenelkirClassHitbox" }