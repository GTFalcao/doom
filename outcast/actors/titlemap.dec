
actor IntroStaticBaron 9251
{
	+FLOORHUGGER
	+NOBLOCKMAP
	states
	{
		Spawn:
			BOSS AB 10
			loop
		Move:
			BOSS ABCD 6
			loop
	}
}

actor IntroStaticWeredragon : IntroStaticBaron 9252
{
	states
	{
		Spawn:
			BEAS AB 10
			loop
		Move:
			BEAS ABCDEF 3
			loop
	}
}

actor IntroStaticWendigo : IntroStaticBaron 9253
{
	states
	{
		Spawn:
			ICEY A 10
			loop
		Move:
			ICEY ABCD 4
			ICEY A 0 A_Jump(128,"Move")
			ICEY A 0 A_Jump(128,2)
			ICEY A 0 A_SpawnItemEx("IceGuyWisp01",random(-22,22),random(-22,22),60,velx,vely,velz,0,SXF_CLIENTSIDE|SXF_ABSOLUTEVELOCITY)
			loop
			ICEY A 0 A_SpawnItemEx("IceGuyWisp02",random(-22,22),random(-22,22),60,velx,vely,velz,0,SXF_CLIENTSIDE|SXF_ABSOLUTEVELOCITY)
			loop
	}
}

actor IntroStaticCrusader : IntroStaticBaron 9254
{
	states
	{
		Spawn:
			ROB2 Q 10
			loop
		Move:
			ROB2 ABCD 6
			loop
	}
}

actor IntroInvisChaser 9255
{
	+INVISIBLE
	+NOTARGET
	+FRIENDLY
	+ISMONSTER
	+NONSHOOTABLE
	+LOOKALLAROUND
	speed 2
	states
	{
		Spawn:
			BOSS A 4
			BOSS A 8 A_Look
			wait
		Idle:
			BOSS AAAAAA 1 A_Wander
			BOSS A 0 A_Look
			BOSS AAAAAA 1 A_Wander
			loop
		See:
			BOSS AAAAAAAA 1 A_Chase
			loop
	}
}

actor IntroBossBaron : BaronOfHell
{
	species "IntroBoss"
	health 10000
	+LOOKALLAROUND
	+CANTLEAVEFLOORPIC
	+MISSILEMORE
	+MISSILEEVENMORE
	states
	{
		Idle:
			BOSS A 0 A_Look
			BOSS AABB 3 A_Wander
			BOSS C 0 A_Look
			BOSS CCDD 3 A_Wander
			loop
		Melee:
		Missile:
			BOSS EF 6 A_FaceTarget
			BOSS G 6 A_BruisAttack
			goto See
		Raise:
			stop
	}
}

actor IntroBossWeredragon : Beast
{
	species "IntroBoss"
	health 10000
	painchance 50
	mass 1000
	dropitem ""
	+LOOKALLAROUND
	+CANTLEAVEFLOORPIC
	states
	{
		Idle:
			BEAS A 0 A_Look
			BEAS ABC 3 A_Wander
			BEAS D 0 A_Look
			BEAS DEF 3 A_Wander
			loop
		Melee:
		Missile:
			BEAS H 10 A_FaceTarget
			BEAS I 10 A_CustomComboAttack("BeastBall0", 32, random[BeastAttack](1,8)*11, "beast/attack")
			goto See
	}
}

actor IntroBossWendigo : IceGuy
{
	species "IntroBoss"
	painchance 0
	mass 1000
	health 10000
	+LOOKALLAROUND
	+CANTLEAVEFLOORPIC
	+MISSILEMORE
	+MISSILEEVENMORE
	states
	{
		Idle:
			ICEY A 0 A_Look
			ICEY AB 4 A_Wander
			ICEY C 0 A_Look
			ICEY CD 4 A_Wander
			loop
		Melee:
		Missile:
			ICEY EF 3 A_FaceTarget
			ICEY G 8 bright A_IceGuyAttack
			ICEY F 3 A_FaceTarget
			goto See
	}
}

actor IntroBossCrusader : Crusader
{
	species "IntroBoss"
	painchance 64
	mass 1000
	health 10000
	dropitem ""
	-INCOMBAT
	+LOOKALLAROUND
	+CANTLEAVEFLOORPIC
	+MISSILEEVENMORE
	states
	{
		Idle:
			ROB2 A 0 A_Look
			ROB2 AABB 3 A_Wander
			ROB2 C 0 A_Look
			ROB2 CCDD 3 A_Wander
			loop
	}
}

actor IntroBossDSparil : Sorcerer1
{
	species "IntroBoss"
	health 3500
	mass 2000
	painchance 28
	+LOOKALLAROUND
	+MISSILEMORE
	+MISSILEEVENMORE
	+NOBLOCKMONST
	-NOTARGET
	states
	{
		Idle:
			SRCR A 0 A_Look
			SRCR AB 5 A_Wander
			SRCR C 0 A_Look
			SRCR CD 5 A_Wander
			loop
		Melee:
		Missile:
			SRCR Q 6 A_FaceTarget
			SRCR R 5 A_FaceTarget
			SRCR S 8 A_Srcr1Attack
			Goto See
		Missile2:
			SRCR S 7 A_FaceTarget
			SRCR Q 6 A_FaceTarget
			SRCR R 5 A_FaceTarget
			SRCR S 8 A_Srcr1Attack
			Goto See
		Death:
			SRCR E 7
			SRCR F 7 A_Scream
			SRCR G 7
			SRCR HIJK 6
			SRCR L 25 A_PlaySound("dsparil/zap", CHAN_BODY, 1, FALSE, ATTN_NONE)
			SRCR MN 5
			SRCR O 4
			SRCR L 20 A_PlaySound("dsparil/zap", CHAN_BODY, 1, FALSE, ATTN_NONE)
			SRCR MN 5
			SRCR O 4
			SRCR L 12
			SRCR P 0 A_Fall
			SRCR P -1 A_SpawnItemEx("IntroBossSorcerer2",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
			stop
	}
}

actor IntroBossSorcerer2 : Sorcerer2
{
	species "IntroBoss"
	attacksound ""
	health 7000
	mass 1000
	painchance 16
	meleerange 64
	speed 12
	-NOTARGET
	+QUICKTORETALIATE
	+LOOKALLAROUND
	+NOBLOCKMONST
	+MISSILEMORE
	+MISSILEEVENMORE
	states
	{
		Spawn:
			SOR2 A 0
			SOR2 A 0 Thing_ChangeTID(0,85)
			SOR2 A 0 Thing_Hate(85,86,2)
		Rise:
			SOR2 A 4 A_PlaySound("dsparil/rise",CHAN_VOICE,1.0,0,ATTN_NONE)
			SOR2 BCDEF 4
			SOR2 G 12 A_PlaySound("dsparil/sight",CHAN_VOICE,1.0,0,ATTN_NONE)
			Goto See
		Idle:
			SOR2 M 0 A_Look
			SOR2 MNOP 4 A_Wander
			loop
		Melee:
		Missile:
			SOR2 R 9 A_FaceTarget
			SOR2 S 0 A_PlaySound("dsparil/attack",CHAN_WEAPON,0.7,0,ATTN_NONE)
			SOR2 S 9 bright A_FaceTarget
			SOR2 T 20 bright A_CustomMissile("Sorcerer2FX10",36,0,0)
			Goto See
		Death:
    		SDTH A 8 bright
			SDTH B 8 bright A_PlaySound("dsparil/scream", CHAN_BODY, 1, 0, ATTN_NONE)
			SDTH C 8 bright A_Sor2DthInit
    		SDTH DEFDEFDEFDEFDEFDEFDEF 7 bright
    		SDTH G 6 bright A_PlaySound("dsparil/explode", CHAN_BODY, 1, 0, ATTN_NONE)
    		SDTH H 6 bright
    		SDTH I 18 bright
    		SDTH J 6 bright A_NoBlocking
    		SDTH K 6 bright A_PlaySound("dsparil/bones", CHAN_BODY, 1, 0, ATTN_NONE)
    		SDTH LMN 6 bright
    		SDTH O -1 bright
    		Stop
	}
}

actor IntroDoomImp : DoomImp { +FRIENDLY +NOICEDEATH }

actor IntroDemon : Demon { +FRIENDLY +NOICEDEATH }

actor IntroLostSoul : LostSoul { +FRIENDLY +NOICEDEATH }

actor IntroSpectre : Spectre { +FRIENDLY +NOICEDEATH }

actor IntroCacodemon : Cacodemon { +FRIENDLY +NOICEDEATH }

actor IntroHellknight : Hellknight { +FRIENDLY +NOICEDEATH }

actor IntroBaronOfHell : BaronOfHell { +FRIENDLY +NOICEDEATH }

actor IntroRevenant : Revenant { +FRIENDLY +NOICEDEATH }

actor IntroFatso : Fatso { +FRIENDLY +NOICEDEATH }

actor IntroArachnotron : Arachnotron { +FRIENDLY +NOICEDEATH }

actor IntroArchvile : Archvile { +FRIENDLY +NOICEDEATH }

actor IntroCyberdemon : Cyberdemon { +FRIENDLY +NOICEDEATH }

actor IntroSpiderMastermind : SpiderMastermind { +FRIENDLY +NOICEDEATH +QUICKTORETALIATE painchance 20 health 6000 mass 5000 }